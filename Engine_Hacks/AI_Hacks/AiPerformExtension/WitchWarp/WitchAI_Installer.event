/*
	- Both AIs use a RNG call to determine whether to use the special skill or behave normally. 25% for starters?
	- Prioritize attacking in range before attempting to warp
	- Prioritize warping to isolated targets
	- no more than 3 warps per turn
	- rank target units by hp+res
	- Needs to be in the Primary AI table for both.
	- When you tell a unit to have an AI of [0x0, 0x3], that means it should use the primary AI that’s 0x0 in the first table, and if that fails, use the AI in the second table pointed to by the 0x3-rd pointer. In this case, that means if there’s something to attack in range, it’ll attack that. Otherwise it’ll fail and fall through to AI2 0x3, which always fails. When both AIs fail, the unit does nothing. Thus, the observed behavior is AttackInRange.
*/

#include "../AiScrDefinitions.event"

InjectAi1(0x13, WitchAIScript)
InjectAiPerformer(0x0E, InitWarping|1)
SetUnitAction(0x27, ActionRoutine(DoTheWarping|1))

WitchAIScript:
	AiFunc(CheckIfAttackPossible|1)
	AiNop
	AiGotoIfEQ(1, 0) //if can't attack, warp
	AiAttack
	AiRestart
	AiLabel(1)
	AiFuncPercentChance(CheckForWitchWarp|1, 50)
	AiGotoIfEQ(2, 0) //if warped, change AI
	AiChangeAi(0x00, 0x12)
	AiLabel(2)
	AiRestart

ALIGN 4
#include "WitchAI.lyn.event"

//CheckForWitchWarp will find a target and mark actionID 0xE if one is found.
//InitWarping will then set the actionID that UnitActionRework uses and any parameters (position etc)
//DoTheWarping will do the actual animation and place the unit in its new location
//9a3bdc calls the warp gfx

PUSH

	ORG $3B41C //fix OOB tile check
	replaceWithHack(AiAreEnemiesWithin3Tiles_New)

POP


MapAnimProcWitchWarp:
	//PROC_YIELD
	PROC_CALL_ROUTINE($15361) //AddSkipThread
	PROC_CALL_ROUTINE(StartWitchWarpAnim+1)
	PROC_SLEEP($0A)
	
	PROC_CALL_ROUTINE(MapAnim_SubjectUnitFlashOut+1)
	PROC_SLEEP($14)
	
	//PROC_CALL_ROUTINE(MapAnim_SubjectStarExplosion+1)
	//PROC_SLEEP($02)
	
	PROC_CALL_ROUTINE(MapAnim_SubjectHide+1)
	PROC_SLEEP($08)
	
	PROC_CALL_ROUTINE(Witchwarp_Move+1)
	PROC_SLEEP($1E)
	
	PROC_CALL_ROUTINE(Witchwarp_Camera+1)	
	PROC_SLEEP($02)
	
	//PROC_CALL_ROUTINE(MapAnim_SubjectStarImplosion+1)	
	//PROC_SLEEP($28)
	
	PROC_CALL_ROUTINE(MapAnim_WarpSubjectWarpRing+1)	
	PROC_SLEEP($0A)
	
	PROC_CALL_ROUTINE(MapAnim_SubjectShow+1)
	PROC_CALL_ROUTINE(MapAnim_SubjectUnitFlashIn+1)	
	PROC_SLEEP($10)
	
	PROC_SLEEP($0A)
	PROC_JUMP($9A35B0) //Jump to MapAnimEnd
	
	PROC_END
