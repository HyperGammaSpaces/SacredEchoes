//Made by CrazyColorz5 of Fire Emblem Universe
//Based off of Camtech75's Skeleton
#include EAstdlib.event

EventPointerTable(plCh4_3A_Event, Dummy_ThisChapter)

Dummy_ThisChapter:
	POIN Dummy_TurnBasedEvents
	POIN Dummy_CharacterBasedEvents
	POIN Dummy_LocationBasedEvents
	POIN Dummy_MiscBasedEvents
	POIN Dummy_Dunno Dummy_Dunno Dummy_Dunno
	POIN Dummy_Tutorial
	POIN Dummy_Ballista1 Dummy_Ballista2
	POIN PlayerPlacement PlayerPlacement
	POIN $0 $0 $0 
	POIN EnemyUnits EnemyUnits EnemyUnits
	POIN Dummy_BeginningScene Dummy_EndingScene

Dummy_TurnBasedEvents:
	OpeningTurnEvent(BaronsEvent)
	TURN 0x0 LoadSummons [3,255] 0x0
	TURN 0x0 LoadReinforcementBandits [4,6] 0x0
	TURN
	
BaronsEvent:
	CHECK_ALIVE Forsyth
	BEQ 0x103 0xC 0x0
	CHECK_DEPLOYED Forsyth
	BEQ 0x103 0xC 0x0
		MUSC SongID_WhatLiesAtTheEnd
		MUSI
		CAMERA_CENTERED MarlaCh4
		STAL 30
		CameraCursor(Forsyth)
		Text(Ch4_3A_Barons)
		MUNO
	LABEL 0x103
	NoFade
	ENDA
	
LoadSummons:
	CHECK_EVENTID 0x1D
	BNE 2 0xC 0x0
		CameraCursor(MarlaCh4)
		MapText(Ch4_3A_MarlaSummons)
		ENUT 0x1D
		GOTO 3
	LABEL 2
	CHECK_EVENTID 0x2 // boss death quote
	BNE 0 0xC 0x0 // branch label 0 if summoner dead
	COUNTER_CHECK 0 // slot c = counter 0 value
	SVAL 0x1 1 // number of turns between summons (1)
	BNE 1 0xC 0x1 // branch label 1 if counter is not at value

	// Counter at value
	COUNTER_SET 0 0 // reset counter
	CAMERA_CENTERED MarlaCh4 // cam on summoner
	ENUN
	LABEL 3
	STAL 20
	WARPIN [12, 1]
    LOAD1 0 WitchSummon1
    ENDWARP
    ENUN
    STAL 20
    WARPIN [16, 1]
    LOAD1 0 WitchSummon2
    ENDWARP
    ENUN
	EVBIT_T 0x7
	GOTO 0

	LABEL 1
	// Counter not at value
	COUNTER_ADD 0 0 // counter 0 ++

	LABEL 0
	// event end
	NoFade
	ENDA
	
LoadReinforcementBandits:
	ReinforcementEvent(BrigandsReinforcements)
	NoFade
	ENDA

Dummy_CharacterBasedEvents:
	CHAR

Dummy_LocationBasedEvents:
	#ifdef __DEBUG__
	  Seize(2,15)
	#endif
	Seize(14,2)
	Village(0xA, VillageEvent, 26,9)
	LOCA
	
VillageEvent:
	VillageEventItem(Ch4_3A_VillageText,0x05,Saunion)
	ENDA

Dummy_MiscBasedEvents:
	CauseGameOverIfLordDies
	MournQuoteAFEVS
	AFEV

Dummy_Dunno:
	END_MAIN

Dummy_Tutorial:
	END_MAIN

Dummy_Ballista1:
	BLST
	ALIGN 4

Dummy_Ballista2:
	BLST
	ALIGN 4

PlayerPlacement:
	UNIT Alm AlmFighter 0x00 Level(1, Ally, 1) [2,15] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI
	UNIT Alm AlmFighter 0x00 Level(1, Ally, 1) [2,14] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI
	UNIT Alm AlmFighter 0x00 Level(1, Ally, 1) [2,16] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI
	UNIT Alm AlmFighter 0x00 Level(1, Ally, 1) [1,16] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI
	UNIT Alm AlmFighter 0x00 Level(1, Ally, 1) [3,15] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI
	UNIT Alm AlmFighter 0x00 Level(1, Ally, 1) [4,14] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI
	UNIT Alm AlmFighter 0x00 Level(1, Ally, 1) [4,15] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI
	UNIT Alm AlmFighter 0x00 Level(1, Ally, 1) [5,15] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI
	UNIT Alm AlmFighter 0x00 Level(1, Ally, 1) [7,14] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI
	UNIT Alm AlmFighter 0x00 Level(1, Ally, 1) [7,15] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI
	UNIT Alm AlmFighter 0x00 Level(1, Ally, 1) [5,14] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI
	UNIT Alm AlmFighter 0x00 Level(1, Ally, 1) [3,13] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI
	UNIT Alm AlmFighter 0x00 Level(1, Ally, 1) [4,12] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI
	UNIT Alm AlmFighter 0x00 Level(1, Ally, 1) [5,12] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI
	UNIT Alm AlmFighter 0x00 Level(1, Ally, 1) [6,13] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI
	UNIT

CutsceneUnits:
	UNIT Alm AlmFighter 0x00 Level(1, Ally, 1) [0,15] 0x00 0x00 0x1 REDA4R14 [BaseSword, Vulnerary] NoAI 
	UNIT Lukas Soldier 0x00 Level(1, Ally, 1) [0,15] 0x00 0x00 0x1 REDA4R15 [BaseLance, Vulnerary] NoAI 
	UNIT Clive Cavalier 0x00 Level(1, Ally, 1) [0,15] 0x00 0x00 0x1 REDA5R15 [BaseLance] NoAI
	UNIT

EnemyUnits:
	UNIT MarlaCh4 Witch	MarlaCh4 Level(9, Enemy, True) [14,2]	0x00 0x00 0x0 0x00 [Aura] NeverMoveAI
	//4 barons
	UNIT Rigel_Soldier Baron MarlaCh4 Level(1, Enemy, True) [21,6] 0x00 0x00 0x0 0x00 [IronLance] ChaseOnceApproachedAI
	UNIT Rigel_Soldier Baron MarlaCh4 Level(1, Enemy, True) [14,3] 0x00 0x00 0x0 0x00 [BaseLance] ChaseOnceApproachedAI
	UNIT Rigel_Soldier Baron MarlaCh4 Level(1, Enemy, True) [13,4] 0x00 0x00 0x0 0x00 [BaseAxe] ChaseOnceApproachedAI
	UNIT Rigel_Soldier Baron MarlaCh4 Level(1, Enemy, True) [12,4] 0x00 0x00 0x0 0x00 [IronAxe] ChaseOnceApproachedAI
	//4 snipers
	UNIT Rigel_Archer Sniper MarlaCh4 Level(9, Enemy, True) [13,5] 0x00 0x00 0x0 0x00 [SilverBow] AttackInRangeAI
	UNIT Rigel_Archer Sniper MarlaCh4 Level(10, Enemy, True) [15,13] 0x00 0x00 0x0 0x00 [SteelBow] NoAI
	UNIT Rigel_Archer Sniper MarlaCh4 Level(10, Enemy, True) [19,12] 0x00 0x00 0x0 0x00 [IronBow] AttackInRangeAI
	UNIT Rigel_Archer Sniper MarlaCh4 Level(8, Enemy, True) [22,9] 0x00 0x00 0x0 0x00 [SilverBow] AttackInRangeAI
	//4 knights
	UNIT Rigel_Soldier Knight MarlaCh4 Level(13, Enemy, True) [18,12] 0x00 0x00 0x0 0x00 [HandAxe] NoAI
	UNIT Rigel_Soldier Knight MarlaCh4 Level(13, Enemy, True) [19,13] 0x00 0x00 0x0 0x00 [IronAxe] NoAI
	UNIT Rigel_Soldier Knight MarlaCh4 Level(13, Enemy, True) [20,13] 0x00 0x00 0x0 0x00 [SteelLance] AttackInRangeAI
	UNIT Rigel_Soldier Knight MarlaCh4 Level(13, Enemy, True) [21,8] 0x00 0x00 0x0 0x00 [IronAxe] AttackInRangeAI
	//2 paladins
	UNIT Rigel_Cav Paladin MarlaCh4 Level(8, Enemy, True) [15,14] 0x00 0x00 0x0 0x00 [SteelLance] NoAI
	UNIT Rigel_Cav Paladin MarlaCh4 Level(8, Enemy, True) [18,13] 0x00 0x00 0x0 0x00 [SteelLance] AttackInRangeAI
	UNIT
	
WitchSummon1:
	UNIT DumaFaith_Witch Witch	MarlaCh4 Level(8, Enemy, True) [12,1]	0x00 0x00 0x1 REDA12R1 [Thunder, WitchWarpItem] WitchWarpAI
    UNIT
WitchSummon2:
	UNIT DumaFaith_Witch Witch	MarlaCh4 Level(8, Enemy, True) [16,1]	0x00 0x00 0x1 REDA16R1 [Thunder, WitchWarpItem] WitchWarpAI
	UNIT
	
BrigandsReinforcements:
	UNIT Bandit_Axe Berserker MarlaCh4 Level(5,Enemy,True) [17,9] 0x00 0x00 0x1 REDA17R9 [IronAxe,Torch] BrigandAI
	UNIT

Dummy_BeginningScene:
	MUSC SongID_HerosPath
	SetBackground(0x47)
	CHECK_ALIVE Silque
	BEQ 0x100 0xC 0x0
		BackgroundText(Ch4_3A_OpeningText)
		GOTO 0x101
	LABEL 0x100
		BackgroundText(Ch4_3A_OpeningText_NoSilque)
	LABEL 0x101
	MUSCMID SongID_Silence
	
	CHECK_ALIVE Forsyth
	BEQ 0x102 0xC 0x0
	CHECK_ALIVE Python
	BEQ 0x102 0xC 0x0
	
		//Forsyth and Python explain the Baron ability through banter?
		FADI 5
		STAL 5
		MUSC SongID_Reunion
		FADU 5
		BackgroundText(Ch4_3A_ForsythPython)
		MUSCMID SongID_Silence
	
	LABEL 0x102
	
	FADI 2
	CLEAN
	STAL 20
		
	MUSC SongID_Uprising
	LOAD1 0x1 EnemyUnits
	ENUN
	CAMERA_CENTERED [1,15]
	FADU 5
	STAL 20
	
	EVBIT_T 0x9
	LOAD2 0x0 CutsceneUnits
	ENUN
	EVBIT_F 0x9
	STAL 20
	CameraCursor(MarlaCh4)
	MapText(Ch4_3A_OpeningText_2)
	STAL 20
	CameraCursor(Alm)
	MapText(Ch4_3A_OpeningText_3)
	
	FADI 5
	CLEA 0x0
	LOAD3 0x0 PlayerPlacement 
	ENUN
	GotoPrepScreen
	ENDA

Dummy_EndingScene:
	EVBIT_MODIFY 0x1
	ENUT gChapter4A3Completed
	EVBIT_MODIFY 0x0
    CALL EndMusicAndMMS
	CALL UnifiedBEXPCaller
	FADI 2
	STAL 5
	MUSC SongID_ShadowOfVictory
	SetBackground(0x28) //fort interior
	BackgroundText(Ch4_3A_EndingText)
	MUSCMID SongID_Silence
	FADI 2
	STAL 5
	MUSC SongID_SilentGround
	SetBackground(0x0E)
	BROWNBOXTEXT Loc_RigelCastle [8,8]
	BackgroundText(FernandRinea_Ch4A_3)
	MUSCMID SongID_Silence
	FADI 2
	
	MNCH 0x26
	ENDA
