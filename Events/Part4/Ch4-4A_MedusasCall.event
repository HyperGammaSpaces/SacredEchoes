//Made by CrazyColorz5 of Fire Emblem Universe
//Based off of Camtech75's Skeleton
#include EAstdlib.event

EventPointerTable(plCh4_4A_Event, Dummy_ThisChapter)

Dummy_ThisChapter:
	POIN Dummy_TurnBasedEvents
	POIN Dummy_CharacterBasedEvents
	POIN Dummy_LocationBasedEvents
	POIN Dummy_MiscBasedEvents
	POIN Dummy_Dunno Dummy_Dunno Dummy_Dunno
	POIN Dummy_Tutorial
	POIN Dummy_Ballista1 Dummy_Ballista2
	POIN PlayerPlacement PlayerPlacement
	POIN $0 $0 $0 
	POIN EnemyUnits EnemyUnits EnemyUnits
	POIN Dummy_BeginningScene Dummy_EndingScene

Dummy_TurnBasedEvents:
	TURN 0x0 LoadSummons [7,255] 0x0
	//Add some sort of incentive to save Tatiana in time. Maybe place her on the summoning circle and she slowly loses HP?
	TURN
	
LoadSummons:
	CHECK_EVENTID 0x2 // boss death quote
	BNE 0 0xC 0x0 // branch label 0 if summoner dead
	COUNTER_CHECK 0 // slot c = counter 0 value
	SVAL 0x1 1 // number of turns between summons (1)
	BNE 1 0xC 0x1 // branch label 1 if counter is not at value

	// Counter at value
	COUNTER_SET 0 0 // reset counter
	CameraCursor(Nuibaba) // cam on summoner
	ENUN
	SOUN 0x3BF // summoning SFX
	STAL 60
    EVBIT_T 0x9
    CAMERA_CENTERED [15,5]
    WARPIN [15,5]
    LOAD1 0 GorgonSummons
    ENDWARP
    ENUN
    EVBIT_F 0x9
	EVBIT_T 0x7
    SetSummonerID(Terror_Gorgon, 0x81)
        
        CheckCurrentHP(Nuibaba)
        SMOV r1 0x2
        BGE 0 r1 rC
        SSUB slotsParam(r1, rC, r1)
        SET_HP Nuibaba
        STAL 5
    
	GOTO 0

	LABEL 1
	// Counter not at value
	COUNTER_ADD 0 0 // counter 0 ++

	LABEL 0
	// event end
	NoFade
	ENDA

Dummy_CharacterBasedEvents:
	CHAR

Dummy_LocationBasedEvents:
	#ifdef __DEBUG__
	  Seize(4,5)
	#endif
	Seize(4,1)
	Chest(Rapier, 24, 9)
	Chest(SilverLance, 21, 13) //TODO: replace item
	LOCA
	
SwitchPush:
	SVAL 0x2 0x0
	CHECK_ALLEGIANCE 0xFFFF
	BEQ 0x0 0xC 0x2
		//not a blue unit
		CHECK_EVENTID
		SADD 0xC2
		ENUF_SLOT2
		EVBIT_T 0x7
		ENDB
	LABEL 0x0 //continue
	MapText(Ch4_4A_SwitchFound)
	STAL 20
	//yes/no prompt
	SVAL 0x1 0x1
		//yes = 1 no = 2
		BEQ 0x1 0xC 0x1
		//said no
		SVAL 0x2 0x0
		CHECK_EVENTID
		SADD 0xC2
		ENUF_SLOT2
		EVBIT_T 0x7
		ENDB
	
	LABEL 0x1 //said yes
	SOUN 0xB1 //unlocking
	TILECHANGE 0x03 //open gate to nuibaba room
	STAL 60
	EARTHQUAKE_START 0x100
	STAL 60
	EARTHQUAKE_END
	STAL 20
	TILECHANGE 0x04 //open gate to nuibaba room
	SOUN 0xAB //village destroyed thump
	STAL 20
	MapText(Ch4_4A_SwitchPushed)
	STAL 20
	EVBIT_T 0x7
	ENDA

Dummy_MiscBasedEvents:
	CauseGameOverIfLordDies
	MournQuoteAFEVS
	AREA 0x15 SwitchPush [21,4] [21,5] //switch that opens tilechange 0x04
	AFEV 0x14 TatianaFreed 0x2
	AFEV
	
TatianaFreed:
	//Remove the trap tile at [4,2]
	RemoveTrapAtCoords(4,2)
	ApplyStatusToCoords(4,2,0x00)
	CHECK_ALIVE Tatiana
	BEQ 0x1 0xC 0x0
		MUSS SongID_Recruitment
		STAL 33
		CameraCursor(Tatiana)
		MapText(Ch4_4A_TatianaFreed)
		CALL $591F40
		SVAL 0x1 0x0
		CHAI Tatiana
		CUSA Tatiana
		STAL 20
		SVAL 0x3 Nosferatu
		GIVEITEMTO Tatiana
		STAL 20
		SVAL 0x3 HolyRing
		GIVEITEMTO Tatiana
		STAL 20
		MURE 0x05
	LABEL 0x1
	NoFade
	ENDA

Dummy_Dunno:
	END_MAIN

Dummy_Tutorial:
	END_MAIN

Dummy_Ballista1:
	DrainFloorTrap(4,2)
	BLST
	ALIGN 4

Dummy_Ballista2:
	BLST
	ALIGN 4

PlayerPlacement:
	UNIT Alm AlmFighter 0x00 Level(1, Ally, 1) [12,16] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI
	UNIT Alm AlmFighter 0x00 Level(1, Ally, 1) [13,16] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI
	UNIT Alm AlmFighter 0x00 Level(1, Ally, 1) [10,16] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI
	UNIT Alm AlmFighter 0x00 Level(1, Ally, 1) [11,15] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI
	UNIT Alm AlmFighter 0x00 Level(1, Ally, 1) [11,17] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI
	UNIT Alm AlmFighter 0x00 Level(1, Ally, 1) [9,17] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI
	UNIT Alm AlmFighter 0x00 Level(1, Ally, 1) [9,15] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI
	UNIT Alm AlmFighter 0x00 Level(1, Ally, 1) [14,15] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI
	UNIT Alm AlmFighter 0x00 Level(1, Ally, 1) [13,17] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI
	UNIT Alm AlmFighter 0x00 Level(1, Ally, 1) [12,18] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI
	UNIT Alm AlmFighter 0x00 Level(1, Ally, 1) [10,18] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI
	UNIT Alm AlmFighter 0x00 Level(1, Ally, 1) [11,19] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI
	UNIT Alm AlmFighter 0x00 Level(1, Ally, 1) [16,16] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI
	UNIT Alm AlmFighter 0x00 Level(1, Ally, 1) [11,16] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI
	UNIT Alm AlmFighter 0x00 Level(1, Ally, 1) [12,17] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI
	UNIT

CutsceneUnits:
	UNIT Alm AlmFighter 0x00 Level(1, Ally, 1) [12,21] 0x00 0x00 0x1 REDA14R15 [BaseSword, Vulnerary] NoAI 
	UNIT Lukas Soldier 0x00 Level(1, Ally, 1) [12,21] 0x00 0x00 0x1 REDA13R16 [BaseLance, Vulnerary] NoAI 
	UNIT Clive Cavalier 0x00 Level(1, Ally, 1) [12,21] 0x00 0x00 0x1 REDA12R16 [BaseLance] NoAI
	UNIT
	
TatianaLoads:
	UNIT Tatiana Saint 0x00 Level(1, NPC, 1) [25,3] 0x00 0x00 0x0 0x00 NoItems TatianaAI
	UNIT

EnemyCutsceneUnits:
	UNIT Nuibaba Cantor_F	Nuibaba Level(16, Enemy, True) [15,11]	0x00 0x00 0x0 0x00 [Silence] DontAttackTatianaBossAI
	UNIT

EnemyUnits:
	UNIT Nuibaba Cantor_F	Nuibaba Level(16, Enemy, True) [4,1]	DropItem 0x00 0x0 0x00 [Medusa, Fortify, MageRing] DontAttackTatianaBossAI
	//1 witch buddy
	UNIT Terror_Gorgon Gorgon	Nuibaba Level(6, Enemy, True) [4,6]	0x00 0x00 0x0 0x00 [EvilEye, Shadowshot, Fortify] DontAttackTatianaMageAI
	UNIT Rigel_Archer BowKnight	Nuibaba Level(4, Enemy, True) [3,7]	0x00 0x00 0x0 0x00 [SteelBow] AttackInRangeExceptTatianaAI
	UNIT Terror_Gorgon Gorgon	Nuibaba Level(4, Enemy, True) [5,7]	0x00 0x00 0x0 0x00 [EvilEye] AttackInRangeExceptTatianaAI
	UNIT Terror_Deathgoyle Deathgoyle	Nuibaba Level(5, Enemy, True) [4,8]	0x00 0x00 0x0 0x00 [SteelLance] AttackInRangeExceptTatianaAI
	UNIT Rigel_Merc DreadFighter Nuibaba Level(3, Enemy, True) [3,5] 0x00 0x00 0x0 0x00 [BaseSword] AttackInRangeExceptTatianaAI
	//1 BK
	UNIT Rigel_Archer BowKnight Nuibaba Level(2, Enemy, True) [11,7] 0x00 0x00 0x0 0x00 [SteelBow] AttackInRangeExceptTatianaAI
	//4 arcanists
	UNIT Rigel_Mage Arcanist Nuibaba Level(10, Enemy, True) [12,3] 0x00 0x00 0x0 0x00 [Mire] NoAI
	UNIT Rigel_Mage Arcanist Nuibaba Level(10, Enemy, True) [14,4] 0x00 0x00 0x0 0x00 [Mire] AttackInRangeAI
	UNIT Rigel_Mage Arcanist Nuibaba Level(9, Enemy, True) [20,4] 0x00 0x00 0x0 0x00 [Mire] AttackInRangeAI
	UNIT Rigel_Merc DreadFighter Nuibaba Level(2, Enemy, True) [18,3] 0x00 0x00 0x0 0x00 [SteelSword] NoAI
	//3 DFs
	UNIT Rigel_Mage Arcanist Nuibaba Level(10, Enemy, True) [9,9] 0x00 0x00 0x0 0x00 [Mire,SteelShield] NeverMoveAI
	UNIT Rigel_Soldier Baron Nuibaba Level(2, Enemy, True) [9,8] DropItem 0x00 0x0 0x00 [SteelAxe,HandAxe] AttackInRangeAI
	UNIT Rigel_Merc Myrmidon Nuibaba Level(9, Enemy, True) [17,8] 0x00 0x00 0x0 0x00 [Armorslayer] AttackInRangeAI
	UNIT Rigel_Merc Myrmidon Nuibaba Level(9, Enemy, True) [17,9] 0x00 0x00 0x0 0x00 [Zanbato] NoAI
	UNIT
	
GorgonSummons:
	UNIT Terror_Gorgon Gorgon	Nuibaba Level(8, Enemy, True) [15,5]	0x00 0x00 0x1 REDA15R5 [EvilEye] NoAI
	UNIT

Dummy_BeginningScene:
	
	MUSC SongID_WitchsTheme
	//Load Nuibaba at front entrance
	LOAD1 0x1 EnemyCutsceneUnits
	ENUN
	LOAD1 0x1 TatianaLoads
	ENUN
	CAMERA_CENTERED [16,16]
	FADU 5
	STAL 20
	
	//Load cutscene units
	EVBIT_T 0x9
	LOAD2 0x1 CutsceneUnits
	ENUN
	EVBIT_F 0x9
	
	CameraCursor(Nuibaba)
	MapText(Ch4_4A_OpeningText)
	//Warp Nuibaba to the throne
	SVAL 0x2 Nuibaba
	CALL Warp_Out_Effect
	STAL 0x08
	SVAL 0x2 Nuibaba
	_WARP 0xFFFF 0xFFFD [4,1]
	CALL Warp_In_Effect
	STAL 20
	CAMERA_CENTERED Nuibaba
	STAL 20
	
	//Warp in Tatiana to [4,2]
	SVAL 0x2 Tatiana
	_WARP 0xFFFF 0xFFFD [4,2]
	CALL Warp_In_Effect
	STAL 20
	
	MUSC SongID_Tension
	MapText(Ch4_4A_OpeningText_2)
	
	//Silence Tatiana
	StartBattle
    SetBattleNumbers(100,0xFF,0xFF,0xFF,0xFF,0xFF)
	Poison(0,0xFF)
	EndAttack

	FIG1 Nuibaba Tatiana Silence
	STAL 20
	ENUF 0x84
	ApplyStatusToCoords(4,2,0x43) //9 turns of silence
	
	CameraCursor(Nuibaba)
	MapText(Ch4_4A_OpeningText_3)
	
	STAL 20
	ENUT 0x84
	CameraCursor(Alm)
	MapText(Ch4_4A_OpeningText_4)
	FADI 16
	
	//Clean up and load actual deployments
	CLEA 0x0
	CLEE 0x0
	
	LOAD1 0x1 EnemyUnits
	ENUN
	LOAD3 0x0 PlayerPlacement 
	ENUN
	GotoPrepScreen
	ENDA

Dummy_EndingScene:
	EVBIT_MODIFY 0x1
	ENUT gChapter4A4Completed
	EVBIT_MODIFY 0x0
    CALL EndMusicAndMMS
	CALL UnifiedBEXPCaller
	FADI 2
	STAL 5
	SetBackground(0x11)
	CHECK_ALIVE Tatiana
	BEQ 0x1 0xC 0x0
		//good ending
		MUSC SongID_Victory
		BackgroundText(Ch4_4A_EndingText)
		STAL 20
		EVBIT_MODIFY 0x1
		CharacterJoinedNotification(Notif_TatianaJoin)
		ENUT gTatianaRecruited
		EVBIT_MODIFY 0x0
        FadeOutMusic
		FADI 5
		GOTO 0x2
	LABEL 0x1
	//bad ending
	MUSC SongID_Sorrow
	BackgroundText(Ch4_4A_EndingText_Bad)
	FadeOutMusic
    FADI 2
	LABEL 0x2
	MNCH 0x27
	ENDA
