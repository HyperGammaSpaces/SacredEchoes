//Made by CrazyColorz5 of Fire Emblem Universe
//Based off of Camtech75's Skeleton
#include EAstdlib.event

EventPointerTable(plCh4_5A_Event, Dummy_ThisChapter)

Dummy_ThisChapter:
	POIN Dummy_TurnBasedEvents
	POIN Dummy_CharacterBasedEvents
	POIN Dummy_LocationBasedEvents
	POIN Dummy_MiscBasedEvents
	POIN Dummy_Dunno Dummy_Dunno Dummy_Dunno
	POIN Dummy_Tutorial
	POIN Dummy_Ballista1 Dummy_Ballista2
	POIN PlayerPlacement PlayerPlacement
	POIN $0 $0 $0 
	POIN EnemyUnits EnemyUnits EnemyUnits
	POIN Dummy_BeginningScene Dummy_EndingScene

Dummy_TurnBasedEvents:
    TurnEventPlayer(0x0,IfTatianaThenBandits,3)
    TurnEventPlayer(0x0,LoadSouthwest,5)
    TurnEventPlayer(0x0,LoadNortheast,7)
	TURN
    
IfTatianaThenBandits:
    CHECK_EVENTID gTatianaRecruited
    BEQ 0x1 rC r0
        LoadReinforcements(BanditReinforcements)
    LABEL 0x1
    NoFade
    ENDA
    
LoadSouthwest:
    ReinforcementEvent(ReinforcementsSW)
    ENDA
    
LoadNortheast:
    ReinforcementEvent(ReinforcementsNE)
    ENDA

Dummy_CharacterBasedEvents:
	CharacterEvent(0x09,TalkToZeke,Tatiana,Zeke)
	CharacterEvent(0x09,TalkToZekeAlm,Alm,Zeke)
	CHAR
	
TalkToZeke:
	MUSS SongID_Recruitment
	STAL 0x20
	MapText(Ch4_5A_TatianaRecruitsZeke)
	SVAL 0x1 0x0
	CHAI Zeke
	//CHAI Rigel_Cav
	STAL 5
	CUSA Zeke
	STAL 0x14
	CharacterJoinedNotification(Notif_ZekeJoin)
	MURE 0x05
	EVBIT_MODIFY 0x1
	ENUT gZekeRecruited
	EVBIT_MODIFY 0x0
	NoFade
	ENDA
	
TalkToZekeAlm:
	MUSS SongID_Recruitment
	STAL 0x20
	MapText(Ch4_5A_AlmRecruitsZeke)
	SVAL 0x1 0x0
	CHAI Zeke
	//CHAI Rigel_Cav
	STAL 5
	CUSA Zeke
	STAL 0x14
	CharacterJoinedNotification(Notif_ZekeJoin)
	MURE 0x05
	EVBIT_MODIFY 0x1
	ENUT gZekeRecruited
	EVBIT_MODIFY 0x0
	NoFade
	ENDA

Dummy_LocationBasedEvents:
	#ifdef __DEBUG__
	  Seize(12,17)
	#endif
    House(0, HouseEvent1, 2,7)
	House(0, HouseEvent2, 13,9)
	House(0, HouseEvent3, 16,15)
	Village(0xA, VillageEvent, 5,5)
	LOCA
	
VillageEvent:
    MUSI
    SMOV r2 0x05
    SMOV r3 Ch4_5A_VillageText_Bad
    CHECK_EVENTID gTatianaRecruited
    BEQ 0x201 rC r0
        SMOV r3 Ch4_5A_VillageText_Good
    LABEL 0x201
    CALL $9EE310
    MUNO
    CHECK_EVENTID gTatianaRecruited
    BEQ 0x202 rC r0
        CALL $591F40
        SMOV 0x3 BraveSword
        GIVEITEMTO 0xFFFF
    LABEL 0x202
    EVBIT_T 0x7
	ENDA
    
HouseEvent1:
	MUSI
    SMOV r2 0x01
    SMOV r3 Ch4A_5_House1_Bad
    CHECK_EVENTID gTatianaRecruited
    BEQ 0x101 rC r0
        SMOV r3 Ch4A_5_House1
    LABEL 0x101
    CALL $9EE310
    MUNO
    EVBIT_T 0x7
	ENDA
	
HouseEvent2:
	MUSI
    SMOV r2 0x01
    SMOV r3 Ch4A_5_House2_Bad
    CHECK_EVENTID gTatianaRecruited
    BEQ 0x102 rC r0
        SMOV r3 Ch4A_5_House2
    LABEL 0x102
    CALL $9EE310
    MUNO
    EVBIT_T 0x7
	ENDA
    
HouseEvent3:
	MUSI
    SMOV r2 0x01
    SMOV r3 Ch4A_5_House3_Bad
    CHECK_EVENTID gTatianaRecruited
    BEQ 0x103 rC r0
        SMOV r3 Ch4A_5_House3
    LABEL 0x103
    CALL $9EE310
    MUNO
    EVBIT_T 0x7
	ENDA

Dummy_MiscBasedEvents:
	CauseGameOverIfLordDies
	DefeatAll(Dummy_EndingScene)
	AFEV 0x11 ZekeTurnsBlue gZekeRecruited
	MournQuoteAFEVS
	AFEV
	
ZekeTurnsBlue:
	MUSI
	CameraCursor(JeromeCh4_5)
	MapText(Ch4_5A_ZekeTurnsBlue)
	MUNO
	NoFade
	ENDA

Dummy_Dunno:
	END_MAIN

Dummy_Tutorial:
	END_MAIN

Dummy_Ballista1:
	BLST
	ALIGN 4

Dummy_Ballista2:
	BLST
	ALIGN 4

PlayerPlacement:
	UNIT Alm AlmFighter 0x00 Level(1, Ally, 1) [12,17] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI
	UNIT Tatiana Saint 0x00 Level(1, Ally, 1) [18,17] 0x00 0x00 0x0 0x00 [Nosferatu, Vulnerary] NoAI
	UNIT Alm AlmFighter 0x00 Level(1, Ally, 1) [11,18] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI
	UNIT Alm AlmFighter 0x00 Level(1, Ally, 1) [12,18] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI
	UNIT Alm AlmFighter 0x00 Level(1, Ally, 1) [10,19] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI
	UNIT Alm AlmFighter 0x00 Level(1, Ally, 1) [11,19] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI
	UNIT Alm AlmFighter 0x00 Level(1, Ally, 1) [12,19] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI
	UNIT Alm AlmFighter 0x00 Level(1, Ally, 1) [14,19] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI
	UNIT Alm AlmFighter 0x00 Level(1, Ally, 1) [13,18] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI
	UNIT Alm AlmFighter 0x00 Level(1, Ally, 1) [13,19] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI
	UNIT Alm AlmFighter 0x00 Level(1, Ally, 1) [12,20] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI
	UNIT Alm AlmFighter 0x00 Level(1, Ally, 1) [7,18] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI
	UNIT Alm AlmFighter 0x00 Level(1, Ally, 1) [6,18] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI
	UNIT Alm AlmFighter 0x00 Level(1, Ally, 1) [6,17] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI
	UNIT Alm AlmFighter 0x00 Level(1, Ally, 1) [18,18] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI
	UNIT Alm AlmFighter 0x00 Level(1, Ally, 1) [17,18] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI
	UNIT

CutsceneUnits:
	UNIT Alm AlmFighter 0x00 Level(1, Ally, 1) [14,21] 0x00 0x00 0x1 REDA18R18 [BaseSword, Vulnerary] NoAI 
	UNIT Clive Cavalier 0x00 Level(1, Ally, 1) [14,21] 0x00 0x00 0x1 REDA17R18 [BaseLance] NoAI
	UNIT Tatiana Saint 0x00 Level(1, Ally, 1) [14,21] 0x00 0x00 0x1 REDA18R17 [Nosferatu] NoAI
	UNIT

EnemyUnits:
	UNIT JeromeCh4_5 GoldKnight	JeromeCh4_5 Level(9, Enemy, True) [10,5]	0x00 0x00 0x0 0x00 [SilverLance,Javelin] NeverMoveAI
	UNIT Rigel_Soldier Baron JeromeCh4_5 Level(2, Enemy, True) [9,8] 0x00 0x00 0x0 0x00 [SteelLance] [0x3,0x3,0xC,0x20]
	UNIT Rigel_Soldier Baron JeromeCh4_5 Level(2, Enemy, True) [10,8] 0x00 0x00 0x0 0x00 [SteelAxe] [0x3,0x3,0xC,0x20]
	UNIT Rigel_Mage Sage JeromeCh4_5 Level(8, Enemy, True) [9,7] 0x00 0x00 0x0 0x00 [Thunder] [0x3,0x3,0xC,0x20]
	UNIT Rigel_Archer Archer JeromeCh4_5 Level(18, Enemy, True) [10,7] 0x00 0x00 0x0 0x00 [RadiantBow] [0x3,0x3,0xC,0x20]
	UNIT Rigel_Mage Arcanist JeromeCh4_5 Level(4, Enemy, True) [12,7] 0x00 0x00 0x0 0x00 [Mire,Physic] [0xE,0x3,0x4,0x20]
	UNIT Rigel_Mage Arcanist JeromeCh4_5 Level(4, Enemy, True) [7,8] 0x00 0x00 0x0 0x00 [Selene,Physic] [0xE,0x3,0x4,0x20]
    //bridge
	UNIT Rigel_Archer BowKnight JeromeCh4_5 Level(2, Enemy, True) [4,13] 0x00 0x00 0x0 0x00 [SteelBow] [0x3,0x3,0x4,0x20]
	UNIT Rigel_Archer BowKnight JeromeCh4_5 Level(2, Enemy, True) [9,13] 0x00 0x00 0x0 0x00 [SteelBow] [0x3,0x3,0x4,0x20]
    UNIT Rigel_Soldier Knight JeromeCh4_5 Level(9, Enemy, True) [6,14] DropItem 0x00 0x0 0x00 [HeavySpear] [0x0,0x0,0xC,0x0]
    UNIT Rigel_Soldier Knight JeromeCh4_5 Level(9, Enemy, True) [7,14] 0x00 0x00 0x0 0x00 [HandAxe] [0x0,0x0,0xC,0x0]
    
    //further out
    UNIT Rigel_Soldier Knight JeromeCh4_5 Level(9, Enemy, True) [19,14] 0x00 0x00 0x0 0x00 [SteelLance] [0x0,0x0,0xC,0x0]
    UNIT Rigel_Soldier Knight JeromeCh4_5 Level(9, Enemy, True) [20,15] 0x00 0x00 0x0 0x00 [SteelAxe] [0x0,0x0,0xC,0x0]
    UNIT Rigel_Merc Myrmidon JeromeCh4_5 Level(9, Enemy, True) [12,11] 0x00 0x00 0x0 0x00 [BaseSword] [0x0,0x3,0xC,0x0]
    
    UNIT Rigel_Cav GoldKnight JeromeCh4_5 Level(2, Enemy, True) [2,9] 0x00 0x00 0x0 0x00 [Ridersbane] [0x0,0x12,0xC,0x0]
	UNIT Rigel_Cav Paladin JeromeCh4_5 Level(12, Enemy, True) [3,7] 0x00 0x00 0x0 0x00 [SteelLance] [0x0,0x12,0xC,0x0]
	UNIT Rigel_Cav Paladin JeromeCh4_5 Level(12, Enemy, True) [1,11] 0x00 0x00 0x0 0x00 [SteelLance] [0x0,0x12,0xC,0x0]
	
	UNIT
    
MoreEnemies:
	UNIT Rigel_Wyvern WyvernLord JeromeCh4_5 Level(8, Enemy, True) [11,2] 0x00 0x00 0x0 0x00 [IronAxe] [0x0,0x12,0xC,0x0]
    UNIT Rigel_Merc Myrmidon JeromeCh4_5 Level(9, Enemy, True) [15,2] 0x00 0x00 0x0 0x00 [SteelSword] [0x0,0x0,0xC,0x0]
    UNIT Rigel_Merc Myrmidon JeromeCh4_5 Level(9, Enemy, True) [13,6] 0x00 0x00 0x0 0x00 [SteelSword] [0x0,0x0,0xC,0x0]
	UNIT Rigel_Cav Paladin JeromeCh4_5 Level(12, Enemy, True) [23,10] 0x00 0x00 0x0 0x00 [Javelin] [0x0,0x0,0xC,0x0]
	UNIT Rigel_Cav Paladin JeromeCh4_5 Level(12, Enemy, True) [23,11] 0x00 0x00 0x0 0x00 [SteelLance] [0x0,0x0,0xC,0x0]
	UNIT Rigel_Soldier Berserker JeromeCh4_5 Level(4, Enemy, True) [2,3] 0x00 0x00 0x0 0x00 [SteelAxe] [0x0,0x0,0xC,0x0]
	UNIT Rigel_Soldier Berserker JeromeCh4_5 Level(4, Enemy, True) [1,4] 0x00 0x00 0x0 0x00 [PoisonAxe] [0x0,0x0,0xC,0x0]
    UNIT
	
ZekeUnits:
	UNIT Zeke GoldKnight 0x00 Level(3,NPC,False) [18,8] 0x00 0x00 0x0 0x00 [SilverLance, BaseLance] [0x3,0x3,0xC,0x20]
	UNIT Rigel_Cav Paladin Zeke Level(12, NPC, True) [16,6] 0x00 0x00 0x0 0x00 [Javelin, Axereaver] [0x3,0x3,0x8,0x20]
	UNIT Rigel_Cav Paladin Zeke Level(12, NPC, True) [16,7] 0x00 0x00 0x0 0x00 [SteelLance,Javelin] [0x3,0x3,0x8,0x20]
	UNIT Rigel_Cav Paladin Zeke Level(8, NPC, True) [17,10] 0x00 0x00 0x0 0x00 [SteelLance] [0x0,0x3,0xC,0x0]
	UNIT Rigel_Cav Paladin Zeke Level(8, NPC, True) [16,9] 0x00 0x00 0x0 0x00 [IronLance] [0x0,0x3,0xC,0x0]
	UNIT Rigel_Cav Paladin Zeke Level(8, NPC, True) [18,11] 0x00 0x00 0x0 0x00 [IronLance] [0x0,0x3,0xC,0x0]
	UNIT Rigel_Cav Sage Zeke Level(4, NPC, True) [18,4] 0x00 0x00 0x0 0x00 [Thunder, Physic] [0xF,0x3,0x4,0x20]
	UNIT Rigel_Cav BowKnight Zeke Level(2, NPC, True) [20,10] 0x00 0x00 0x0 0x00 [BaseBow] [0x0,0x3,0x8,0x0]
	UNIT Rigel_Cav BowKnight Zeke Level(2, NPC, True) [21,7] 0x00 0x00 0x0 0x00 [BaseBow] [0x0,0x3,0x8,0x0]
	UNIT
	
ZekeEnemyUnits:
	UNIT ZekeBoss GoldKnight 0x00 Level(3,Enemy,False) [18,8] 0x00 0x00 0x0 0x00 [SilverLance, BaseLance] [0x0,0x0,0xC,0x0]
	UNIT Rigel_Cav Paladin ZekeBoss Level(12, Enemy, True) [16,6] 0x00 0x00 0x0 0x00 [Javelin, Axereaver] [0x0,0x0,0x8,0x0]
	UNIT Rigel_Cav Paladin ZekeBoss Level(12, Enemy, True) [16,7] 0x00 0x00 0x0 0x00 [SteelLance,Javelin] [0x0,0x0,0x8,0x0]
	UNIT Rigel_Cav Paladin ZekeBoss Level(8, Enemy, True) [17,10] 0x00 0x00 0x0 0x00 [SteelLance] [0x0,0x6,0xC,0x0]
	UNIT Rigel_Cav Paladin ZekeBoss Level(8, Enemy, True) [16,9] 0x00 0x00 0x0 0x00 [IronLance] [0x0,0x6,0xC,0x0]
	UNIT Rigel_Cav Paladin ZekeBoss Level(8, Enemy, True) [18,11] 0x00 0x00 0x0 0x00 [IronLance] [0x0,0x6,0xC,0x0]
	UNIT Rigel_Cav Sage ZekeBoss Level(4, Enemy, True) [18,4] 0x00 0x00 0x0 0x00 [Thunder, Physic] [0xF, 0x3, 0x4, 0x20]
	UNIT Rigel_Cav BowKnight ZekeBoss Level(2, Enemy, True) [20,10] 0x00 0x00 0x0 0x00 [BaseBow] [0x0,0x6,0x8,0x0]
	UNIT Rigel_Cav BowKnight ZekeBoss Level(2, Enemy, True) [21,7] 0x00 0x00 0x0 0x00 [BaseBow] [0x0,0x6,0x8,0x0]
	UNIT
    
BanditReinforcements:
    UNIT Bandit_Strong Berserker 0x0 Level(4, Enemy, True) [2,0] 0x00 0x00 0x1 REDA2R0 [IronAxe,Torch] [0x0,0x4,0xC,0x0]
    UNIT Bandit_Axe Brigand Bandit_Strong Level(16, Enemy, True) [2,0] 0x00 0x00 0x1 REDA2R1 [SteelAxe,Torch] [0x0,0x4,0xC,0x0]
    UNIT Bandit_Axe Brigand Bandit_Strong Level(16, Enemy, True) [2,0] 0x00 0x00 0x1 REDA1R0 [SteelAxe,Torch] [0x0,0x4,0xC,0x0]
    UNIT Bandit_Axe Brigand Bandit_Strong Level(16, Enemy, True) [2,0] 0x00 0x00 0x1 REDA3R0 [SteelAxe,Torch] [0x0,0x4,0xC,0x0]
    UNIT
    
ReinforcementsSW:
    UNIT Rigel_Wyvern WyvernLord JeromeCh4_5 Level(8, Enemy, True) [0,21] 0x00 0x00 0x1 REDA0R20 [IronAxe] [0x0,0x0,0xC,0x0]
    UNIT Rigel_Wyvern WyvernLord JeromeCh4_5 Level(8, Enemy, True) [0,21] 0x00 0x00 0x1 REDA1R21 [IronAxe] [0x0,0x0,0xC,0x0]
    UNIT    
    
ReinforcementsNE:
    UNIT Rigel_Wyvern WyvernLord JeromeCh4_5 Level(12, Enemy, True) [23,0] 0x00 0x00 0x1 REDA22R0 [IronAxe] [0x0,0x0,0xC,0x0]
    UNIT Rigel_Wyvern WyvernLord JeromeCh4_5 Level(12, Enemy, True) [23,0] 0x00 0x00 0x1 REDA23R1 [IronAxe] [0x0,0x0,0xC,0x0]
    UNIT

Dummy_BeginningScene:

	LOAD1 0x1 EnemyUnits
	ENUN
	LOAD1 0x1 ZekeEnemyUnits
	ENUN
	
	MUSC SongID_SolveTheRiddle
	CAMERA_CENTERED [14,6]
	//FADU 5
	//CameraCursor(JeromeCh4_5)
	SetBackground(0x25)
	BackgroundText(Ch4_5A_OpeningText)
	FADI 5
	CLEAN
	FADU 5
	STAL 20
	CameraCursor(ZekeBoss)
	MapText(Ch4_5A_OpeningText_Zeke)
	STAL 20
	
	CHECK_EVENTID gTatianaRecruited
	BEQ 0xD rC r0
		//got tatiana
		EVBIT_T 0x9
		LOAD2 0x0 CutsceneUnits
		ENUN
		EVBIT_F 0x9
		STAL 20
		
		CameraCursor(Tatiana)
		MUSI
		MapText(Ch4_5A_OpeningText_2)
		MUNO
		
		CameraCursor(ZekeBoss)
		MUSCMID SongID_Silence
		TEXTMODE 0
		TEXTSHOW Ch4_5A_OpeningText_3
		TEXTEND
		MUSC SongID_ImportantBattle
		TEXTCONT
		TEXTEND
		TEXTCLEAR

		STAL 20
		CUSN ZekeBoss
		STAL 20
		CameraCursor(Alm)
		
		MapText(Ch4_5A_OpeningText_4)
	LABEL 0xD
	
	FADI 16
	CLEA 0x0
	CLEE 0x0
	CLEN 0x0
	LOAD1 0x1 EnemyUnits
	ENUN
	LOAD3 0x0 PlayerPlacement 
	ENUN
	CHECK_EVENTID gTatianaRecruited
	BEQ 0xF rC r0
		LOAD1 0x1 ZekeUnits
		ENUN
        LOAD1 0x1 MoreEnemies
        ENUN
		GOTO 0x10
	LABEL 0xF
		LOAD1 0x1 ZekeEnemyUnits
		ENUN
		ENUT 0x09 //disable the talk option for alm and zeke
	LABEL 0x10
	GotoPrepScreen
	ENDA

Dummy_EndingScene:
	CALL UnifiedBEXPCaller
	MUSCMID SongID_Silence
	FADI 2
	EVBIT_MODIFY 0x1
	ENUT gChapter4A5Completed
	EVBIT_MODIFY 0x0
	
	CHECK_EVENTID gTatianaRecruited
	BEQ 0xA rC r0
		MUSC SongID_Victory
		SetBackground(0x02)
		TEXTMODE 1
		TEXTSHOW Ch4_5A_EndingText_Good
		TEXTEND
		MUSC SongID_Silence
		TEXTCONT
		TEXTEND
		TEXTCLEAR
		FADI 5
		MUSC SongID_Truth
		
		//CG stuff here
		EVBIT_MODIFY 0x0
		TEXTMODE 1
		BACG ZekeBrandCG
		FADU 5
		STAL 60
		CgText(Ch4_5A_EndingText_Good_2)
		CgText( Ch4_5A_EndingText_Good_3)
		FADI 5
		TEXTMODE 1
		SetBackground(0x02, 5)
        TEXTMODE 1
		TEXTSHOW Ch4_5A_EndingText_Good_4
        TEXTEND
        TEXTCLEAR
		FADI 2
		MUSCMID SongID_Silence
		STAL 60
		
		//Alm questions the prophecy
		MUSC SongID_Env_Night
		SetBackground(0x43, 2)
		TEXTMODE 1
		TEXTSHOW Ch4_5A_EndingText_Good_5
		TEXTEND
		MUSCSLOW SongID_CastleTheme
		TEXTCONT
		TEXTEND
		TEXTCLEAR
		
		GOTO 0xB
	LABEL 0xA
        MUSC SongID_SilentGround
        SetBackground(0x4B) //demolished village
        BackgroundText(Ch4_5A_EndingText_Bad)
        MUSCMID SongID_Silence
        STAL 20
        FADI 2
        MUSC SongID_Env_Night
        SetBackground(0x43, 2)
        BackgroundText(Ch4_5A_EndingText_Bad_5)
	LABEL 0xB
	MUSCMID SongID_Silence
	FADI 2
	MNCH 0x29
	ENDA
