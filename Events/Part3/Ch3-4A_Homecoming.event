//Made by CrazyColorz5 of Fire Emblem Universe
//Based off of Camtech75's Skeleton
#include EAstdlib.event

EventPointerTable(plCh3A_4_Event, Ch3_4A_ThisChapter)

Ch3_4A_ThisChapter:
	POIN Ch3_4A_TurnBasedEvents
	POIN Ch3_4A_CharacterBasedEvents
	POIN Ch3_4A_LocationBasedEvents
	POIN Ch3_4A_MiscBasedEvents
	POIN Ch3_4A_Dunno Ch3_4A_Dunno Ch3_4A_Dunno
	POIN Ch3_4A_Tutorial
	POIN Ch3_4A_Ballista1 Ch3_4A_Ballista2
	POIN Ch3_4A_Units1 Ch3_4A_Units1
	POIN $0 $0 $0 $0 $0 $0
	POIN Ch3_4A_BeginningScene Ch3_4A_EndingScene

Ch3_4A_TurnBasedEvents:
	TURN 0x0 LoadThief [8,0] 0x0
	TurnEventPlayer(0x0,LoadRightCavalry,6)
	TurnEventPlayer(0x0,LoadLeftCavalry,6)
	TurnEventPlayer(0x0,LoadSnipers,4)
	TURN

Ch3_4A_CharacterBasedEvents:
	CharacterEvent(8,TalkToMathilda,Clive,Mathilda)
	CharacterEventBothWays(gSirPurr, AlmLuthierSirPurrTalk, Alm, Luthier)
	CHAR
	
TalkToMathilda:
	MUSS SongID_Recruitment
	STAL 0x20
	MapText(Ch3_4A_MathildaJoins)
	SVAL 0x1 0x0
	CHAI Mathilda
	STAL 5
	CUSA Mathilda
	STAL 0x14
	CharacterJoinedNotification(Notif_MathildaJoin)
	MURE 0x05
	NoFade
	ENDA

Ch3_4A_LocationBasedEvents:
	#ifdef __DEBUG__
	  Seize(9,19)
	#endif
	Seize(9,2)
	Door(8, 15)
	Door(9, 15)
	Door(10, 15)
	Door(9, 9)
	Door(0, 2)
	Door(1, 2)
	Door(17, 2)
	Door(18, 2)
	Chest(Lancereaver, 1, 0)
	LOCA
	
LoadThief:
	ReinforcementEvent(ThiefReinforcement)

LoadSnipers:
	ReinforcementEvent(SniperReinforcement)

LoadRightCavalry:
	ReinforcementEvent(CavReinforcementRight)

LoadLeftCavalry:
	ReinforcementEvent(CavReinforcementLeft)
	
DesaixCommand:
	MUSI
	STAL 20
	CAMERA_CENTERED Desaix
	STAL 20
	FlashCursor(Desaix,60)
	STAL 20
	MapText(Ch3_4A_DesaixText2)
	STAL 20
	MUNO
	//ReinforcementEvent(DesaixCommandUnits)
	NoFade
	ENDA

Ch3_4A_MiscBasedEvents:
	CauseGameOverIfLordDies
	MournQuoteAFEVS
	AFEV 0x9 DesaixCommand 0x8
	AFEV

Ch3_4A_Dunno:
	END_MAIN

Ch3_4A_Tutorial:
	END_MAIN

Ch3_4A_Ballista1:
	BLST
	ALIGN 4

Ch3_4A_Ballista2:
	BLST
	ALIGN 4

CutsceneAllies:
	UNIT Alm AlmFighter 0x00 Level(1, Ally, 1) [9,19] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI 
	UNIT Lukas Soldier 0x00 Level(1, Ally, 1) [5,19] 0x00 0x00 0x0 0x00 [BaseLance, Vulnerary] NoAI 
	UNIT Clive Cavalier 0x00 Level(1, Ally, 1) [10,18] 0x00 0x00 0x0 0x00 [BaseLance] NoAI
	UNIT

Ch3_4A_Units1:
	UNIT Alm AlmFighter 0x00 Level(1, Ally, 1) [9,19] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI 
	UNIT Lukas Soldier 0x00 Level(1, Ally, 1) [5,19] 0x00 0x00 0x0 0x00 [BaseLance, Vulnerary] NoAI 
	UNIT Tobin Villager 0x00 Level(1, Ally, 1) [7,19] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI 
	UNIT Gray Villager 0x00 Level(1, Ally, 1) [12,20] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI 
	UNIT Kliff Villager 0x00 Level(1, Ally, 1) [6,20] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI
	UNIT Faye Villager_F 0x00 Level(1, Ally, 1) [8,20] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI
	UNIT Silque Cleric 0x00 Level(1, Ally, 1) [8,18] 0x00 0x00 0x0 0x00 [Nosferatu, Vulnerary] NoAI
	UNIT Clair PegasusKnight 0x00 Level(1, Ally, 1) [6,18] 0x00 0x00 0x0 0x00 [BaseLance] NoAI
	UNIT Clive Cavalier 0x00 Level(1, Ally, 1) [10,18] 0x00 0x00 0x0 0x00 [BaseLance] NoAI
	UNIT Forsyth Soldier 0x00 Level(1, Ally, 1) [11,19] 0x00 0x00 0x0 0x00 [BaseLance] NoAI
	UNIT Python Archer 0x00 Level(1, Ally, 1) [12,18] 0x00 0x00 0x0 0x00 [BaseBow,IronBow] NoAI
	UNIT Acantha Thief_F 0x00 Level(1, Ally, 1) [13,19] 0x00 0x00 0x0 0x00 [BaseSword] NoAI
	UNIT Luthier Mage 0x00 Level(1, Ally, 1) [10,20] 0x00 0x00 0x0 0x00 [Fire] NoAI
	UNIT

EnemyUnits:
	UNIT Desaix Baron 0x0 Level(5, Enemy, False) [9,2] 0x00 0x00 0x0 0x00 [Javelin, SteelSword] NeverMoveAI
	UNIT CantorBoss_Bonewalkers Cantor Desaix Level(7, Enemy, True) [13,6] 0x00 0x00 0x0 0x00 [Miasma, Invoke] CantorAI
	
	// Cell guard knight
	UNIT Rigel_Soldier Knight	Desaix Level(5, Enemy, True) [9,10]	0x00 0x00 0x0 0x00 [SteelAxe,HandAxe] NeverMoveAI

	// Outside Witch
	UNIT Rigel_Witch Witch	Desaix Level(4, Enemy, True) [17,20]	0x00 0x00 0x0 0x00 [Fire,Rewarp] WitchWarpAI
	
	// Main gate guard
	UNIT Rigel_Soldier Soldier	Desaix Level(13, Enemy, True) [8,13]	0x00 0x00 0x0 0x00 [Javelin] NeverMoveAI
	UNIT Rigel_Soldier Knight	Desaix Level(6, Enemy, True) [9,13]	DropItem 0x00 0x0 0x00 [Hammer] NeverMoveAI
	UNIT Rigel_Soldier Soldier	Desaix Level(14, Enemy, True) [10,13]	0x00 0x00 0x0 0x00 [IronLance] NeverMoveAI

	// Main gate behind
	UNIT Rigel_Archer Archer Desaix Level(13, Enemy, True) [8,12] 0x00 0x00 0x0 0x00 [BaseBow] AttackInRangeAI
	UNIT Rigel_Archer Sniper Desaix Level(7, Enemy, True) [9,11]	DropItem 0x00 0x0 0x00 [SteelBow,Elixir] [0x00,0x03,0x04,0x00]
	UNIT Rigel_Archer Archer Desaix Level(13, Enemy, True) [10,12] 0x00 0x00 0x0 0x00 [BaseBow] AttackInRangeAI
	UNIT Rigel_Mage Mage	Desaix Level(8, Enemy, True) [13,10]	0x00 0x00 0x0 0x00 [Thunder] AttackInRangeAI
	UNIT Rigel_Merc Mercenary	Desaix Level(9, Enemy, True) [15,11]	DropItem 0x00 0x0 0x00 [Armorslayer] AttackInRangeAI

	// Throne room
	UNIT Rigel_Mage Shaman	Desaix Level(12, Enemy, True) [4,1]	0x00 0x00 0x0 0x00 [Miasma,Heal] [0x0E,0x06]
	UNIT Rigel_Mage Shaman	Desaix Level(12, Enemy, True) [14,1]	0x00 0x00 0x0 0x00 [Miasma,Heal] [0x0E,0x06]
	UNIT Rigel_Archer Archer	Desaix Level(14, Enemy, True) [7,3]	0x00 0x00 0x0 0x00 [IronBow] AttackInRangeAI
	UNIT Rigel_Archer Archer	Desaix Level(14, Enemy, True) [11,3]	0x00 0x00 0x0 0x00 [IronBow] AttackInRangeAI
	UNIT Remnant_Soldier Soldier	Desaix Level(18, Enemy, True) [3,4]	0x00 0x00 0x0 0x00 [Ridersbane,IronShield] NeverMoveAI
	UNIT Remnant_Soldier Soldier	Desaix Level(16, Enemy, True) [4,4]	0x00 0x00 0x0 0x00 [Javelin] NeverMoveAI
	UNIT Rigel_Witch Cleric	Desaix Level(8, Enemy, True) [13,2]	0x00 0x00 0x0 0x00 [Physic,Nosferatu] [0x0E,0x03,0x0,0x20]
	UNIT Rigel_Witch Cleric	Desaix Level(8, Enemy, True) [5,2]	0x00 0x00 0x0 0x00 [Physic,Nosferatu] [0x0E,0x03,0x0,0x20]

	// Left hall
	UNIT Rigel_Archer Archer	Desaix Level(12, Enemy, True) [18,9]	0x00 0x00 0x0 0x00 [IronBow] AttackInRangeAI
	UNIT Rigel_Archer Archer	Desaix Level(14, Enemy, True) [18,3]	0x00 0x00 0x0 0x00 [IronBow] AttackInRangeAI
	UNIT Rigel_Soldier Soldier	Desaix Level(13, Enemy, True) [3,11]	0x00 0x00 0x0 0x00 [IronLance] AttackInRangeAI
	UNIT Rigel_Soldier Knight	Desaix Level(5, Enemy, True) [1,10]	0x00 0x00 0x0 0x00 [IronLance] NeverMoveAI
	UNIT Rigel_Soldier Knight	Desaix Level(6, Enemy, True) [1,11]	0x00 0x00 0x0 0x00 [BaseAxe] NeverMoveAI
	UNIT Rigel_Soldier Soldier	Desaix Level(13, Enemy, True) [1,6]	0x00 0x00 0x0 0x00 [IronLance] AttackInRangeAI
	UNIT Rigel_Merc Mercenary	Desaix Level(12, Enemy, True) [0,7]	0x00 0x00 0x0 0x00 [BaseSword] AttackInRangeAI
	UNIT Rigel_Mage Shaman	Desaix Level(10, Enemy, True) [3,5]	0x00 0x00 0x0 0x00 [Miasma] AttackInRangeAI

	// Right hall
	UNIT Rigel_Archer Archer	Desaix Level(12, Enemy, True) [0,9]	0x00 0x00 0x0 0x00 [IronBow] AttackInRangeAI
	UNIT Rigel_Soldier Knight	Desaix Level(6, Enemy, True) [17,10]	0x00 0x00 0x0 0x00 [BaseLance] NeverMoveAI
	UNIT Rigel_Soldier Knight	Desaix Level(5, Enemy, True) [17,11]	0x00 0x00 0x0 0x00 [IronAxe] NeverMoveAI
	UNIT Remnant_Soldier Soldier	Desaix Level(11, Enemy, True) [16,5]	0x00 0x00 0x0 0x00 [IronLance,HideShield] NeverMoveAI
	UNIT Remnant_Soldier Soldier	Desaix Level(14, Enemy, True) [16,6]	0x00 0x00 0x0 0x00 [Javelin] NeverMoveAI
	UNIT

	
ThiefReinforcement:
	UNIT TreasureHunter Thief Desaix Level(10,Enemy,True) [0,6] 0x00 0x00 0x1 REDA0R6 [BaseSword, Lockpick] LootingThiefAI
	UNIT TreasureHunter Thief Desaix Level(11,Enemy,True) [18,0] DropItem 0x00 0x1 REDA18R0 [SteelSword, Lockpick, 0x9F] [0x6,0xC,0x0,0x0]
	UNIT
	
CavReinforcementRight:
	UNIT Rigel_Cav Cavalier_F Desaix Level(13,Enemy,True) [19,17] 0x00 0x00 0x1 REDA19R17 [Javelin] NoAI
	UNIT Rigel_Cav Cavalier Desaix Level(14,Enemy,True) [19,16] 0x00 0x00 0x1 REDA19R16 [SteelLance] NoAI
	UNIT
	
CavReinforcementLeft:
	UNIT Remnant_Cav Cavalier Desaix Level(13,Enemy,True) [0,19] 0x00 0x00 0x1 REDA0R19 [Javelin] NoAI
	UNIT Remnant_Cav Cavalier_F Desaix Level(14,Enemy,True) [0,20] 0x00 0x00 0x1 REDA0R20 [SteelLance] NoAI
	UNIT

SniperReinforcement:
	UNIT Rigel_Archer Sniper Desaix Level(6, Enemy, True) [4,2]	0x00 0x00 1 REDA4R2 [SteelBow] [0x0,0x12,0x0,0x0]
	UNIT Rigel_Archer Sniper Desaix Level(6, Enemy, True) [14,2]	0x00 0x00 1 REDA14R2 [SteelBow] [0x0,0x12,0x0,0x0]
	UNIT
	
CutsceneFernand:
	UNIT FernandPaladin Paladin 0x00 Level(1, Enemy, False) [0,11] 0x00 0x00 0x0 0x00 [BaseLance] NoAI
	UNIT
	
MathildaJoins:
	UNIT Mathilda Paladin_F 0x00 Level(1, NPC, False) [9,6] 0x00 0x00 0x0 0x00 [BaseLance] [0x06, 0x0D, 0x00, 0x00]
	UNIT

Ch3_4A_BeginningScene:
	//Load Fernand and Mathilda, show CG, show textID FernandMathilda_Dungeon
	MUSC SongID_Env_GustyWind
	LOAD1 0x1 MathildaJoins
	ENUN
	LOAD1 0x1 CutsceneFernand
	ENUN
	CAMERA_CENTERED Mathilda
	STAL 4
	FADU 5
	MOVE 0x08 FernandPaladin [9,10]
	ENUN
	STAL 20
	FlashCursor(FernandPaladin, 60)
	FADI 5
	
	SetBackground(FernandMathildaCG,5)
	STAL 90
		CgText(FernandMathilda_Dungeon_CGText_1)
		CgText( FernandMathilda_Dungeon_CGText_2)
		CgText(FernandMathilda_Dungeon_CGText_3)
		FADI 5
	
	SetBackground(0x17)
	TEXTMODE 1 
	TEXTSHOW FernandMathilda_Dungeon
	TEXTEND
	MUSC SongID_Env_GustyWindStop
	STAL 20
	MUSC SongID_Truth
	TEXTCONT
	TEXTEND
	TEXTCLEAR
	ClearBackgroundSmooth
	MOVE 0x08 FernandPaladin [18,6]
	ENUN
	STAL 20
	FADI 2
	MUSCMID SongID_Silence
	CLEE 0x0
	STAL 40
	LOAD2 0x1 CutsceneAllies
	ENUN
	LOAD1 0x1 EnemyUnits
	ENUN
	FADU 2

	//Then load Desaix and set BG to 0xE
	MUSC SongID_SolveTheRiddle
	CAMERA_CENTERED Desaix
	STAL 20
	FlashCursor(Desaix, 60)
	STAL 20

	//SetBackground(0xE)
	MapText(Ch3_4A_OpeningText)
	FADI 16
	CLEA 0x0
	LOAD3 0x0 Ch3_4A_Units1 
	ENUN
	GotoPrepScreen
	ENDA

Ch3_4A_EndingScene:
	EVBIT_MODIFY 0x1
	ENUT gChapter3A4Completed
	EVBIT_MODIFY 0x0
    CALL EndMusicAndMMS
	CALL UnifiedBEXPCaller
	FADI 2
    STAL 5
	MUSC SongID_CastleTheme
	
	CHECK_ALIVE Tobin 
	BEQ 0x4 0xC 0x0
	
		//If Tobin is alive
		SetBackground(0xE)
		TEXTMODE 1
		TEXTSHOW Ch3_4A_EndingText
		TEXTEND
		MUSI
		TEXTCONT
		TEXTEND
		MUNO
		MUSC SongID_Truth
		TEXTCONT
		TEXTEND
		TEXTCLEAR
		
		FADI 2
		FadeOutMusic
		STAL 20
		MUSC SongID_Reunion
		SetBackground(0x49)
		BackgroundText(Ch3_4A_EndingText_2)
		//CG goes here
		FadeOutMusic
		STAL 20
		MUSC SongID_Env_LegendaryWeapon
		FADI 8
		TEXTMODE 1
		SetBackground(RoyalSwordCG)
		STAL 90
		CgText(Ch3_4A_CGText)
		FADI 5
		
		MUSC SongID_Legacy
		SetBackground(0x49)
		BackgroundText(Ch3_4A_EndingText_3)
		GOTO 0x3
	
	LABEL 0x4
		SetBackground(0xE)
		BackgroundText(Ch3_4A_EndingText_NoTobin)
		FADI 2
		STAL 20
		MUSC SongID_CastleTheme
		SetBackground(0x49)
		BackgroundText(Ch3_4A_EndingText_2_NoTobin)
		//CG goes here
		FadeOutMusic
		STAL 20
		MUSC SongID_Env_LegendaryWeapon
		FADI 8
		TEXTMODE 1
		SetBackground(RoyalSwordCG)
		STAL 90
		CgText(Ch3_4A_CGText)
		FADI 5
		
		MUSC SongID_Legacy
		SetBackground(0x49)
		BackgroundText(Ch3_4A_EndingText_3_NoTobin)
	LABEL 0x3
	
	EVBIT_MODIFY 0x1
	SetBackground(0x49)
	SVAL 0x3 RoyalSword
	GIVEITEMTO Alm
	STAL 20
	EVBIT_MODIFY 0x0
	
	FADI 2
	MNCH 0x13
	ENDA
