//Made by CrazyColorz5 of Fire Emblem Universe
//Based off of Camtech75's Skeleton
#include EAstdlib.event

EventPointerTable(plCh3A_5_Event, Ch3_5A_ThisChapter)

Ch3_5A_ThisChapter:
	POIN Ch3_5A_TurnBasedEvents
	POIN Ch3_5A_CharacterBasedEvents
	POIN Ch3_5A_LocationBasedEvents
	POIN Ch3_5A_MiscBasedEvents
	POIN Ch3_5A_Dunno Ch3_5A_Dunno Ch3_5A_Dunno
	POIN Ch3_5A_Tutorial
	POIN Ch3_5A_Ballista1 Ch3_5A_Ballista2
	POIN Ch3_5A_Units1 Ch3_5A_Units1
	POIN $0 $0 $0 $0 $0 $0
	POIN Ch3_5A_BeginningScene Ch3_5A_EndingScene

Ch3_5A_TurnBasedEvents:
	TurnEventPlayer(0x9,BossChargeSetup,0,256)

	TurnEventPlayer(0x0,TurretArchersSpawn,6)
	TurnEventPlayer(0x0,TopCavsSpawn,8)
	TurnEventPlayer(0x0,PunishmentPalsSpawn,14,(256-14))
	TURN

TopCavsSpawn:
	LoadReinforcements(RightCavs)
	ReinforcementEvent(LeftCavs)
	
TurretArchersSpawn:
	ReinforcementEvent(TurretArchers)

PunishmentPalsSpawn:
	ReinforcementEvent(PunishmentPals)


BossChargeCheck:
	CHECK_ALLEGIANCE 0xFFFF
	BNE 1 0xC 0x0 // branch label 1 if not ally
		// do nothing (eid 0x8 now true)
		GOTO 2
	LABEL 1
		ENUF 0x8
		LABEL 2
	NoFade
	ENDA

BossChargeSetup:
	CHECK_EVENTID 0x8
	BEQ 1 0xC 0x0 // branch label 1 if check not set
		//Lawson says something
		CAM1 Lawson
		FlashCursor(Lawson, 60)
		Text(Ch3_5A_BossWarningText)
		//CHAI his group
		SetAIParameters(0x0,0x0,0x04,0x0)
		CHAI Lawson
		SetAIParameters(0x0D,0x03,0x04,0x0)
		CHAI 0x87
		GOTO 2
	LABEL 1
		ENUF 0x9
		LABEL 2
	NoFade
	ENDA

Ch3_5A_CharacterBasedEvents:
	CHAR

Ch3_5A_LocationBasedEvents:
	#ifdef __DEBUG__
	  Seize(12,5)
	#endif
	LOCA

Ch3_5A_MiscBasedEvents:
	CauseGameOverIfLordDies
	DefeatBoss(Ch3_5A_EndingScene)
	MournQuoteAFEVS
	AREA 0x8 BossChargeCheck [9,13] [16,14]
	AFEV

Ch3_5A_Dunno:
	END_MAIN

Ch3_5A_Tutorial:
	END_MAIN

Ch3_5A_Ballista1:
	BLST [2,16] 0x35
	BLST [23,16] 0x35
	BLST
	ALIGN 4

Ch3_5A_Ballista2:
	BLST
	ALIGN 4

Ch3_5A_Units1:
#ifdef __DEBUG__
	UNIT Alm AlmFighter 0x00 Level(13,Ally,True) [12,5] 0x00 0x00 0x0 0x00 [BaseSword, RoyalSword, Vulnerary] NoAI 
	UNIT Lukas Knight 0x00 Level(8,Ally,True) [13,5] 0x00 0x00 0x0 0x00 [BaseLance, BaseAxe, Vulnerary] NoAI 
	UNIT Tobin Sniper 0x00 Level(7,Ally,True) [14,4] 0x00 0x00 0x0 0x00 [BaseBow, SteelBow, Vulnerary] NoAI 
	UNIT Gray Myrmidon 0x00 Level(7,Ally,True) [11,4] 0x00 0x00 0x0 0x00 [BaseSword, Armorslayer, Vulnerary] NoAI 
	UNIT Kliff Mage 0x00 Level(13,Ally,True) [14,6] 0x00 0x00 0x0 0x00 [Fire, Vulnerary] NoAI
	UNIT Faye Cleric 0x00 Level(10,Ally,True) [11,6] 0x00 0x00 0x0 0x00 [Nosferatu, Vulnerary] NoAI
	UNIT Silque Cleric 0x00 Level(13,Ally,True) [10,5] 0x00 0x00 0x0 0x00 [Nosferatu, Vulnerary] NoAI
	UNIT Clair PegasusKnight 0x00 Level(13,Ally,True) [15,5] 0x00 0x00 0x0 0x00 [BaseLance,SteelLance] NoAI
	UNIT Clive Paladin 0x00 Level(6,Ally,True) [12,3] 0x00 0x00 0x0 0x00 [BaseLance,SteelLance,HeavySpear] NoAI
	UNIT Forsyth Knight 0x00 Level(6,Ally,True) [13,3] 0x00 0x00 0x0 0x00 [BaseAxe,BaseLance,Forscythe] NoAI
	UNIT Python Archer 0x00 Level(13,Ally,True) [10,3] 0x00 0x00 0x0 0x00 [BaseBow] NoAI
	UNIT Acantha Thief_F 0x00 Level(13,Ally,True) [15,3] 0x00 0x00 0x0 0x00 [BaseSword,Lockpick,Thunderblade] NoAI
	UNIT Luthier Mage 0x00 Level(11,Ally,True) [12,7] 0x00 0x00 0x0 0x00 [Fire] NoAI
	UNIT Mathilda Paladin_F 0x00 Level(3,Ally,True) [13,7] 0x00 0x00 0x0 0x00 [BaseLance,Ridersbane] NoAI
	UNIT
#else
	UNIT Alm AlmFighter 0x00 Level(1, Ally, 1) [12,5] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI 
	UNIT Lukas Soldier 0x00 Level(1, Ally, 1) [13,5] 0x00 0x00 0x0 0x00 [BaseLance, Vulnerary] NoAI 
	UNIT Tobin Villager 0x00 Level(1, Ally, 1) [14,4] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI 
	UNIT Gray Villager 0x00 Level(1, Ally, 1) [11,4] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI 
	UNIT Kliff Villager 0x00 Level(1, Ally, 1) [14,6] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI
	UNIT Faye Villager_F 0x00 Level(1, Ally, 1) [11,6] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI
	UNIT Silque Cleric 0x00 Level(1, Ally, 1) [10,5] 0x00 0x00 0x0 0x00 [Nosferatu, Vulnerary] NoAI
	UNIT Clair PegasusKnight 0x00 Level(1, Ally, 1) [15,5] 0x00 0x00 0x0 0x00 [BaseLance] NoAI
	UNIT Clive Cavalier 0x00 Level(1, Ally, 1) [12,3] 0x00 0x00 0x0 0x00 [BaseLance] NoAI
	UNIT Forsyth Soldier 0x00 Level(1, Ally, 1) [13,3] 0x00 0x00 0x0 0x00 [BaseLance] NoAI
	UNIT Python Archer 0x00 Level(1, Ally, 1) [10,3] 0x00 0x00 0x0 0x00 [BaseBow] NoAI
	UNIT Acantha Thief_F 0x00 Level(1, Ally, 1) [15,3] 0x00 0x00 0x0 0x00 [BaseSword] NoAI
	UNIT Luthier Mage 0x00 Level(1, Ally, 1) [12,7] 0x00 0x00 0x0 0x00 [Fire] NoAI
	UNIT Mathilda Paladin_F 0x00 Level(1, Ally, 1) [13,7] 0x00 0x00 0x0 0x00 [BaseLance] NoAI
	UNIT
#endif

CutsceneUnits:
	UNIT Alm AlmFighter 0x00 Level(1, Ally, 1) [4,11] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI 
	UNIT Lukas Soldier 0x00 Level(1, Ally, 1) [5,12] 0x00 0x00 0x0 0x00 [BaseLance, Vulnerary] NoAI 
	UNIT

EnemyUnits:
	//main entrance
	UNIT Lawson Paladin 0x00 Level(5,Enemy,False) [11,18] 0x00 0x00 0x0 0x00 [Ridersbane, Javelin, IronShield] NeverMoveAI
	UNIT 0x87 Sage Lawson Level(1,Enemy,True) [10,19] 0x00 0x00 0x0 0x00 [Thunder,Physic] [0x0F,0x03,0x04,0x20]
	UNIT 0x87 Sniper Lawson Level(3,Enemy,True) [12,19] DropItem 0x00 0x0 0x00 [RadiantBow] NeverMoveAI
	UNIT 0x87 Paladin_F Lawson Level(2,Enemy,True) [9,18] 0x00 0x00 0x0 0x00 [IronLance,HideShield] NeverMoveAI
	UNIT 0x87 Paladin Lawson Level(2,Enemy,True) [13,18] 0x00 0x00 0x0 0x00 [HeavySpear,HideShield] NeverMoveAI
	UNIT 0x87 Witch Lawson Level(4,Enemy,True) [9,19] DropItem 0x00 0x0 0x00 [Thunder,Elixir] NeverMoveAI
	
	UNIT 0x64 Thief Lawson Level(12,Enemy,True) [5,19] DropItem 0x00 0x0 0x00 [IronSword,Soma] [0x6,0xC,0x0,0x0]
	
	//turrets
	UNIT 0x89 Sniper Lawson Level(2,Enemy,True) [2,16] 0x00 0x00 0x0 0x00 [SteelBow] AttackInRangeAI
	UNIT 0x89 Sniper_F Lawson Level(3,Enemy,True) [23,16] 0x00 0x00 0x0 0x00 [SteelBow] AttackInRangeAI
	UNIT 0x88 Knight Lawson Level(4,Enemy,True) [2,7] 0x00 0x00 0x0 0x00 [SteelAxe,Javelin] AttackInRangeAI
	UNIT 0x88 Knight Lawson Level(4,Enemy,True) [23,7] 0x00 0x00 0x0 0x00 [SteelLance,HandAxe] AttackInRangeAI
	UNIT 0x89 Archer_F Lawson Level(10,Enemy,True) [2,3] 0x00 0x00 0x0 0x00 [SilverBow] AttackInRangeAI
	UNIT 0x89 Archer Lawson Level(14,Enemy,True) [23,3] 0x00 0x00 0x0 0x00 [IronBow] AttackInRangeAI
	
	//north
	UNIT 0x8A WyvernRider Lawson Level(14,Enemy,True) [5,1] 0x00 0x00 0x0 0x00 [SteelAxe] AttackSnagsAI
	UNIT 0x8A WyvernRider Lawson Level(11,Enemy,True) [4,0] 0x00 0x00 0x0 0x00 [IronAxe] AttackSnagsAI
	UNIT 0x8A WyvernRider Lawson Level(11,Enemy,True) [6,0] 0x00 0x00 0x0 0x00 [IronAxe] AttackSnagsAI
	
	UNIT 0x89 Sniper_F Lawson Level(3,Enemy,True) [19,1] 0x00 0x00 0x0 0x00 [SteelBow] AttackInRangeAI
	UNIT 0x89 Archer_F Lawson Level(14,Enemy,True) [18,0] 0x00 0x00 0x0 0x00 [IronBow] AttackInRangeAI
	UNIT 0x89 Archer Lawson Level(14,Enemy,True) [20,0] 0x00 0x00 0x0 0x00 [IronBow] AttackInRangeAI
	
	//cav squadrons
	UNIT 0x86 Paladin_F Lawson Level(3,Enemy,True) [1,12] 0x00 0x00 0x0 0x00 [SteelLance,Javelin] [0x0,0x12,0x0,0x0]
	UNIT 0x86 Cavalier Lawson Level(9,Enemy,True) [0,11] 0x00 0x00 0x0 0x00 [IronLance] [0x0,0x12,0x0,0x0]
	UNIT 0x86 Cavalier_F Lawson Level(9,Enemy,True) [0,12] 0x00 0x00 0x0 0x00 [IronLance] [0x0,0x12,0x0,0x0]
	UNIT 0x86 Cavalier Lawson Level(9,Enemy,True) [0,13] 0x00 0x00 0x0 0x00 [SteelLance] [0x0,0x12,0x0,0x0]
	
    UNIT 0x86 Paladin Lawson Level(3,Enemy,True) [24,12] 0x00 0x00 0x0 0x00 [SteelLance,Javelin] AttackInRangeAI
	UNIT 0x86 Cavalier Lawson Level(9,Enemy,True) [25,11] 0x00 0x00 0x0 0x00 [IronLance] [0x0,0x12,0x0,0x0]
	UNIT 0x86 Cavalier_F Lawson Level(9,Enemy,True) [25,12] 0x00 0x00 0x0 0x00 [IronLance] [0x0,0x12,0x0,0x0]
	UNIT 0x86 Cavalier_F Lawson Level(9,Enemy,True) [25,13] 0x00 0x00 0x0 0x00 [SteelLance] [0x0,0x12,0x0,0x0]

	
	UNIT 0x8B Mage Lawson Level(7,Enemy,True) [0,8] 0x00 0x00 0x0 0x00 [Thunder] AttackInRangeAI
	UNIT 0x8C Mage_F Lawson Level(6,Enemy,True) [25,8] 0x00 0x00 0x0 0x00 [Thunder] AttackInRangeAI

	UNIT

LeftCavs:
	UNIT 0x86 Cavalier Lawson Level(11,Enemy,True) [0,11] 0x00 0x00 0x1 REDA0R11 [SteelLance] NoAI
	UNIT 0x86 Cavalier_F Lawson Level(11,Enemy,True) [0,12] 0x00 0x00 0x1 REDA0R12 [IronLance] NoAI
	UNIT

RightCavs:
	UNIT 0x86 Cavalier Lawson Level(11,Enemy,True) [25,9] 0x00 0x00 0x1 REDA25R9 [IronLance] NoAI
	UNIT 0x86 Cavalier_F Lawson Level(11,Enemy,True) [25,10] 0x00 0x00 0x1 REDA25R10 [Javelin] NoAI
	UNIT

PunishmentPals:
	UNIT 0x86 Paladin Lawson Level(10,Enemy,True) [19,19] 0x00 0x00 0x1 REDA19R19 [SilverLance,SteelShield] NoAI
	UNIT 0x86 Paladin_F Lawson Level(10,Enemy,True) [20,19] 0x00 0x00 0x1 REDA20R19 [Ridersbane,Javelin,HideShield] NoAI
	UNIT

TurretArchers:
	UNIT 0x89 Archer Lawson Level(15,Enemy,True) [2,19] 0x00 0x00 0x0 0x00 [SteelBow] NoAI
	UNIT 0x89 Archer_F Lawson Level(12,Enemy,True) [23,19] 0x00 0x00 0x0 0x00 [SilverBow] NoAI
	UNIT

Ch3_5A_BeginningScene:
	MUSC 0x4A
	LOAD2 0x1 CutsceneUnits
	ENUN
	CAM1 Alm
	VCWF 0x0
	
	SetBackground(0x44)
	Text(Ch3_5A_OpeningText)
	ClearBackground
	
	//move alm and lukas around
	EVBIT_T 0x9
	MOVE 0x08 Alm [12, 10]
	MOVE 0x08 Lukas [13, 11]
	ENUN
	STAL 40
	MOVE 0x08 Alm [12, 5]
	MOVE 0x08 Lukas [13, 5]
	ENUN
	EVBIT_F 0x9
	STAL 20
	VCWF 0x4
	
	SetBackground(0x4A)
	Text(Ch3_5A_OpeningText_2)
	ClearBackgroundSmooth
	
	LOAD1 0x1 EnemyUnits
	ENUN
	FADU 2
	SOUN 0x9B
	STAL 5
	SOUN 0x9C
	STAL 20
	CAM1 Lawson
	FlashCursor(Lawson, 60)
	MUSC 0x2E
	Text(Ch3_5A_OpeningText_3)
	FADI 5
	REMA
	
	CLEA 0x0
	#ifdef __DEBUG__
		LOAD1 0x1 Ch3_5A_Units1
		ENUN
	#else
		LOAD3 0x0 Ch3_5A_Units1 
		ENUN
		GotoPrepScreen
	#endif
	ENDA

Ch3_5A_EndingScene:
	CALL UnifiedBEXPCaller
	FADI 2
	EVBIT_MODIFY 0x1
	ENUT gChapter3A5Completed
	EVBIT_MODIFY 0x0
	
	MUSC 0x3A //Silent Ground
	SetBackground(0x44)
	
	//check Gray and Tobin
	CHECK_ALIVE Gray
	BEQ 6 0x0 0xC
		CHECK_ALIVE Tobin
		BEQ 6 0x0 0xC
		//check mathilda
			CHECK_EXISTS Mathilda
			BEQ 7 0x0 0xC
			CHECK_ALIVE Mathilda
			BEQ 7 0x0 0xC
			//alive: 
			Text(Ch3_5A_EndingText)
			GOTO 9
			//dead: 
			LABEL 7
			Text(Ch3_5A_EndingText_NoMathilda)
			GOTO 9
	LABEL 6
	//else
		//check mathilda
		CHECK_EXISTS Mathilda
		BEQ 8 0x0 0xC
		CHECK_ALIVE Mathilda
		BEQ 8 0x0 0xC
			//alive: 
			Text(Ch3_5A_EndingText_NoGray)
			GOTO 9
			LABEL 8
			//dead:
			Text(Ch3_5A_EndingText_EveryoneDied)
	
	LABEL 9
	FADI 2
	MNCH 0x14
	ENDA
