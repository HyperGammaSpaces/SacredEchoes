//Made by CrazyColorz5 of Fire Emblem Universe
//Based off of Camtech75's Skeleton
#include EAstdlib.event
#define DeltheaLocalFlag 0x20

EventPointerTable(plCh3A_7_Event, Ch3_7A_ThisChapter)

Ch3_7A_ThisChapter:
	POIN Ch3_7A_TurnBasedEvents
	POIN Ch3_7A_CharacterBasedEvents
	POIN Ch3_7A_LocationBasedEvents
	POIN Ch3_7A_MiscBasedEvents
	POIN Ch3_7A_Dunno Ch3_7A_Dunno Ch3_7A_Dunno
	POIN Ch3_7A_Tutorial
	POIN Ch3_7A_Ballista1 Ch3_7A_Ballista2
	POIN Ch3_7A_Units1 Ch3_7A_Units1
	POIN $0 $0 $0 $0 $0 $0
	POIN Ch3_7A_BeginningScene Ch3_7A_EndingScene

Ch3_7A_TurnBasedEvents:
	TurnEventPlayer(0x0,SluiceGateStart,3)
	TurnEventPlayer(0x0,SluiceGateDrain1,4)
	TurnEventPlayer(0x0,SluiceGateDrain2,5)
	TurnEventPlayer(0x0,WyvernsSpawn,7)
	//Wall starts to crack
	TurnEventPlayer(0x0,SluiceGateWallCrack1,9)
	TurnEventPlayer(0x0,SluiceGateWallCrack2,10)
	TurnEventPlayer(0x0,SluiceGateWallCrack3,11)
	TurnEventPlayer(0x0,SluiceGateWallCrack4,12)
	TurnEventPlayer(0x0,SluiceGateFlood,13)
	TURN
	
WyvernsSpawn:
	ReinforcementEvent(Reinforcements1)
	ENDA
	
SluiceGateStart:
	CAM1 Tatarrah
	FlashCursor(Tatarrah, 60)
	Text(Ch3_7A_SluiceGateStart)
	EARTHQUAKE_START 0x100
	STAL 30
	FADI 16
	STAL 30
	EARTHQUAKE_END
	TILECHANGE 0x1 //tilechange 0x01 (wall)
	STAL 30
	FADU 16
	STAL 30
	NoFade
	ENDA
	
SluiceGateDrain1:
	EARTHQUAKE_START 0x100
	STAL 30
	FADI 16
	STAL 30
	EARTHQUAKE_END
	TILECHANGE 0x2 //wlevel half left
	TILECHANGE 0x3 //wlevel half right
	STAL 30
	FADU 16
	SOUN 0xBD //water going down
	STAL 30
	NoFade
	ENDA
	
SluiceGateDrain2:
	EARTHQUAKE_START 0x100
	STAL 30
	FADI 16
	STAL 30
	EARTHQUAKE_END
	TILECHANGE 0x4 //wlevel none left
	TILECHANGE 0x5 //wlevel none right
	STAL 30
	FADU 16
	SOUN 0xBD //water going down
	STAL 30
	NoFade
	ENDA
	
SluiceGateWallCrack1:
	CAM1 Tatarrah
	EARTHQUAKE_START 0x100
	STAL 60
	EARTHQUAKE_END
	TILECHANGE 0x6 //wall right 1
	STAL 30
	SOUN 0xB0 //cracking wall
	STAL 30
	CAM1 Alm
	FlashCursor(Alm, 60)
	Text(Ch3_7A_SluiceGateCracking)
	NoFade
	ENDA
	
SluiceGateWallCrack2:
	CAM1 Tatarrah
	EARTHQUAKE_START 0x100
	STAL 60
	EARTHQUAKE_END
	TILECHANGE 0x7 //wall left 1
	STAL 30
	SOUN 0xB0 //cracking wall
	STAL 30
	NoFade
	ENDA
	
SluiceGateWallCrack3:
	CAM1 Tatarrah
	EARTHQUAKE_START 0x100
	STAL 60
	EARTHQUAKE_END
	TILECHANGE 0x8 //wall right 2
	STAL 30
	SOUN 0xB0 //cracking wall
	STAL 30
	NoFade
	ENDA
	
SluiceGateWallCrack4:
	CAM1 Tatarrah
	EARTHQUAKE_START 0x100
	STAL 60
	EARTHQUAKE_END
	TILECHANGE 0x9 //wall left 2
	STAL 30
	SOUN 0xB0 //cracking wall
	STAL 30
	NoFade
	ENDA
	
SluiceGateFlood:
	CAM1 Tatarrah
	FlashCursor(Tatarrah, 60)
	EARTHQUAKE_START 0x100
	STAL 60
	Text(Ch3_7A_Flood)
	FADI 2
	ASMC 0x0800D391 //game over
	ENDB

Ch3_7A_CharacterBasedEvents:
	CHAR

Ch3_7A_LocationBasedEvents:
	#ifdef __DEBUG__
	  Seize(10,20)
	#endif
	Seize(12,5)
	LOCA

Ch3_7A_MiscBasedEvents:
	CauseGameOverIfLordDies
	AFEV 0x21 DeltheaWakesUp 0x2
	MournQuoteAFEVS
	AFEV

Ch3_7A_Dunno:
	END_MAIN

Ch3_7A_Tutorial:
	END_MAIN

Ch3_7A_Ballista1:
	BLST
	ALIGN 4

Ch3_7A_Ballista2:
	BLST
	ALIGN 4

Ch3_7A_Units1:
#ifdef __DEBUG__
	UNIT Alm AlmFighter 0x00 Level(15,Ally,True) [10,20] 0x00 0x00 0x0 0x00 [BaseSword, RoyalSword, Vulnerary] NoAI 
	UNIT Luthier Mage 0x00 Level(12,Ally,True) [11,21] 0x00 0x00 0x0 0x00 [Fire, Vulnerary] NoAI 
	UNIT Tobin Sniper 0x00 Level(8,Ally,True) [8,20] 0x00 0x00 0x0 0x00 [BaseBow, HolyBow, Vulnerary] NoAI 
	UNIT Gray Myrmidon 0x00 Level(8,Ally,True) [9,21] 0x00 0x00 0x0 0x00 [BaseSword, Zweihander, Vulnerary] NoAI 
	UNIT Kliff Sage 0x00 Level(2,Ally,True) [7,21] 0x00 0x00 0x0 0x00 [Fire, Vulnerary] NoAI
	UNIT Mathilda Paladin_F 0x00 Level(5,Ally,True) [6,22] 0x00 0x00 0x0 0x00 [BaseLance,Ridersbane] NoAI
	UNIT Silque Cleric 0x00 Level(15,Ally,True) [8,22] 0x00 0x00 0x0 0x00 [Nosferatu, Vulnerary] NoAI
	UNIT Clair PegasusKnight 0x00 Level(15,Ally,True) [10,22] 0x00 0x00 0x0 0x00 [BaseLance,SteelLance] NoAI
	UNIT Clive Paladin 0x00 Level(7,Ally,True) [12,22] 0x00 0x00 0x0 0x00 [BaseLance,SteelLance,HeavySpear] NoAI
	UNIT Forsyth Knight 0x00 Level(8,Ally,True) [13,21] 0x00 0x00 0x0 0x00 [BaseAxe,BaseLance,Forscythe] NoAI
	UNIT Python Archer 0x00 Level(15,Ally,True) [14,22] 0x00 0x00 0x0 0x00 [BaseBow, SteelBow] NoAI
	UNIT Acantha Thief_F 0x00 Level(15,Ally,True) [15,21] 0x00 0x00 0x0 0x00 [BaseSword,Lockpick,Thunderblade] NoAI
	UNIT
#else
	UNIT Alm AlmFighter 0x00 Level(1, Ally, 1) [10,20] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI 
	UNIT Luthier Mage 0x00 Level(1, Ally, 1) [11,21] 0x00 0x00 0x0 0x00 [Fire, Vulnerary] NoAI 
	UNIT Tobin Villager 0x00 Level(1, Ally, 1) [8,20] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI 
	UNIT Gray Villager 0x00 Level(1, Ally, 1) [9,21] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI 
	UNIT Kliff Villager 0x00 Level(1, Ally, 1) [7,21] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI
	UNIT Faye Villager_F 0x00 Level(1, Ally, 1) [6,22] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI
	UNIT Silque Cleric 0x00 Level(1, Ally, 1) [8,22] 0x00 0x00 0x0 0x00 [Nosferatu, Vulnerary] NoAI
	UNIT Clair PegasusKnight 0x00 Level(1, Ally, 1) [10,22] 0x00 0x00 0x0 0x00 [BaseLance] NoAI
	UNIT Clive Cavalier 0x00 Level(1, Ally, 1) [12,22] 0x00 0x00 0x0 0x00 [BaseLance] NoAI
	UNIT Forsyth Soldier 0x00 Level(1, Ally, 1) [13,21] 0x00 0x00 0x0 0x00 [BaseLance] NoAI
	UNIT Python Archer 0x00 Level(1, Ally, 1) [14,22] 0x00 0x00 0x0 0x00 [BaseBow] NoAI
	UNIT Acantha Thief_F 0x00 Level(1, Ally, 1) [15,21] 0x00 0x00 0x0 0x00 [BaseSword] NoAI
	UNIT
#endif

CutsceneUnits:
	UNIT Alm AlmFighter 0x00 Level(1, Ally, 1) [9,22] 0x00 0x00 0x1 REDA10R20 [BaseSword, Vulnerary] NoAI 
	UNIT Lukas Soldier 0x00 Level(1, Ally, 1) [10,22] 0x00 0x00 0x1 REDA11R21 [BaseLance, Vulnerary] NoAI 
	UNIT Luthier Mage 0x00 Level(1, Ally, 1) [9,22] 0x00 0x00 0x1 REDA9R21 [Fire, Vulnerary] NoAI 
	UNIT Clive Cavalier 0x00 Level(1, Ally, 1) [10,22] 0x00 0x00 0x1 REDA12R22 [BaseLance] NoAI
	UNIT
	
CutsceneBosses:
	UNIT Tatarrah Arcanist 0x00 Level(5,Enemy,False) [12,5] 0x00 0x00 0x0 0x00 [Thanatos, Fortify] NeverMoveAI
	UNIT DeltheaBoss Mage_F 0x00 Level(3,Enemy,False) [12,6] 0x00 0x00 0x0 0x00 [Aura, PrayerRing] NoAI
	UNIT

EnemyUnits:
	//Boss room
	UNIT Tatarrah Arcanist 0x00 Level(5,Enemy,False) [12,5] 0x00 0x00 0x0 0x00 [Thanatos, Fortify] NeverMoveAI
	UNIT DeltheaBoss Mage_F 0x00 Level(3,Enemy,False) [12,6] 0x00 0x00 0x0 0x00 [Aura, PrayerRing] NoAI
	UNIT 0x89 Sniper Tatarrah Level(1,Enemy,True) [13,7] 0x00 0x00 0x0 0x00 [SilverBow] AttackInRangeAI
	UNIT 0x88 Knight Tatarrah Level(7,Enemy,True) [13,5] 0x00 0x00 0x0 0x00 [SteelAxe] AttackInRangeAI
	UNIT 0x92 Shaman Tatarrah Level(14,Enemy,True) [11,5] 0x00 0x00 0x0 0x00 [Mire] [0x0,0x12,0x0,0x0]
	UNIT 0x94 Witch Tatarrah Level(7,Enemy,True) [11,7] 0x00 0x00 0x0 0x00 [Thunder, Rewarp] WitchWarpAI
	
	//Left hall
	UNIT 0x89 Archer_F Tatarrah Level(12,Enemy,True) [6,7] 0x00 0x00 0x0 0x00 [IronBow] AttackInRangeAI
	UNIT 0x92 Mage Tatarrah Level(12,Enemy,True) [6,6] 0x00 0x00 0x0 0x00 [Thunder] AttackInRangeAI
	
	//Main hall
	UNIT 0x86 PegasusKnight Tatarrah Level(11,Enemy,True) [5,3] 0x00 0x00 0x0 0x00 [IronLance] AttackInRangeAI
    UNIT 0x86 PegasusKnight Tatarrah Level(12,Enemy,True) [11,3] 0x00 0x00 0x0 0x00 [SteelLance] AttackInRangeAI
	UNIT 0x87 Myrmidon Tatarrah Level(5,Enemy,True) [15,3] DropItem 0x00 0x0 0x00 [Zweihander,SteelShield] NeverMoveAI

    UNIT 0x92 Arcanist Tatarrah Level(7,Enemy,True) [8,4] 0x00 0x00 0x0 0x00 [Miasma] AttackInRangeAI
    UNIT 0x92 Arcanist Tatarrah Level(7,Enemy,True) [9,5] 0x00 0x00 0x0 0x00 [Miasma] AttackInRangeAI
    UNIT 0x94 Witch Tatarrah Level(8,Enemy,True) [9,7] 0x00 0x00 0x0 0x00 [Nosferatu,IronShield] AttackInRangeAI
	UNIT 0x92 Arcanist Tatarrah Level(7,Enemy,True) [8,8] 0x00 0x00 0x0 0x00 [Selene,IronShield] AttackInRangeAI
	
	//Bridge
	UNIT 0x93 Arcanist Tatarrah Level(6,Enemy,True) [17,14] 0x00 0x00 0x0 0x00 [Selene,Mire,Physic] NeverMoveAI
	UNIT 0x92 Shaman Tatarrah Level(16,Enemy,True) [16,14] DropItem 0x00 0x0 0x00 [Miasma,Elixir] NeverMoveAI
	UNIT 0x87 Myrmidon Tatarrah Level(7,Enemy,True) [16,16] 0x00 0x00 0x0 0x00 [Rapier,IronShield] AttackInRangeAI
    UNIT 0x94 Witch Tatarrah Level(8,Enemy,True) [17,17] 0x00 0x00 0x0 0x00 [Thunder] AttackInRangeAI
	
	//Frontline
	UNIT 0x8A WyvernRider Tatarrah Level(14,Enemy,True) [6,10] 0x00 0x00 0x0 0x00 [SteelAxe] AttackInRangeAI
	UNIT 0x8A WyvernRider Tatarrah Level(14,Enemy,True) [11,10] 0x00 0x00 0x0 0x00 [IronAxe] NoAI
	UNIT 0x8A WyvernRider Tatarrah Level(15,Enemy,True) [14,10] 0x00 0x00 0x0 0x00 [SteelAxe,HandAxe] AttackInRangeAI

	UNIT 0x88 Knight Tatarrah Level(7,Enemy,True) [16,11] DropItem 0x00 0x0 0x00 [IronLance,PureWater] AttackInRangeAI
	UNIT 0x89 Sniper Tatarrah Level(7,Enemy,True) [17,11] 0x00 0x00 0x0 0x00 [Longbow] AttackInRangeAI

	UNIT 0x87 Myrmidon Tatarrah Level(6,Enemy,True) [8,10] 0x00 0x00 0x0 0x00 [Lancereaver] NoAI
	UNIT 0x87 Myrmidon Tatarrah Level(6,Enemy,True) [9,10] 0x00 0x00 0x0 0x00 [SteelSword] NoAI
	
	UNIT

Reinforcements1:
	UNIT 0x8A WyvernRider	Tatarrah Level(16, Enemy, True) [19,10]	0x00 0x00 0x1 REDA19R10 [SteelAxe] NoAI
	UNIT
	
DeltheaJoins:
	UNIT Delthea Mage_F	0x0 Level(3, Ally, True) [12,6]	0x00 0x00 0x0 0x00 [Fire,PrayerRing] NoAI
	UNIT

Ch3_7A_BeginningScene:
	MUSC 0x15
	LOAD1 0x1 EnemyUnits
	ENUN
	FADU 5
	SVAL 0xA 0x0
	CHECK_EXISTS Luthier
	SADD 0xCA
	LOAD2 0x1 CutsceneUnits
	ENUN
	CAM1 [12, 5]
	STAL 30
	CAM1 Alm
	FlashCursor(Alm, 60)
	Text(Ch3_7A_OpeningText)
	
	SVAL 0x2 Tatarrah
	_WARP 0xFFFF 0xFFFD [9,17]
	CALL Warp_In_Effect
	STAL 20
	SVAL 0x2 DeltheaBoss
	_WARP 0xFFFF 0xFFFD [8,17]
	CALL Warp_In_Effect
	STAL 20
	
	FlashCursor(Tatarrah, 60)
	Text(Ch3_7A_OpeningText_2)

	SVAL 0x2 Tatarrah
	CALL Warp_Out_Effect
	STAL 0x08
	DISA Tatarrah
	
	CAM1 DeltheaBoss
	MOVE 0x08 DeltheaBoss [10, 18]
	ENUN
		
	StartBattle
	MissedAttack(0,0)
	EndAttack
	FIG1 DeltheaBoss Alm Aura
	
	CHECK_EXISTS Luthier
	BNE 0x3 0xC 0xA
		
		Text(Ch3_7A_OpeningText_LuthierExists)
		CHECK_ALIVE Luthier
		BEQ 0x4 0xC 0x0
			CAM1 Luthier
			FlashCursor(Luthier, 60)
			Text(Ch3_7A_OpeningText_LuthierAlive)
			GOTO 0x4
	LABEL 0x3
		Text(Ch3_7A_OpeningText_NoLuthier)
	LABEL 0x4
	STAL 20
	SVAL 0x2 DeltheaBoss
	CALL Warp_Out_Effect
	STAL 0x08
	DISA DeltheaBoss
	
	FADI 5
	CLEA 0x0
	CLEE 0x0
	LOAD1 0x1 EnemyUnits
	ENUN
	#ifdef __DEBUG__
		LOAD1 0x1 Ch3_7A_Units1
		ENUN
	#else
		LOAD3 0x0 Ch3_7A_Units1 
		ENUN
		GotoPrepScreen
	#endif
	ENDA
	
DeltheaWakesUp:
	CHECK_ALIVE DeltheaBoss
	BEQ 0x2 0xC 0x0
		ENUT DeltheaLocalFlag
		MUSI
		CAM1 DeltheaBoss
		STAL 30
		FlashCursor(DeltheaBoss, 60)
		Text(Ch3_7A_DeltheaWakesUp)
		NoFade
		STAL 20
		CUSN DeltheaBoss
		STAL 20
		SVAL 0x2 DeltheaBoss
		CALL Warp_Out_Effect
		STAL 0x08
		DISA DeltheaBoss
		MUNO
	LABEL 0x2
	NoFade
	ENDA

Ch3_7A_EndingScene:
	CALL UnifiedBEXPCaller
	FADI 2
	EVBIT_MODIFY 0x1
	ENUT gChapter3A7Completed
	EVBIT_MODIFY 0x0
	SetBackground(0x0B)
	CHECK_ALIVE Luthier
	BEQ 0x4 0xC 0x0
		CHECK_EVENTID DeltheaLocalFlag
		BEQ 0x5 0xC 0x0
			MUSC 0x2F
			TEXTSTART
			TEXTSHOW Ch3_7A_EndingText //both alive
			TEXTEND
			FADI 2
			STAL 20
			FADU 2
			TEXTCONT
			TEXTEND
			MUSC 0x3B
			TEXTCONT
			TEXTEND
			REMA
			FADI 2
			EVBIT_MODIFY 0x1
			LOAD1 0x1 DeltheaJoins
			ENUN
			CharacterJoinedNotification(Notif_DeltheaJoin)
			EVBIT_MODIFY 0x0
			GOTO 0x6
		LABEL 0x5
			MUSC 0x3A
			Text(Ch3_7A_Ending_LuthierOnly) //luthier alive, delthea dead
			FADI 2
			GOTO 0x6
	LABEL 0x4 //luthier dead
	CHECK_EVENTID DeltheaLocalFlag
		BEQ 0x6 0xC 0x0
		MUSC 0x30
		TEXTSTART
		TEXTSHOW Ch3_7A_Ending_DeltheaOnly //only delthea
		TEXTEND
		FADI 2
		STAL 20
		FADU 2
		TEXTCONT
		TEXTEND
		MUSC 0x3B
		TEXTCONT
		TEXTEND
		REMA
		FADI 2
		EVBIT_MODIFY 0x1
		LOAD1 0x1 DeltheaJoins
		ENUN
		CharacterJoinedNotification(Notif_DeltheaJoin)
		EVBIT_MODIFY 0x0
	LABEL 0x6
	EVBIT_MODIFY 0x1
	CHECK_EVENTID gChapter3C7Completed
	BEQ 0x13 0xC 0x0
		//If gChapter3A7Completed
		CALL Ch3_EndingScene
	//else
	LABEL 0x13
	EVBIT_MODIFY 0x0
	SetBackground(0x0B)
	Text(Ch3_EndingText_SluiceGate_NotOpen)
	FADI 2
	
	MNCH 0x1E
	SHORT $0D40 $0
	POIN Act3EndSwitch|1
	ENDA
