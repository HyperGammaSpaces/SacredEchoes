//Made by CrazyColorz5 of Fire Emblem Universe
//Based off of Camtech75's Skeleton
#include EAstdlib.event

EventPointerTable(plCh3C_3_Event, Ch3_3C_ThisChapter)

Ch3_3C_ThisChapter:
	POIN Ch3_3C_TurnBasedEvents
	POIN Ch3_3C_CharacterBasedEvents
	POIN Ch3_3C_LocationBasedEvents
	POIN Ch3_3C_MiscBasedEvents
	POIN Ch3_3C_Dunno Ch3_3C_Dunno Ch3_3C_Dunno
	POIN Ch3_3C_Tutorial
	POIN Ch3_3C_Ballista1 Ch3_3C_Ballista2
	POIN PlayerPlacement PlayerPlacement
	POIN $0 $0 $0 $0 $0 $0
	POIN Ch3_3C_BeginningScene Ch3_3C_EndingScene

Ch3_3C_TurnBasedEvents:
	TurnEventPlayer(0, LoadJesse, 3)
	TURN 0x0 LoadReinforcements [5,7] 0x0
	TURN 0x0 LoadReinforcementShamans [7,9] 0x0
	TURN

LoadReinforcements:
	ReinforcementEvent(Reinforcements1)
	NoFade
	ENDA
	
LoadReinforcementShamans:
	ReinforcementEvent(Reinforcements2)
	NoFade
	ENDA

LoadJesse:
	LoadReinforcements(JesseJoins)
	FlashCursor(Jesse, 60)
    MUSI
	MapText(Ch3_3C_JesseAppears)
    MUNO
	NoFade
	ENDA

//Have Jesse pop out of the stairs on turn 3 or 4
//Seize map
//Add ballista in bottom right
//Split party starting positions in 3
//Wall of 3 mercs blocking the entrance, backed up by some sorcerers, the rest are archers.
//Wolff has a steel bow


Ch3_3C_CharacterBasedEvents:
	CharacterEvent(gJesseRecruited,TalkToJesse,Celica,Jesse)
	CHAR
	
TalkToJesse:
	MUSS SongID_Recruitment
	STAL 0x20
	MapText(Ch3_3C_RecruitJesse)
	SVAL 0x1 0x0
	CHAI Jesse
	STAL 5
	CUSA Jesse
	STAL 0x14
	CharacterJoinedNotification(Notif_JesseJoin)
	MURE 0x05
	NoFade
	ENDA

Ch3_3C_LocationBasedEvents:
	#ifdef __DEBUG__
	  Seize(2,12)
	#endif
	Seize(11,3)
	Door(3, 4)
	Door(4, 2)
	Door(19, 5)
	Chest(SteelLance, 2, 2)
	LOCA

Ch3_3C_MiscBasedEvents:
	CauseGameOverIfLordDies
	MournQuoteAFEVS
	AFEV

Ch3_3C_Dunno:
	END_MAIN

Ch3_3C_Tutorial:
	END_MAIN

Ch3_3C_Ballista1:
	BLST [21,13] 0x35
	BLST
	ALIGN 4

Ch3_3C_Ballista2:
	BLST
	ALIGN 4

PlayerPlacement:
	UNIT Celica CelicaPriestess 0x00 Level(1, Ally, 1) [2,12] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI 
	UNIT Mae Mage_F 0x00 Level(1, Ally, 1) [3,13] 0x00 0x00 0x0 0x00 [Fire, Vulnerary] NoAI 
	UNIT Mae Mage_F 0x00 Level(1, Ally, 1) [2,14] 0x00 0x00 0x0 0x00 [Fire, Vulnerary] NoAI
	UNIT Mae Mage_F 0x00 Level(1, Ally, 1) [3,15] 0x00 0x00 0x0 0x00 [Fire, Vulnerary] NoAI
	UNIT Mae Mage_F 0x00 Level(1, Ally, 1) [10,15] 0x00 0x00 0x0 0x00 [Fire, Vulnerary] NoAI
	UNIT Mae Mage_F 0x00 Level(1, Ally, 1) [11,15] 0x00 0x00 0x0 0x00 [Fire, Vulnerary] NoAI
	UNIT Mae Mage_F 0x00 Level(1, Ally, 1) [12,15] 0x00 0x00 0x0 0x00 [Fire, Vulnerary] NoAI
	UNIT Mae Mage_F 0x00 Level(1, Ally, 1) [18,12] 0x00 0x00 0x0 0x00 [Fire, Vulnerary] NoAI
	UNIT Mae Mage_F 0x00 Level(1, Ally, 1) [19,13] 0x00 0x00 0x0 0x00 [Fire, Vulnerary] NoAI
	UNIT Mae Mage_F 0x00 Level(1, Ally, 1) [18,14] 0x00 0x00 0x0 0x00 [Fire, Vulnerary] NoAI
	UNIT Mae Mage_F 0x00 Level(1, Ally, 1) [19,15] 0x00 0x00 0x0 0x00 [Fire, Vulnerary] NoAI
	UNIT Kliff Villager 0x00 Level(1,Ally,True) [11,14] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI 
    UNIT

EnemyUnits:
	UNIT Wolff Sniper Wolff Level(6, Enemy, False) [11,3] 0x00 0x00 0x0 0x00 [SteelBow] NeverMoveAI
	UNIT Bandit_Bow Sniper	Wolff Level(5, Enemy, True) [21,13]	0x00 0x00 0x0 0x00 [IronBow] NoAI
	UNIT Bandit_Bow Sniper	Wolff Level(5, Enemy, True) [3,7]	0x00 0x00 0x0 0x00 [IronBow] NoAI
	UNIT Enemy_Mercenary Mercenary	Wolff Level(12, Enemy, True) [10,8]	0x00 0x00 0x0 0x00 [Rapier, IronShield] NeverMoveAI
	UNIT Enemy_Mercenary Mercenary	Wolff Level(12, Enemy, True) [11,8]	0x00 0x00 0x0 0x00 [BraveSword, IronShield] NeverMoveAI
	UNIT Enemy_Mercenary Mercenary	Wolff Level(12, Enemy, True) [12,8]	0x00 0x00 0x0 0x00 [Zweihander, IronShield] NeverMoveAI
	UNIT Bandit_Bow Archer	Wolff Level(15, Enemy, True) [10,6]	0x00 0x00 0x0 0x00 [IronBow] AttackInRangeAI
	UNIT Bandit_Bow Archer	Wolff Level(13, Enemy, True) [12,6]	0x00 0x00 0x0 0x00 [SteelBow] AttackInRangeAI
	UNIT Enemy_Mercenary Mercenary	Wolff Level(13, Enemy, True) [19,6]	DropItem 0x00 0x0 0x00 [Lancereaver,SteelShield, DoorKey] GuardTileAI
	UNIT Bandit_Axe Pirate	Wolff Level(15, Enemy, True) [18,2]	0x00 0x00 0x0 0x00 [SteelAxe] GuardTileAI	
	UNIT Enemy_Mercenary Mercenary	Wolff Level(13, Enemy, True) [17,3]	0x00 0x00 0x0 0x00 [Zweihander] GuardTileAI	
	UNIT Bandit_Axe Pirate	Wolff Level(11, Enemy, True) [2,3]	0x00 0x00 0x0 0x00 [SteelAxe] [0xC, 0x3, 0x0, 0x0]
	UNIT Bandit_Bow Archer	Wolff Level(15, Enemy, True) [8,4]	0x00 0x00 0x0 0x00 [SteelBow] DefensiveTerrainAI
	UNIT Bandit_Bow Archer	Wolff Level(15, Enemy, True) [14,4]	0x00 0x00 0x0 0x00 [IronBow] DefensiveTerrainAI
	UNIT DumaFaith_Sorcerer Shaman	Wolff Level(9, Enemy, True) [10,7]	0x00 0x00 0x0 0x00 [Miasma] NoAI
	UNIT DumaFaith_Sorcerer Shaman	Wolff Level(9, Enemy, True) [11,7]	0x00 0x00 0x0 0x00 [Miasma, Heal] MoveAndHealAI
	UNIT DumaFaith_Sorcerer Shaman	Wolff Level(9, Enemy, True) [12,7]	0x00 0x00 0x0 0x00 [Miasma] NoAI
	UNIT Bandit_Bow Archer	Wolff Level(13, Enemy, True) [7,6]	0x00 0x00 0x0 0x00 [IronBow] NeverMoveAI
	UNIT Bandit_Bow Archer	Wolff Level(15, Enemy, True) [15,6]	0x00 0x00 0x0 0x00 [IronBow] NeverMoveAI
	UNIT

Reinforcements1:
	UNIT Enemy_Mercenary Mercenary	Wolff Level(13, Enemy, True) [11,16]	0x00 0x00 0x1 REDA11R16 [BaseSword] NoAI
	UNIT Enemy_Mercenary Mercenary	Wolff Level(12, Enemy, True) [12,16]	0x00 0x00 0x1 REDA12R16 [IronSword] NoAI
	UNIT
	
Reinforcements2:
	UNIT DumaFaith_Sorcerer Shaman	Wolff Level(11, Enemy, True) [23,9]	0x00 0x00 0x1 REDA23R9 [Miasma,Heal] MoveAndHealAI
	UNIT DumaFaith_Sorcerer Shaman	Wolff Level(10, Enemy, True) [23,10]	0x00 0x00 0x1 REDA23R9 [Miasma] NoAI
	UNIT
	
JesseJoins:
	UNIT Jesse Thief 0x00 Level(6, NPC, True) [7,2]	0x00 0x00 0x1 DelayedREDA6R2 [BaseSword,Rapier,Lockpick] StealingThiefAI
	UNIT
	
CutsceneUnits_Grieth:
	UNIT Grieth DreadFighter Grieth Level(5, Enemy, False) [20,2] 0x00 0x00 0x0 0x00 [SteelSword] NeverMoveAI
	UNIT
	
CutsceneUnits_GriethLackey:
	UNIT Bandit_Axe Pirate	Grieth Level(17, Enemy, True) [17,5]	0x00 0x00 0x1 DelayedREDA20R3 [IronAxe] NoAI
	UNIT
	
CutsceneAlliesNoJesse:
	UNIT Celica CelicaPriestess 0x00 Level(1, Ally, 1) [11,3] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI 
	UNIT
	
CutsceneAlliesJesse:
	UNIT Celica CelicaPriestess 0x00 Level(1, Ally, 1) [11,10] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI 
	UNIT Boey Mage 0x00 Level(1, Ally, 1) [10,10] 0x00 0x00 0x0 0x00 [Fire, Vulnerary] NoAI
	UNIT Jesse Thief 0x00 Level(6, Ally, True) [12,10]	0x00 0x00 0x0 0x00 [BaseSword,Rapier,Lockpick] NoAI
	UNIT
	
DelayedREDA6R2:
REDA [6,2] NoFlags Walk_Slow 0x0 0x10
DelayedREDA20R3:
REDA [20,3] NoFlags Walk_Slow 0x0 0x10

Ch3_3C_BeginningScene:
	MUSC SongID_SolveTheRiddle
	LOAD1 0x1 EnemyUnits
	ENUN
	FADU 5
	STAL 20
	CAMERA_CENTERED Wolff
	STAL 20
	FlashCursor(Wolff, 60)
	STAL 20
	SetBackground(0x28) //crumbly fort interior
	BackgroundText(Ch3_3C_OpeningText)
	FADI 5
	CLEAN
	LOAD3 0x0 PlayerPlacement 
	ENUN
	GotoPrepScreen
	ENDA

Ch3_3C_EndingScene:
	EVBIT_MODIFY 0x1
	ENUT gChapter3C3Completed
//debug
	CHECK_EVENTID 0x8
	BEQ 0x9 rC r0
		ENUT gJesseRecruited
	LABEL 0x9
//end debug
	EVBIT_MODIFY 0x0
    CALL EndMusicAndMMS
	CALL UnifiedBEXPCaller
    FADI 2
	STAL 5
	MUSC SongID_SolveTheRiddle
	//Switch to Grieth's Fortress
	
	//LOMA chapterID will load the map associated with the given chapter and sets the camera given the position in sB
		CLEA 0x0
		CLEE 0x0
		CLEN 0x0
		SVAL 0xB Coords(20, 2)
		LOMA 0x1B
		LOAD1 0x1 CutsceneUnits_Grieth
		ENUN
		FADU 5
		
		BROWNBOXTEXT Loc_GriethCitadel [8,8]
		STAL 20
		LOAD1 0x1 CutsceneUnits_GriethLackey
		ENUN
		STAL 20
		FlashCursor(Grieth, 60)
		STAL 20
	
	SetBackground(0x0C)
	BackgroundText(Ch3_3C_EndingText_Grieth)
    FadeOutMusic
	FADI 2
    CLEAN
	
	//Put us back to the desert fortress
	CLEE 0x0
	STAL 5
	CHECK_EVENTID gJesseRecruited
	BEQ 0x2 rC r0
		CHECK_ALIVE Jesse
		BEQ 0x2 rC r0
		
		SVAL 0xB Coords(11, 10)
		LOMA 0x18
		
		MUSC SongID_Victory
		LOAD2 0x1 CutsceneAlliesJesse
		ENUN
		FADU 5
		BROWNBOXTEXT Loc_DesertBastion [8,8]
		STAL 20
		FlashCursor(Celica, 60)
		STAL 20
		
		SetBackground(0x28) //crumbly fort interior
		BackgroundText(Ch3_3C_EndingText)
		GOTO 0x3
	LABEL 0x2
		SVAL 0xB Coords(7, 2)
		LOMA 0x18
	
		MUSC SongID_Victory
		LOAD2 0x1 CutsceneAlliesNoJesse
		ENUN
		FADU 5
		BROWNBOXTEXT Loc_DesertBastion [8,8]
		STAL 20
		FlashCursor(Celica, 60)
		
		MOVE 0x10 Celica [7, 2]
		ENUN
		CLEA 0x0
		STAL 4
		FADI 5
		
		SetBackground(0x17) //dungeon cell
		BackgroundText(Ch3_3C_EndingText_NoJesse)
	LABEL 0x3
	
	//Call a lordsplit menu and store the result to gFightSonyaChosen.
	STAL 20
	EVBIT_MODIFY 0x1
	SetBackground(0x28)
	ASMC prCallDeenSonyaEventMenu+1 // Call the Split Menu
	
	SVAL r1 True 
	BNE 0x0 rC r1 
		//Sonya
		ENUT gFightSonyaChosen
		EVBIT_MODIFY 0x0
		CHECK_EVENTID gJesseRecruited
		BEQ 0x4 rC r0
		CHECK_ALIVE Jesse
		BEQ 0x4 rC r0
			BackgroundText(Ch3_3C_EndingText_SonyaChosen)
			GOTO 0x5
		LABEL 0x4
			BackgroundText(Ch3_3C_EndingText_SonyaChosen_NoJesse)
		LABEL 0x5
		FADI 2
		MNCH 0x1A
		GOTO 0x1
	LABEL 0x0
		//Deen
		ENUF gFightSonyaChosen
		EVBIT_MODIFY 0x0
		CHECK_EVENTID gJesseRecruited
		BEQ 0x6 rC r0
		CHECK_ALIVE Jesse
		BEQ 0x6 rC r0
			BackgroundText(Ch3_3C_EndingText_DeenChosen)
			GOTO 0x7
		LABEL 0x6
			BackgroundText(Ch3_3C_EndingText_DeenChosen_NoJesse)
		LABEL 0x7
		FADI 2
		MNCH 0x19
	LABEL 0x1
	ENDA
