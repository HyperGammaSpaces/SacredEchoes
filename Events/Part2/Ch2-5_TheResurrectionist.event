#include EAstdlib.event

EventPointerTable(plCh2_5_Event, Ch2_5_ThisChapter)

Ch2_5_ThisChapter:
	POIN Ch2_5_TurnBasedEvents
	POIN Ch2_5_CharacterBasedEvents
	POIN Ch2_5_LocationBasedEvents
	POIN Ch2_5_MiscBasedEvents
	POIN Ch2_5_Dunno Ch2_5_Dunno Ch2_5_Dunno
	POIN Ch2_5_Tutorial
	POIN Ch2_5_Ballista1 Ch2_5_Ballista3
	POIN PlayerUnits PlayerUnits
	POIN $0 $0 $0 $0 $0 $0
	POIN Ch2_5_BeginningScene Ch2_5_EndingScene

Ch2_5_TurnBasedEvents:
	TurnEventEnemy(0x00, CastingSummon, 1)
	TURN 0x0 LoadSummons [1,255] 0x0
	TURN

Ch2_5_CharacterBasedEvents:
	CHAR

Ch2_5_LocationBasedEvents:
	#ifdef __DEBUG__
	  Seize(5,12)
	#endif
	Chest(0x5D, 15, 12) //skill statbooster
	LOCA

Ch2_5_MiscBasedEvents:
	CauseGameOverIfLordDies
	DefeatAll(Ch2_5_EndingScene)
	MournQuoteAFEVS
	AFEV

Ch2_5_Dunno:
	END_MAIN

Ch2_5_Tutorial:
	END_MAIN

Ch2_5_Ballista1:
	BLST

Ch2_5_Ballista3:
	BLST
	ALIGN 4

PlayerUnits:
#ifdef __DEBUG__
	UNIT Celica CelicaPriestess 0x00 Level(6,Ally,True) [5,12] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI 
	UNIT Mae Mage_F 0x00 Level(5,Ally,True) [5,11] 0x00 0x00 0x0 0x00 [Fire, Vulnerary] NoAI 
	UNIT Boey Mage 0x00 Level(5,Ally,True) [5,10] 0x00 0x00 0x0 0x00 [Fire, Vulnerary] NoAI 
	UNIT Genny Cleric 0x00 Level(5,Ally,True) [4,12] 0x00 0x00 0x0 0x00 [Nosferatu, Vulnerary] NoAI 
	UNIT Saber Mercenary 0x00 Level(5,Ally,True) [4,11] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI 
	UNIT Valbar Knight 0x00 Level(2,Ally,True) [3,10] 0x00 0x00 0x0 0x00 [BaseLance,BaseAxe,Vulnerary] NoAI
	UNIT Leon Archer 0x00 Level(5,Ally,True) [3,11] 0x00 0x00 0x0 0x00 [IronBow, BaseBow, Vulnerary] NoAI
	UNIT Kamui Mercenary 0x00 Level(4,Ally,True) [4,8] 0x00 0x00 0x0 0x00 [BaseSword] NoAI 
	UNIT
#else
	UNIT Celica CelicaPriestess 0x00 Level(1,Ally,True) [5,12] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI 
	UNIT Mae Mage_F 0x00 Level(1,Ally,True) [5,11] 0x00 0x00 0x0 0x00 [Fire, Vulnerary] NoAI 
	UNIT Boey Mage 0x00 Level(1,Ally,True) [5,10] 0x00 0x00 0x0 0x00 [Fire, Vulnerary] NoAI 
	UNIT Genny Cleric 0x00 Level(1,Ally,True) [4,12] 0x00 0x00 0x0 0x00 [Nosferatu, Vulnerary] NoAI 
	UNIT Saber Mercenary 0x00 Level(1,Ally,True) [4,11] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI 
	UNIT Valbar Knight 0x00 Level(1,Ally,True) [3,10] 0x00 0x00 0x0 0x00 [BaseLance,BaseAxe,Vulnerary] NoAI
	UNIT Leon Archer 0x00 Level(4,Ally,True) [3,11] 0x00 0x00 0x0 0x00 [IronBow, BaseBow, Vulnerary] NoAI
	UNIT Kamui Mercenary 0x00 Level(3,Ally,True) [4,8] 0x00 0x00 0x0 0x00 [BaseSword] NoAI 
	UNIT
#endif
	
EnemyUnits:
	UNIT 0x8F Cantor 0x8F Level(1,Enemy,True) [13,5] 0x06 0x00 0x0 0x00 [Miasma, Lifefruit] NeverMoveAI 
	UNIT 0xAA Revenant 0x8F Level(11,Enemy,True) [12,12] 0x04 0x00 0x0 0x00 [RottenClaw] AttackInRangeAI
	UNIT 0xAA Revenant 0x8F Level(9,Enemy,True) [11,8] 0x04 0x00 0x0 0x00 [RottenClaw] AttackInRangeAI
	UNIT 0xAA Revenant 0x8F Level(10,Enemy,True) [13,9] 0x04 0x00 0x0 0x00 [RottenClaw] AttackInRangeAI
	UNIT 0xAA Bonewalker 0x8F Level(9,Enemy,True) [11,13] 0x04 0x00 0x0 0x00 [IronLance] NoAI
	UNIT
	
ZombieGroup:
	UNIT 0xAA Revenant 0x8F Level(8,Enemy,True) [13,6] 0x04 0x00 0x1 REDA13R6 [RottenClaw] NoAI
	UNIT 0xAA Revenant 0x8F Level(8,Enemy,True) [12,5] 0x04 0x00 0x1 REDA12R5 [RottenClaw] NoAI
	UNIT 0xAA Revenant 0x8F Level(8,Enemy,True) [14,5] 0x04 0x00 0x1 REDA14R5 [RottenClaw] NoAI
	UNIT

Ch2_5_BeginningScene:
	#ifdef __DEBUG__
		LOAD1 0x1 PlayerUnits
		ENUN
	#else
		LOAD3 0x0 PlayerUnits 
		ENUN
	#endif
	
	FADU 5
	MUSC 0x4B
	STAL 20
	Text(Ch2_5_OpeningText)
	
	MUSC 0x25
	CAM1 [11,0]
	EARTHQUAKE_START 0x100
	STAL 30
	FADI 16
	STAL 30
	EARTHQUAKE_END
	TILECHANGE 0x0 // Boat
	LOAD1 0x1 EnemyUnits
	ENUN
	FADU 16
	STAL 20
	CAM1 0x8F
	STAL 20
	FlashCursor(0x8F, 60)
	Text(Ch2_5_OpeningText_2)
	
	NoFade
	GotoPrepScreen
	ENDA
	
CastingSummon:
	Text(Ch2_5_SummonText)
	NoFade
	ENDA
	
LoadSummons: //every other turn, shamelessly stolen from VBA for now lol
	CHECK_EVENTID 0x2
	BNE 0x0 0xc 0x0 //stop if boss killed
	CHECK_TURNS //store current turn to slot c
	//Now get remainder mod 2
	SMOV r1 0x2
	SDIV slotsParam(r2, rC, r1) //r2 = rC/2
	SMUL slotsParam(r1, r2)     //r1 = r2*2
	SSUB slotsParam(r1, rC, r1) //r1 -= rC, r1 = remainder
	BNE 0x0 r1 0
	CAM1 0x8F
	ENUN
	FlashCursor(0x8F, 60)
	SOUN 0x3BF //summoning SFX
	STAL 20
	ReinforcementEvent(ZombieGroup) //Remainder was 0 (right)
	LABEL 0x0
	NoFade
	ENDA

Ch2_5_EndingScene:
	CALL UnifiedBEXPCaller
	MUSC 0x31 //victory (good ending)
	SetBackground(0x41) //nightboat
	Text(Ch2_5_PostBattle)
	MNCH 0x0D //go to world map
	ENDA

#ifndef InlineEvents
  MESSAGE Events end at offset currentOffset
#endif