//Made by CrazyColorz5 of Fire Emblem Universe
//Based off of Camtech75's Skeleton
#include EAstdlib.event

#define lFlag_ValbarTalked 0x8
#define lFlag_KamuiTalked 0x9

EventPointerTable(plCh2_4_Event, Ch2_4_ThisChapter)

Ch2_4_ThisChapter:
	POIN Ch2_4_TurnBasedEvents
	POIN Ch2_4_CharacterBasedEvents
	POIN Ch2_4_LocationBasedEvents
	POIN Ch2_4_MiscBasedEvents
	POIN Ch2_4_Dunno Ch2_4_Dunno Ch2_4_Dunno
	POIN Ch2_4_Tutorial
	POIN Ch2_4_Ballista1 Ch2_4_Ballista2
	POIN Ch2_4_Units1 Ch2_4_Units1
	POIN $0 $0 $0 $0 $0 $0
	POIN Ch2_4_BeginningScene Ch2_4_EndingScene

Ch2_4_TurnBasedEvents:
	OpeningTurnEvent(CelicaTalkEvent)
	TURN 0x0 BoatWave[6,0] 0x0
	TURN
	
BoatWave:
	ReinforcementEvent(ReinforcementsSouth)
	END

CelicaTalkEvent:
	MUSIC SongID_MilasDivineProtection
	MUSIC_QUIET_T
	STAL 20
	CAMERA_CENTERED Valbar
	STAL 30
	FlashCursor(Valbar, 60)
	STAL 30
	CAMERA_CENTERED Celica
	STAL 30
	FlashCursor(Celica, 60)
	Text(Ch2_4_PreBattle_CelicaTalk)
	MUSIC_QUIET_F
	NoFade
	END

Ch2_4_CharacterBasedEvents:
	CharacterEvent(lFlag_ValbarTalked, TalkToValbar, Celica, Valbar)
	CharacterEvent(lFlag_KamuiTalked, TalkToKamui, Valbar, Kamui)
	CharacterEvent(lFlag_KamuiTalked, TalkToKamuiCelica, Celica, Kamui)
	CHAR

Ch2_4_LocationBasedEvents:
	#ifdef __DEBUG__
		Seize(2,14)
	#endif
	Chest(IronSword, 18, 0)
	LOCA 0xA VillageEvent [18,10] 0x10
	Seize(15, 1)
	LOCA

VillageEvent:
	MUSIC_QUIET_T
	SetBackground(Inn)
	Text(Ch2_4_PirateInn)
	EndConvo
	MUSIC_QUIET_F
	CALL $591F40
	SMOV 0x3 SteelSword
	GIVEITEM 0xFFFF
	EVBIT_T 0x7
	END
	//VillageEventItem(Ch2_4_PirateInn, 0x09, SteelSword)
	END

Ch2_4_MiscBasedEvents:
	CauseGameOverIfLordDies
	MournQuoteAFEVS
	AFEV

Ch2_4_Dunno:
	END_MAIN

Ch2_4_Tutorial:
	END_MAIN

Ch2_4_Ballista1:
	BLST

Ch2_4_Ballista2:
	BLST
	ALIGN 4

Ch2_4_Units1:
	UNIT Celica CelicaPriestess 0x00 Level(4,Ally,True) [2,14] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI
	UNIT Mae Mage_F 0x00 Level(4,Ally,True) [2,15] 0x00 0x00 0x0 0x00 [Fire, Vulnerary] NoAI
	UNIT Boey Mage 0x00 Level(4,Ally,True) [2,13] 0x00 0x00 0x0 0x00 [Fire, Vulnerary] NoAI
	UNIT Genny Cleric 0x00 Level(4,Ally,True) [1,13] 0x00 0x00 0x0 0x00 [Nosferatu, Vulnerary] NoAI
	UNIT Saber Mercenary 0x00 Level(4,Ally,True) [1,12] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI
	UNIT

ValbarJoins:
	UNIT Valbar Knight 0x00 Level(1, NPC, 1) [6,3] 0x00 0x00 0x0 0x00 [BaseLance,BaseAxe] AttackInRangeAI
	UNIT Leon Archer 0x00 Level(4, NPC, 1) [7,3] 0x00 0x00 0x0 0x00 [IronBow, BaseBow, Vulnerary] [AttackInRange,DoNotMove,DefaultRecovery,GuardTile]
	UNIT Kamui Mercenary 0x00 Level(3, NPC, 1) [7,4] 0x00 0x00 0x0 0x00 [BaseSword,Vulnerary] [AttackInRange,DoNotMove,CautiousRecovery,0x0]
	UNIT

EnemyUnits:
	UNIT Barth Pirate Barth Level(10,Enemy,False) [15,1] DropItem 0x00 0x0 0x00 [SteelAxe,HandAxe,Item100G] NeverMoveAI
	UNIT Bandit_Sword Mercenary Barth Level(6,Enemy,True) [13,1] 0x00 0x00 0x0 0x00 [Armorslayer] NeverMoveAI //Make this guy bulky
	UNIT Bandit_Bow Mage Barth Level(6,Enemy,True) [17,1] DropItem 0x00 0x0 0x00 [Fire, HideShield, ChestKey] NeverMoveAI //Make this guy bulky
	UNIT Bandit_Bow Archer Barth Level(6,Enemy,True) [14,2] 0x00 0x00 0x0 0x00 [IronBow] AttackInRangeAI
	UNIT Bandit_Axe Pirate_F Barth Level(8,Enemy,True) [11,6] 0x00 0x00 0x0 0x00 [BaseAxe] AttackInRangeAI
	UNIT Bandit_Sword Thief Barth Level(7,Enemy,True) [13,7] DropItem 0x00 0x0 0x00 [IronSword] AttackInRangeAI
	UNIT Bandit_Axe Pirate Barth Level(8,Enemy,True) [12,8] 0x00 0x00 0x0 0x00 [BaseAxe] AttackInRangeAI
	UNIT Bandit_Axe Pirate Barth Level(7,Enemy,True) [6,6] 0x00 0x00 0x0 0x00 [IronAxe] AttackInRangeAI
	UNIT Bandit_Axe Pirate Barth Level(7,Enemy,True) [4,5] 0x00 0x00 0x0 0x00 [HandAxe] NeverMoveAI
	UNIT Bandit_Sword Mercenary Barth Level(8,Enemy,True) [17,7] 0x00 0x00 0x0 0x00 [IronSword] AttackInRangeAI
	UNIT Bandit_Bow Archer_F Barth Level(7,Enemy,True) [16,13] 0x00 0x00 0x0 0x00 [SteelBow] AttackInRangeAI
	UNIT Bandit_Axe Pirate Barth Level(7,Enemy,True) [5,1] 0x00 0x00 0x0 0x00 [BaseAxe] AttackInRangeAI
	UNIT Bandit_Axe Pirate_F Barth Level(8,Enemy,True) [15,10] DropItem 0x00 0x0 0x00 [BaseAxe,IronShield] AttackInRangeAI
	UNIT Bandit_Axe Pirate Barth Level(7,Enemy,True) [17,5] 0x00 0x00 0x0 0x00 [BaseAxe] NoAI
	UNIT

EnemyUnits_Hard:
	UNIT Barth Pirate Barth Level(10,Enemy,False) [15,1] DropItem 0x00 0x0 0x00 [SteelAxe,HandAxe,Item100G] NeverMoveAI
	UNIT Bandit_Sword Mercenary Barth Level(6,Enemy,True) [13,1] DropItem 0x00 0x0 0x00 [Armorslayer,IronShield] NeverMoveAI //Make this guy bulky
	UNIT Bandit_Bow Mage Barth Level(6,Enemy,True) [17,1] DropItem 0x00 0x0 0x00 [Fire, HideShield, ChestKey] AttackInRangeAI //Make this guy bulky
	UNIT Bandit_Bow Archer Barth Level(6,Enemy,True) [14,2] 0x00 0x00 0x0 0x00 [IronBow] AttackInRangeAI
	UNIT Bandit_Axe Pirate_F Barth Level(8,Enemy,True) [11,6] 0x00 0x00 0x0 0x00 [IronAxe] AttackInRangeAI
	UNIT Bandit_Sword Thief Barth Level(7,Enemy,True) [13,7] DropItem 0x00 0x0 0x00 [IronSword] AttackInRangeAI
	UNIT Bandit_Axe Pirate Barth Level(8,Enemy,True) [12,8] 0x00 0x00 0x0 0x00 [BaseAxe] AttackInRangeAI
	UNIT Bandit_Axe Pirate Barth Level(7,Enemy,True) [6,6] 0x00 0x00 0x0 0x00 [IronAxe] AttackInRangeAI
	UNIT Bandit_Axe Pirate Barth Level(7,Enemy,True) [4,5] 0x00 0x00 0x0 0x00 [HandAxe] NeverMoveAI
	UNIT Bandit_Sword Mercenary Barth Level(8,Enemy,True) [17,7] 0x00 0x00 0x0 0x00 [IronSword] AttackInRangeAI
	UNIT Bandit_Bow Archer_F Barth Level(7,Enemy,True) [16,13] 0x00 0x00 0x0 0x00 [SteelBow] AttackInRangeAI
	UNIT Bandit_Axe Pirate Barth Level(7,Enemy,True) [5,1] 0x00 0x00 0x0 0x00 [BaseAxe] AttackInRangeAI
	UNIT Bandit_Axe Pirate_F Barth Level(8,Enemy,True) [15,10] DropItem 0x00 0x0 0x00 [IronAxe,IronShield] AttackInRangeAI
	UNIT Bandit_Axe Pirate Barth Level(7,Enemy,True) [17,5] 0x00 0x00 0x0 0x00 [SteelAxe] NoAI
	UNIT

EnemyUnitsREDAs:
	UNIT Bandit_Axe Pirate_F Barth Level(7,Enemy,True) [4,0] 0x00 0x00 0x1 REDA6R10 [BaseAxe] NoAI
	UNIT Bandit_Axe Pirate Barth Level(6,Enemy,True) [4,0] 0x00 0x00 0x1 REDA7R9 [BaseAxe] NoAI
	UNIT Bandit_Axe Pirate_F Barth Level(8,Enemy,True) [4,0] 0x00 0x00 0x1 REDA5R9 [BaseAxe] NoAI
	UNIT Bandit_Bow Archer Barth Level(7,Enemy,True) [4,0] 0x00 0x00 0x1 REDA6R9 [BaseBow] AttackInRangeAI
	UNIT

ReinforcementsSouth:
	UNIT Bandit_Axe Pirate Barth Level(5,Enemy,True) [16,16] 0x00 0x00 0x1 REDA15R16 [IronAxe] NoAI
	UNIT Bandit_Axe Pirate_F Barth Level(7,Enemy,True) [16,16] 0x00 0x00 0x1 REDA16R15 [SteelAxe] NoAI
	UNIT Bandit_Bow Archer Barth Level(6,Enemy,True) [16,16] 0x00 0x00 0x1 REDA17R16 [SteelBow] NoAI
	UNIT

Ch2_4_BeginningScene:
	MUSIC SongID_Tension
	#ifdef __DEBUG__
		LOAD 0x1 Ch2_4_Units1
		ENUN
	#else
		LOAD_DEPLOYED 0x0 Ch2_4_Units1
		ENUN
	#endif
	SVAL 0x2 (EnemyUnits|IsPointer)
	CHECK_ISHARD
	BEQ 0x204 rC r0
		SVAL 0x2 (EnemyUnits_Hard|IsPointer)
	LABEL 0x204
	LOAD_S2 0x1
	ENUN
	LOAD 0x1 ValbarJoins
	ENUN
	CAMERA_CENTERED Celica

	SetBackground(Harbor)
	BackgroundText(Ch2_4_PreBattle)
	EndConvo
	STAL 20

	CAMERA_CENTERED Valbar
	STAL 20
	LOAD 0x1 EnemyUnitsREDAs
	ENUN
	FlashCursor(Valbar, 60)
	STAL 30
	MapText(Ch2_4_PreBattle_Valbar)
	STAL 20
	CAMERA_CENTERED Celica
	STAL 20

	NoFade
	GotoPrepScreen
	END

Ch2_4_EndingScene:
	CALL EndMusicAndMMS
	CALL UnifiedBEXPCaller
	FADEIN 5
	STAL 5
	//Check if Valbar is dead. If he's OK call the following
	CONFIG_SKIP 0x1
	CHECK_ISALIVE Valbar
	BEQ 0x6 0xC 0x0
		CONFIG_SKIP 0x0
		MUSIC SongID_Victory
		FACTION_BLUE Valbar
		FACTION_BLUE Leon
		FACTION_BLUE Kamui
		SetBackground(Harbor)
		TEXTMODE 1
		TEXTSHOW Ch2_4_PostBattle
		TEXTEND
		TEXTCLEAR
		TEXTMODE 1

		BACKGROUND_CHANGE LeonValbarKamuiCG 1 4
		TEXTMODE 1
		STAL 20
		TEXTMODE 2
		TEXTSHOW Ch2_4_CGText
		TEXTEND
		TEXTCLEAR
		STAL 20
		BACKGROUND_CHANGE Harbor 1 4
		TEXTMODE 1
		BackgroundText(Ch2_4_PostBattle_2)
		CALL $591F40
		CONFIG_SKIP 0x1
		CHECK_FLAG lFlag_ValbarTalked
		BEQ 0x9 0xC 0x0
			CharacterJoinedNotification(Notif_ValbarLeonJoin)
		LABEL 0x9
		CHECK_FLAG lFlag_KamuiTalked
		BEQ 0x8 0xC 0x0
			CharacterJoinedNotification(Notif_KamuiJoin)
		LABEL 0x8
		CONFIG_SKIP 0x0
		FADEIN 2
		GOTO 0xB

	LABEL 0x6 //if Valbar is dead
	CONFIG_SKIP 0x0
	MUSIC SongID_ShadowOfVictory
	SetBackground(Harbor)
	BackgroundText(Ch2_4_PostBattle_NoValbar_1)
	CLEAN
	CONFIG_SKIP 0x1
	CHECK_ISALIVE Leon
	BEQ 0x7 0xC 0x0
		CONFIG_SKIP 0x0
		FADEIN 16
		STAL 5
		FACTION_BLUE Leon
		SetBackground(Harbor)
		Text(Ch2_4_PostBattle_NoValbar_Leon)
		CALL $591F40
		CONFIG_SKIP 0x1
		CharacterJoinedNotification(Notif_LeonJoin)
		CONFIG_SKIP 0x0
	LABEL 0x7
	CHECK_ISALIVE Kamui
	BEQ 0xA 0xC 0x0
		CONFIG_SKIP 0x0
		FADEIN 16
		CLEAN
		STAL 5
		FACTION_BLUE Kamui
		SetBackground(Harbor)
		Text(Ch2_4_PostBattle_NoValbar_Kamui)
		CONFIG_SKIP 0x1
		CHECK_FLAG lFlag_KamuiTalked
		BEQ 0xA 0xC 0x0
			CALL $591F40
			CharacterJoinedNotification(Notif_KamuiJoin)
	LABEL 0xA
	CONFIG_SKIP 0x0
	FADEIN 16
	LABEL 0xB
	STAL 5
	GOTO_CHAPTER 0x0C
	END

TalkToValbar:
	MUSIC_REPLACE SongID_Recruitment
	STAL 0x20
	SVAL 0x1 0x1
	CHECK_ISALIVE Leon
	IFEQ 0x2 0xC 0x1 //if not, goto NoFaye
		Text(Ch2_4_GetValbar)
		CALL $591F40
		FACTION_BLUE Leon
		CharacterJoinedNotification(Notif_ValbarLeonJoin)
		GOTO 0x3
	LABEL 0x2
	Text(Ch2_4_GetValbar_NoLeon)
	CALL $591F40
	CharacterJoinedNotification(Notif_ValbarJoin)
	LABEL 0x3
	FACTION_BLUE Valbar
	STAL 0x14
	MUSIC_RESTORE 0x05
	NoFade
	END

TalkToKamui:
	MUSIC_REPLACE SongID_Recruitment
	STAL 0x20
	Text(Ch2_4_GetKamui)
	FACTION_BLUE Kamui
	STAL 0x14
	CALL $591F40
	CharacterJoinedNotification(Notif_KamuiJoin)
	MUSIC_RESTORE 0x05
	NoFade
	END

TalkToKamuiCelica:
	STAL 0x20
	CHECK_ISALIVE Valbar
	BEQ 0x4 0xC 0x0
		Text(Ch2_4_GetKamui_NoValbar)
		FLAG_F lFlag_KamuiTalked
		GOTO 0x5
	LABEL 0x4
	MUSIC_REPLACE SongID_Recruitment
	Text(Ch2_4_GetKamui_ValbarDead)
	CALL $591F40
	FACTION_BLUE Kamui
	CharacterJoinedNotification(Notif_KamuiJoin)
	LABEL 0x5
	STAL 0x14
	MUSIC_RESTORE 0x05
	NoFade
	END

#ifndef InlineEvents
  MESSAGE Events end at offset currentOffset
#endif
