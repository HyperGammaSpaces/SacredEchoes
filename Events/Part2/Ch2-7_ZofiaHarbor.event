#include EAstdlib.event

EventPointerTable(plCh2_7_Event, Ch2_7_ThisChapter)

Ch2_7_ThisChapter:
	POIN Ch2_7_TurnBasedEvents
	POIN Ch2_7_CharacterBasedEvents
	POIN Ch2_7_LocationBasedEvents
	POIN Ch2_7_MiscBasedEvents
	POIN Ch2_7_Dunno Ch2_7_Dunno Ch2_7_Dunno
	POIN Ch2_7_Tutorial
	POIN Ch2_7_Ballista1 Ch2_7_Ballista3
	POIN PlayerUnits PlayerUnits
	POIN $0 $0 $0 $0 $0 $0
	POIN Ch2_7_BeginningScene Ch2_7_EndingScene

Ch2_7_TurnBasedEvents:
	TURN 0x0 PostPrep[1,0] 0x0
	TURN 0x0 WaveFirst[4,0] 0x0
	TURN 0x0 WaveSecond[5,0] 0x0
	TURN

PostPrep:
	MUSC SongID_Env_Water
		CHECK_EXISTS Leon
		BEQ 0x3 0xC 0x0
		CHECK_ALIVE Leon
		BEQ 0x3 0xC 0x0
		CHECK_EXISTS Kamui
		BEQ 0x3 0xC 0x0
		CHECK_ALIVE Kamui
		BEQ 0x3 0xC 0x0
			CAMERA_CENTERED Celica
			Text(Ch2_7_PostPrep_1) //hmm...
			CAMERA_CENTERED [4,16]
			Text(Ch2_7_PostPrep_2) //we may have company later
			GOTO 0x4
	LABEL 0x3
		CAMERA_CENTERED Celica
		Text(Ch2_7_PostPrep_1_NoLeon) //hmm...
		CAMERA_CENTERED [4,16]
		Text(Ch2_7_PostPrep_2_NoLeon) //we may have company later
	LABEL 0x4
	TEXTCLEAR
	NoFade
	ENDA

WaveFirst:
	SVAL 0x1 0x0
	CHAI DumaFaith_Sorcerer_2
	CHAI Enemy_Mercenary
	ReinforcementEvent(PirateSouthREDAs)
	NoFade
	ENDA

WaveSecond:
	ReinforcementEvent(PirateNorthREDAs)
	ReinforcementEvent(CavWestREDAs)
	NoFade
	ENDA

Ch2_7_CharacterBasedEvents:
	CHAR

Ch2_7_LocationBasedEvents:
	#ifdef __DEBUG__
	  Seize(12,16)
	#endif
	Village(0x8, VillageEventWest, 4,8)
	Village(0x9, VillageEventEast, 13,3)
	House(0x0,House1,15,3)
	Armory(Shop_Armorer1, 5, 0)
	LOCA

VillageEventWest:
	VillageEventItem(Ch2_7_Village_1, 0x05, Ambrosia)
	ENDA

VillageEventEast:
	VillageEventItem(Ch2_7_Village_2, 0x05, KillerAxe)
	ENDA

House1:
	SVAL 0x2 Celica
	CHECK_ACTIVE
	BEQ 0x100 0xC 0x2
		HouseEvent(Ch2_7_House,HouseBG)
		GOTO 0x101
	LABEL 0x100
		HouseEvent(Ch2_7_House_Celica,HouseBG)
	LABEL 0x101
	ENDA

Ch2_7_MiscBasedEvents:
	CauseGameOverIfLordDies
	DefeatAll(Ch2_7_EndingScene)
	AFEV 0xA ChangeAIEvent1 0x8
	AFEV 0xB ChangeAIEvent2 0x9
	MournQuoteAFEVS
	AFEV

ChangeAIEvent1:
	SVAL 0x1 0x0
	CHAI TreasureHunter
	NoFade
	ENDA

ChangeAIEvent2:
	SVAL 0x1 0x0
	CHAI Bandit_Strong
	NoFade
	ENDA

Ch2_7_Dunno:
	END_MAIN

Ch2_7_Tutorial:
	END_MAIN

Ch2_7_Ballista1:
	BLST

Ch2_7_Ballista3:
	BLST
	ALIGN 4

PlayerUnits:
#ifdef __DEBUG__
	UNIT Celica CelicaPriestess 0x00 Level(8,Ally,True) [12,16] 0x00 0x00 0x1 REDA12R15 [BaseSword, Vulnerary] NoAI 
	UNIT Mae Mage_F 0x00 Level(8,Ally,True) [12,16] 0x00 0x00 0x1 REDA13R14 [Fire, Vulnerary] NoAI 
	UNIT Boey Mage 0x00 Level(7,Ally,True) [11,16] 0x00 0x00 0x1 REDA11R14 [Fire, Vulnerary] NoAI 
	UNIT Genny Cleric 0x00 Level(8,Ally,True) [11,16] 0x00 0x00 0x1 REDA10R15 [Nosferatu, Vulnerary] NoAI 
	UNIT Saber Myrmidon 0x00 Level(1,Ally,True) [12,16] 0x00 0x00 0x1 REDA12R13 [BaseSword, Vulnerary] NoAI 
	UNIT Valbar Knight 0x00 Level(3,Ally,True) [11,16] 0x00 0x00 0x1 REDA10R13 [BaseLance,BaseAxe,Vulnerary] NoAI
	UNIT Leon Sniper 0x00 Level(1, Ally, 1) [12,16] 0x00 0x00 0x1 REDA13R16 [IronBow, BaseBow, Vulnerary] NoAI
	UNIT Kamui Mercenary 0x00 Level(8, Ally, 1) [11,16] 0x00 0x00 0x1 REDA11R16 [BaseSword] NoAI 
	UNIT
#else
	UNIT Celica CelicaPriestess 0x00 Level(1, Ally, 1) [12,16] 0x00 0x00 0x1 REDA12R15 [BaseSword, Vulnerary] NoAI 
	UNIT Mae Mage_F 0x00 Level(1, Ally, 1) [12,16] 0x00 0x00 0x1 REDA13R14 [Fire, Vulnerary] NoAI 
	UNIT Boey Mage 0x00 Level(1, Ally, 1) [11,16] 0x00 0x00 0x1 REDA11R14 [Fire, Vulnerary] NoAI 
	UNIT Genny Cleric 0x00 Level(1, Ally, 1) [11,16] 0x00 0x00 0x1 REDA10R15 [Nosferatu, Vulnerary] NoAI 
	UNIT Saber Mercenary 0x00 Level(1, Ally, 1) [12,16] 0x00 0x00 0x1 REDA12R13 [BaseSword, Vulnerary] NoAI 
	UNIT Valbar Knight 0x00 Level(1, Ally, 1) [11,16] 0x00 0x00 0x1 REDA10R13 [BaseLance,BaseAxe,Vulnerary] NoAI
	UNIT Leon Archer 0x00 Level(4, Ally, 1) [12,16] 0x00 0x00 0x1 REDA13R16 [IronBow, BaseBow, Vulnerary] NoAI
	UNIT Kamui Mercenary 0x00 Level(3, Ally, 1) [11,16] 0x00 0x00 0x1 REDA11R16 [BaseSword] NoAI 
	UNIT
#endif
	
EnemyUnits:
	UNIT ArcanistBoss Arcanist ArcanistBoss Level(3,Enemy,False) [4,1] DropItem 0x00 0x0 0x00 [Miasma,AngelRing,Item100G] NeverMoveAI 
	UNIT Enemy_Mercenary Mercenary ArcanistBoss Level(9,Enemy,True) [3,3] 0x00 0x00 0x0 0x00 [Armorslayer] NeverMoveAI 
	UNIT Bandit_Bow Mage ArcanistBoss Level(11,Enemy,True) [5,9] 0x00 0x00 0x0 0x00 [Fire] NeverMoveAI 
	UNIT Bandit_Bow Archer_F ArcanistBoss Level(12,Enemy,True) [11,6] 0x00 0x00 0x0 0x00 [SteelBow] NeverMoveAI 
	UNIT Bandit_Axe Pirate ArcanistBoss Level(13,Enemy,True) [2,14] 0x00 0x00 0x0 0x00 [BaseAxe] NoAI 
	UNIT Bandit_Axe Pirate ArcanistBoss Level(13,Enemy,True) [5,15] 0x00 0x00 0x0 0x00 [SteelAxe] NoAI 
	UNIT Bandit_Sword Mercenary ArcanistBoss Level(8,Enemy,True) [11,10] 0x00 0x00 0x0 0x00 [SteelSword] AttackInRangeAI
	UNIT Bandit_Axe Pirate ArcanistBoss Level(13,Enemy,True) [9,11] 0x00 0x00 0x0 0x00 [BaseAxe] AttackInRangeAI
	UNIT Bandit_Sword Mercenary ArcanistBoss Level(9,Enemy,True) [15,8] 0x00 0x00 0x0 0x00 [IronSword] AttackInRangeAI
	UNIT Bandit_Bow Archer ArcanistBoss Level(13,Enemy,True) [2,6] 0x00 0x00 0x0 0x00 [SteelBow] AttackInRangeAI 
	UNIT Remnant_Soldier Knight ArcanistBoss Level(3,Enemy,False) [11,5] 0x00 0x00 0x0 0x00 [BaseAxe,Javelin] NeverMoveAI 
	UNIT Bandit_Sword Mercenary ArcanistBoss Level(10,Enemy,True) [16,8] 0x00 0x00 0x0 0x00 [IronSword,HideShield] AttackInRangeAI 
	UNIT Bandit_Axe Pirate_F ArcanistBoss Level(13,Enemy,True) [16,11] 0x00 0x00 0x0 0x00 [BaseAxe] NoAI 
	UNIT DumaFaith_Sorcerer Shaman ArcanistBoss Level(9,Enemy,True) [6,0] 0x00 0x00 0x0 0x00 [Miasma,Heal] HealUnits 
	UNIT DumaFaith_Sorcerer_2 Shaman ArcanistBoss Level(8,Enemy,True) [2,2] 0x00 0x00 0x0 0x00 [Miasma,Heal] AttackInRangeAI 
	UNIT DumaFaith_Sorcerer Shaman ArcanistBoss Level(8,Enemy,True) [9,2] 0x00 0x00 0x0 0x00 [Miasma] AttackInRangeAI //Give this guy Silence once it's implemented.
	UNIT DumaFaith_Sorcerer Shaman ArcanistBoss Level(8,Enemy,True) [17,4] 0x00 0x00 0x0 0x00 [Miasma] AttackInRangeAI
	UNIT Remnant_Soldier Soldier ArcanistBoss Level(12,Enemy,True) [9,8] 0x00 0x00 0x0 0x00 [BaseLance,HideShield] AttackInRangeAI 
	UNIT DumaFaith_Sorcerer Shaman ArcanistBoss Level(6,Enemy,True) [14,1] 0x00 0x00 0x0 0x00 [Miasma] AttackInRangeAI
	UNIT Bandit_Sword Mercenary ArcanistBoss Level(9,Enemy,True) [14,15] 0x00 0x00 0x0 0x00 [BaseSword] AttackInRangeAI
	UNIT

CavWestREDAs:
	UNIT Remnant_Cav Cavalier ArcanistBoss Level(13,Enemy,True) [0,6] 0x00 0x00 0x1 REDA0R6 [IronLance,IronShield] DefaultAI 
	UNIT Remnant_Cav Cavalier_F ArcanistBoss Level(13,Enemy,True) [0,7] 0x00 0x00 0x1 REDA0R7 [Javelin] DefaultAI 
	UNIT

PirateSouthREDAs:
	UNIT TreasureHunter Pirate_F ArcanistBoss Level(13,Enemy,True) [3,16] 0x00 0x00 0x1 REDA3R16 [BaseAxe,Torch] BrigandAI 
	UNIT TreasureHunter Pirate ArcanistBoss Level(14,Enemy,True) [4,16] 0x00 0x00 0x1 REDA4R16 [BaseAxe,Torch] BrigandAI 
	UNIT
	
PirateNorthREDAs:	
	UNIT Bandit_Strong Pirate ArcanistBoss Level(15,Enemy,True) [10,0] 0x00 0x00 0x1 REDA10R0 [HandAxe,Torch] BrigandAI 
	UNIT
	
ConradAppears:
	UNIT Conrad Paladin 0x00 Level(7,NPC,True) [5,0] 0x00 0x00 0x1 REDA10R4 [BaseLance, Vulnerary] NoAI 
	UNIT

CutsceneUnits:
	UNIT Celica CelicaPriestess 0x00 Level(1, Ally, 1) [10,2] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI 
	UNIT Saber Mercenary 0x00 Level(1, Ally, 1) [11,1] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI 
	UNIT Mae Mage_F 0x00 Level(1, Ally, 1) [11,12] 0x00 0x00 0x0 0x00 [Fire, Vulnerary] NoAI 
	UNIT Boey Mage 0x00 Level(1, Ally, 1) [10,11] 0x00 0x00 0x0 0x00 [Fire, Vulnerary] NoAI 
	UNIT
	
CutsceneEnemies1:
	UNIT ArcanistBoss Arcanist 0x00 Level(1, Enemy, 1) [4,1] 0x00 0x00 0x0 0x00 [Miasma, Vulnerary] NoAI 
	UNIT
CutsceneEnemies2:
	UNIT SlaydePaladin Paladin 0x00 Level(1, Enemy, 1) [0,7] 0x00 0x00 0x1 REDA0R7 [BaseLance] NoAI 
	UNIT

Ch2_7_BeginningScene:
	LOAD1 0x1 EnemyUnits
	ENUN
	CAMERA_CENTERED [11,16]
	FADU 16
	MUSC Tension
	#ifdef __DEBUG__
		LOAD1 0x1 PlayerUnits
		ENUN
	#else
		LOAD3 0x0 PlayerUnits
		ENUN
	#endif
		CHECK_EXISTS Valbar
		BEQ 0x1 0xC 0x0
		CHECK_ALIVE Valbar
		BEQ 0x1 0xC 0x0
		CHECK_EXISTS Leon
		BEQ 0x1 0xC 0x0
		CHECK_ALIVE Leon
		BEQ 0x1 0xC 0x0
		CHECK_EXISTS Kamui
		BEQ 0x1 0xC 0x0
		CHECK_ALIVE Kamui
		BEQ 0x1 0xC 0x0
			Text(Ch2_7_OpeningScene) // bandits?
			CAMERA_CENTERED [4,8]
			STAL 20
			CAMERA_CENTERED [11,3]
			STAL 20
			CAMERA_CENTERED Celica
			Text(Ch2_7_OpeningScene_2) // lets get em
			GOTO 0x2
	LABEL 0x1
		Text(Ch2_7_OpeningScene_NoValbar) // bandits?
		CAMERA_CENTERED [4,8]
		STAL 20
		CAMERA_CENTERED [11,3]
		STAL 20
		CAMERA_CENTERED Celica
		Text(Ch2_7_OpeningScene_2_NoValbar) // lets get em
	LABEL 0x2
	GotoPrepScreen
	NoFade
	ENDA

Ch2_7_EndingScene:
	CALL UnifiedBEXPCaller
	MUSC SongID_Victory
	FADI 16
	CLEN 0x0
	CLEA 0x0
	CLEE 0x0
	CAMERA_CENTERED [11,0]
	SetBackground(0x24)
	
	CHECK_ALIVE Mae
	BEQ 0x102 0xC 0x0
	CHECK_ALIVE Boey
	BEQ 0x102 0xC 0x0
	
		TEXTSTART
		TEXTSHOW Ch2_7_EndingScene_1 //everyone alive
		TEXTEND
		GOTO 0x103
		
	LABEL 0x102
	
		TEXTSTART
		TEXTSHOW Ch2_7_EndingScene_1_Bad //no mae or boey
		TEXTEND
		
	LABEL 0x103
	
	MUSC SongID_ShadowOfVictory
	TEXTCONT
	TEXTEND
	TEXTCLEAR
	ClearBackgroundSmooth
	STAL 20
	
	//Put the slayde/cantor scene here. 
	CAMERA_CENTERED [11,12]
	MUSCFAST SongID_Env_WindBirds
	//warpin effect pls
	LOAD1 0x0 CutsceneEnemies1
	ENUN
	LOAD1 0x0 CutsceneEnemies2
	ENUN
	MOVE 0x08 ArcanistBoss [11,9]
	ENUN
	STAL 20
	MOVE 0x08 SlaydePaladin [12,9]
	ENUN
	FADI 16
	CLEE 0x0
	CAMERA_CENTERED [9,5]
	SetBackground(0x24)
	TEXTSTART
	TEXTSHOW Ch2_Epilogue2 // blah blah
	TEXTEND
		MUSC SongID_FromTheDarkness
	TEXTCONT
	TEXTEND
	TEXTCLEAR
	FADI 3
	ClearBackground

	STAL 20
	MUSCFAST SongID_Silence
	STAL 20
	LOAD1 0x1 ConradAppears
	ENUN
	FlashCursor(Conrad,60)
	Text(Ch2_7_ConradAppears) // conrad goes ...
	//MoveCoords(10,4,17,4)
	MOVE 0x08 Conrad [17,4]

	FADI 2
	ENUN
	ENUT gPart2Completed
	ENUT gRouteSplit1Flag
	MNCH 0x07
	//MNTS 0x0 //kick to title screen
	ENDA

#ifndef InlineEvents
  MESSAGE Events end at offset currentOffset
#endif