//Made by CrazyColorz5 of Fire Emblem Universe
//Based off of Camtech75's Skeleton
#include EAstdlib.event

EventPointerTable(plCh2_2_Event, Ch2_2_ThisChapter)

Ch2_2_ThisChapter:
	POIN Ch2_2_TurnBasedEvents
	POIN Ch2_2_CharacterBasedEvents
	POIN Ch2_2_LocationBasedEvents
	POIN Ch2_2_MiscBasedEvents
	POIN Ch2_2_Dunno Ch2_2_Dunno Ch2_2_Dunno
	POIN Ch2_2_Tutorial
	POIN Ch2_2_Ballista1 Ch2_2_Ballista2
	POIN Ch2_2_Units1 Ch2_2_Units1
	POIN $0 $0 $0 $0 $0 $0
	POIN Ch2_2_BeginningScene Ch2_2_EndingScene

Ch2_2_TurnBasedEvents:
	//Enemy spawn point is [11,12]
	OpeningTurnEvent(ChestEvent)
	TURN

Ch2_2_CharacterBasedEvents:
CHAR

Ch2_2_LocationBasedEvents:
	#ifdef __DEBUG__
	  Seize(2,9)
	#endif
	Chest(HideShield, 10, 3)
	//Defend point is [4,10]
	LOCA

Ch2_2_MiscBasedEvents:
	CauseGameOverIfLordDies
	DefeatAll(Ch2_2_EndingScene)
	MournQuoteAFEVS
	AFEV

Ch2_2_Dunno:
	END_MAIN

Ch2_2_Tutorial:
	END_MAIN

Ch2_2_Ballista1:
	BLST

Ch2_2_Ballista2:
	BLST
	ALIGN 4

Ch2_2_Units1:
	UNIT Celica CelicaPriestess 0x00 Level(2,Ally,True) [2,9] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI 
	UNIT Mae Mage_F 0x00 Level(2,Ally,True) [3,8] 0x00 0x00 0x0 0x00 [Fire, Vulnerary] NoAI 
	UNIT Boey Mage 0x00 Level(2,Ally,True) [3,9] 0x00 0x00 0x0 0x00 [Fire, Vulnerary] NoAI 
	UNIT Genny Cleric 0x00 Level(2,Ally,True) [3,10] 0x00 0x00 0x0 0x00 [Nosferatu, Vulnerary] NoAI 
	UNIT Saber Mercenary 0x00 Level(1,Ally,True) [4,6] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI 
	UNIT

SaberJoins:
	UNIT Saber Mercenary 0x00 Level(1, Ally, 1) [4,6] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI 
	UNIT

EnemyUnits:
	UNIT 0x4c Pirate 0x4c Level(7,Enemy,True) [11,11] DropItem 0x00 0x0 0x00 [BaseAxe,Item100G] AttackInRangeAI 
	UNIT 0x65 Pirate_F 0x4c Level(3,Enemy,True) [10,4] DropItem 0x00 0x0 0x00 [BaseAxe, ChestKey] NeverMoveAI 
	UNIT 0x65 Pirate 0x4c Level(4,Enemy,True) [11,4] 0x00 0x00 0x0 0x00 [BaseAxe] AttackInRangeAI 
	UNIT 0x65 Brigand 0x4c Level(2,Enemy,True) [10,10] 0x00 0x00 0x0 0x00 [BaseAxe] NoAI 
	UNIT 0x65 Pirate 0x4c Level(3,Enemy,True) [10,9] 0x00 0x00 0x0 0x00 [BaseAxe] AttackInRangeAI 
	UNIT 0x65 Brigand 0x4c Level(2,Enemy,True) [10,8] 0x00 0x00 0x0 0x00 [BaseAxe] AttackInRangeAI 
	UNIT 0x66 Archer 0x4c Level(2,Enemy,True) [11,3] 0x00 0x00 0x0 0x00 [BaseBow] AttackInRangeAI 
	UNIT 0x66 Archer 0x4c Level(2,Enemy,True) [12,11] 0x00 0x00 0x0 0x00 [BaseBow] AttackInRangeAI 
	UNIT 0x65 Pirate 0x4c Level(4,Enemy,True) [10,7] 0x00 0x00 0x0 0x00 [BaseAxe] NoAI 
	UNIT 0x65 Pirate 0x4c Level(3,Enemy,True) [9,6] 0x00 0x00 0x0 0x00 [BaseAxe] AttackInRangeAI 
	UNIT 0x65 Pirate 0x4c Level(3,Enemy,True) [9,11] 0x00 0x00 0x0 0x00 [BaseAxe] NoAI 
	UNIT

Ch2_2_BeginningScene:
	MUSC 0x55 //port town music
	SetBackground(0x07) //Port town
	Text(Ch2_2_Intro1)
	FADI 5

	LOAD1 0x1 SaberJoins
	ENUN

	SetBackground(0x07) //Port town
	TEXTSTART
	TEXTSHOW Ch2_2_Intro2
	TEXTEND
	REMA
	REMOVEPORTRAITS
	FADI 2

	MUSC 0x54 //inn music
		
	SetBackground(0x09)

	TEXTSTART 
	TEXTSHOW Ch2_2_RecruitSaber
	TEXTEND
	REMA
	REMOVEPORTRAITS

	_0x2141 HiringSaberCG 0x1 0x4
	REMOVEPORTRAITS
	STAL 90
		
	//give item 0x78 for G.Dagger
	//TODO: add 7743's EVENTSCRIPT_COND_ITEM ASMC to remove the item before re-giving
	RemoveItemFromAny(0x78)
	SVAL 0x3 0x78
	GIVEITEMTO Saber
	STAL 20

	_0x2141 0x09 0x1 0x4
	REMOVEPORTRAITS

	Text(Ch2_2_RecruitSaber_2)
	CharacterJoinedNotification(Notif_SaberJoin)
	FADI 2

	#ifdef __DEBUG__
		LOAD1 0x1 Ch2_2_Units1
		ENUN
	#else
		LOAD3 0x0 Ch2_2_Units1
		ENUN
	#endif

	STAL 30

	MUSC 0x4B
	SetBackground(0x08) //Boat
	Text(Ch2_2_SetSail)
	EndConvo
	STAL 30

	CAM1 [10,7]
	EARTHQUAKE_START 0x100
	STAL 30
	FADI 16
	STAL 30
	EARTHQUAKE_END
	//Trigger the map change 0 here
	TILECHANGE 0x0
	LOAD1 0x1 EnemyUnits
	ENUN
	STAL 30
	FADU 16

	MUSC 0x25
	//Pirates say something
	CAM1 0x4c
	FlashCursor(0x4c, 60)
	Text(Ch2_2_Pirates)
	//Trigger map change 1 here
	SOUN 0xF2
	TILECHANGE 0x1
	STAL 60

	NoFade
	GotoPrepScreen
	ENDA

ChestEvent:
	MUSC 0x4B
	MUSI
	CAM1 [10,3]
	STAL 20
	FlashCursor(10,3,60)
	STAL 20
	Text(Ch2_2_ChestText)
	MUNO
	NoFade
	ENDA

Ch2_2_EndingScene:
	CALL UnifiedBEXPCaller
	MUSC 0x31 //victory
	SetBackground(0x08)
		CHECK_EXISTS Genny
		BEQ 0x1 0xC 0x0
		CHECK_ALIVE Genny
		BEQ 0x1 0xC 0x0
		Text(Ch2_2_PostBattle)
		GOTO 0x2
	LABEL 0x1
	Text(Ch2_2_PostBattle_NoGenny)
	LABEL 0x2
	MNCH 0x0B //go to ch11 but get intercepted
	ENDA

#ifndef InlineEvents
  MESSAGE Events end at offset currentOffset
#endif