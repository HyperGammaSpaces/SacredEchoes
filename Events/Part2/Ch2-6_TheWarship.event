#include EAstdlib.event

EventPointerTable(plCh2_6_Event, Ch2_6_ThisChapter)

Ch2_6_ThisChapter:
POIN Ch2_6_TurnBasedEvents
POIN Ch2_6_CharacterBasedEvents
POIN Ch2_6_LocationBasedEvents
POIN Ch2_6_MiscBasedEvents
POIN Ch2_6_Dunno Ch2_6_Dunno Ch2_6_Dunno
POIN Ch2_6_Tutorial
POIN Ch2_6_Ballista1 Ch2_6_Ballista3
POIN PlayerUnits PlayerUnits
POIN $0 $0 $0 $0 $0 $0
POIN Ch2_6_BeginningScene Ch2_6_EndingScene

Ch2_6_TurnBasedEvents:
TURN 0x0 SecondBoat[4,0] 0x0
TURN 0x0 WitchesAppear[6,0] 0x0
TURN

SecondBoat:
CAM1 [11,0]
EARTHQUAKE_START 0x100
STAL 30
FADI 16
STAL 30
EARTHQUAKE_END
TILECHANGE 0x1 // Boat
//ReinforcementEvent(EnemyUnitsBoat2)
SMOV 0x2 (EnemyUnitsBoat2|0x8000000)
CALL $9EE274
//
FADU 16
STAL 20
CAM1 0x90
FlashCursor(0x90,60)
MUSI
STAL 20
Text(Ch2_6_Reinforcements1) // duma leader says something spooky
MUNO
NoFade
ENDA

WitchesAppear:
//ReinforcementEvent(EnemyUnitsWitches)
SMOV 0x2 (EnemyUnitsWitches|0x8000000)
CALL $9EE274
//
STAL 20
CAM1 Celica
FlashCursor(Celica,60)
Text(Ch2_6_Reinforcements2) // oh no, witches
NoFade
ENDA

Ch2_6_CharacterBasedEvents:
CHAR

Ch2_6_LocationBasedEvents:
#ifdef __DEBUG__
  Seize(4,5)
#endif
ChestMoney(3000, 1, 2)
LOCA

Ch2_6_MiscBasedEvents:
CauseGameOverIfLordDies
DefeatAll(Ch2_6_EndingScene)
MournQuoteAFEVS
AFEV

Ch2_6_Dunno:
END_MAIN

Ch2_6_Tutorial:
END_MAIN

Ch2_6_Ballista1:
BLST

Ch2_6_Ballista3:
BLST
ALIGN 4

PlayerUnits:
	UNIT Celica CelicaPriestess 0x00 Level(1, Ally, 1) [11,18] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI 
	UNIT Mae Mage_F 0x00 Level(1, Ally, 1) [10,17] 0x00 0x00 0x0 0x00 [Fire, Vulnerary] NoAI 
	UNIT Boey Mage 0x00 Level(1, Ally, 1) [10,16] 0x00 0x00 0x0 0x00 [Fire, Vulnerary] NoAI 
	UNIT Genny Cleric 0x00 Level(1, Ally, 1) [12,18] 0x00 0x00 0x0 0x00 [Nosferatu, Vulnerary] NoAI 
	UNIT Saber Mercenary 0x00 Level(1, Ally, 1) [11,17] 0x00 0x00 0x0 0x00 [IronSword, Vulnerary] NoAI 
	UNIT Valbar Knight 0x00 Level(1, Ally, 1) [12,16] 0x00 0x00 0x0 0x00 [IronLance,IronAxe,Vulnerary] NoAI
	UNIT Leon Archer 0x00 Level(4, Ally, 1) [12,17] 0x00 0x00 0x0 0x00 [IronBow, IronBow, Vulnerary] NoAI
	UNIT Kamui Mercenary 0x00 Level(3, Ally, 1) [11,15] 0x00 0x00 0x0 0x00 [IronSword] NoAI 
	UNIT
	
EnemyUnits:
	// Upper Deck
	UNIT 0x4F Mercenary 0x4F Level(13,Enemy,False) [3,1] DropItem 0x00 0x0 0x00 [SteelSword,IronShield,Item100G] NeverMoveAI 
	UNIT 0x66 Archer 0x4F Level(10,Enemy,True) [3,0] 0x00 0x00 0x0 0x0 [IronBow] NeverMoveAI
	UNIT 0x65 Pirate 0x4F Level(12,Enemy,True) [2,1] 0x00 0x00 0x0 0x0 [IronAxe,HandAxe] NeverMoveAI
	UNIT 0x65 Pirate 0x4F Level(12,Enemy,True) [4,1] 0x00 0x00 0x0 0x0 [SteelAxe,HideShield] NeverMoveAI
	UNIT 0x66 Archer_F 0x4F Level(10,Enemy,True) [0,2] 0x00 0x00 0x0 0x0 [IronBow] AttackInRangeAI
	UNIT 0x67 Mercenary 0x4F Level(10,Enemy,True) [1,4] 0x00 0x00 0x0 0x0 [BaseSword] AttackInRangeAI
	UNIT 0x65 Brigand 0x4F Level(11,Enemy,True) [4,5] 0x00 0x00 0x0 0x0 [BaseAxe] AttackInRangeAI
	UNIT 0x65 Pirate_F 0x4F Level(12,Enemy,True) [2,8] 0x00 0x00 0x0 0x0 [HandAxe,SteelAxe] NeverMoveAI
	UNIT 0x65 Pirate 0x4F Level(12,Enemy,True) [3,8] 0x00 0x00 0x0 0x0 [HandAxe,HideShield] NeverMoveAI
	UNIT 0x65 Mage 0x4F Level(10,Enemy,True) [3,4] 0x00 0x00 0x0 0x0 [Thunder] AttackInRangeAI

	// Lower Deck
	UNIT 0x66 Archer 0x4F Level(11,Enemy,True) [5,9] 0x00 0x00 0x0 0x0 [SteelBow] AttackInRangeAI
	UNIT 0x65 Soldier 0x4F Level(10,Enemy,True) [4,14] 0x00 0x00 0x0 0x0 [Javelin] NeverMoveAI
	UNIT 0x65 Soldier 0x4F Level(10,Enemy,True) [3,15] 0x00 0x00 0x0 0x0 [Javelin,HideShield] NeverMoveAI
	UNIT 0x65 Pirate 0x4F Level(11,Enemy,True) [2,14] 0x00 0x00 0x0 0x0 [BaseAxe] DefaultAI
	UNIT 0x65 Pirate_F 0x4F Level(10,Enemy,True) [1,14] DropItem 0x00 0x0 0x0 [HandAxe] DefaultAI
	UNIT 0x65 Pirate 0x4F Level(10,Enemy,True) [2,13] 0x00 0x00 0x0 0x0 [BaseAxe] DefaultAI
	UNIT 0x65 Mage 0x4F Level(10,Enemy,True) [4,12] 0x00 0x00 0x0 0x0 [Fire] DefaultAI
	UNIT 0x67 Mercenary 0x4F Level(10,Enemy,True) [0,9] 0x00 0x00 0x0 0x0 [BaseSword] AttackInRangeAI
	UNIT 0x67 Mercenary 0x4F Level(11,Enemy,True) [4,9] 0x00 0x00 0x0 0x0 [BaseSword] AttackInRangeAI
	UNIT

EnemyUnitsBoat2: 
	UNIT 0x90 Shaman 0x90 Level(9,Enemy,False) [13,3] DropItem 0x00 0x0 0x00 [Miasma,Item100G] NeverMoveAI 
	UNIT 0x92 Shaman 0x90 Level(6,Enemy,True) [9,2] 0x00 0x00 0x0 0x00 [Miasma] AttackInRangeAI 
	UNIT 0x92 Mage 0x90 Level(7,Enemy,True) [11,4] DropItem 0x00 0x0 0x00 [Fire, HideShield, ChestKey] AttackInRangeAI 
	UNIT 0x92 Shaman 0x90 Level(6,Enemy,True) [10,1] 0x00 0x00 0x0 0x00 [Miasma] DefaultAI
	UNIT 0x92 Shaman 0x90 Level(8,Enemy,True) [9,4] 0x00 0x00 0x0 0x00 [Miasma] DefaultAI 
	UNIT

EnemyUnitsWitches:
	UNIT 0x92 Mage_F 0x90 Level(3,Enemy,True) [10,0] 0x00 0x00 0x1 REDA10R2 [Thunder] DefaultAI
	UNIT 0x95 Witch 0x90 Level(2,Enemy,True) [13,0] 0x00 0x00 0x1 REDA13R2 [Nosferatu] WitchWarpAI
	UNIT

Ch2_6_BeginningScene:
	LOAD3 0x0 PlayerUnits 
	ENUN
	LOAD1 0x1 EnemyUnits
	ENUN
	MUSC 0x4B // Water
	SetBackground(0x41) //night boat bg
	Text(Ch2_6_OpeningScene) // seems spooky huh
	FADI 16
	ClearBackground
	MUSCFAST 0x7FFF
	STAL 10
	CAM1 Celica
	EARTHQUAKE_START 0x100
	STAL 60
	EARTHQUAKE_END
	STAL 10
	MUSC 0x25
	FADU 16
	STAL 20
	CAM1 0x4F
	FlashCursor(0x4F, 60)
	STAL 20
		CHECK_EXISTS Valbar
		BEQ 0x1 0xC 0x0
		CHECK_ALIVE Valbar
		BEQ 0x1 0xC 0x0
		Text(Ch2_6_OpeningScene_2) // ahh pirates
		GOTO 0x2
	LABEL 0x1
	Text(Ch2_6_OpeningScene_2_NoValbar) // ahh pirates
	LABEL 0x2
	GotoPrepScreen
	NoFade
	ENDA

Ch2_6_EndingScene:
	CALL UnifiedBEXPCaller
	MUSC 0x31 //victory (good ending)
	FADI 16
	SetBackground(0x41) //night boat bg
	Text(Ch2_6_EndingText) // yay we won
	MNCH 0x0E //go to world map
	ENDA

#ifndef InlineEvents
  MESSAGE Events end at offset currentOffset
#endif