//Made by CrazyColorz5 of Fire Emblem Universe
//Based off of Camtech75's Skeleton
#include EAstdlib.event

EventPointerTable(plCh1_1_Event, Ch1_1_ThisChapter)

Ch1_1_ThisChapter:
	POIN Ch1_1_TurnBasedEvents
	POIN Ch1_1_CharacterBasedEvents
	POIN Ch1_1_LocationBasedEvents
	POIN Ch1_1_MiscBasedEvents
	POIN Ch1_1_Dunno Ch1_1_Dunno Ch1_1_Dunno
	POIN Ch1_1_Tutorial
	POIN Ch1_1_Ballista1 Ch1_1_Ballista2
	POIN Ch1_1_Units1 Ch1_1_Units1
	POIN $0 $0 $0 $0 $0 $0
	POIN Ch1_1_BeginningScene Ch1_1_EndingScene

Ch1_1_TurnBasedEvents:
	OpeningTurnEvent(WTAEvent)
	TURN

WTAEvent:
	MUSIC SongID_MarchToDeliverance // Use MUSIC_REPLACE during turn events to return to the map music, but since this is turn 1, MUSIC is fine.
	MUSIC_QUIET_T
	CameraCursor(Lukas)
	MapText(Ch1_1_LukasAdvice)
	MUSIC_QUIET_F
	NoFade
	END

Ch1_1_CharacterBasedEvents:
	CharacterEvent(gKliffRecruited, TalkToKliff, Alm, Kliff)
	CharacterEvent(gKliffRecruited, TalkToKliff, Tobin, Kliff)
	CharacterEvent(gKliffRecruited, TalkToKliff, Gray, Kliff)
	CharacterEvent(gKliffRecruited, TalkToKliff, Faye, Kliff)
	CharacterEvent(gFayeRecruited, TalkToFaye, Alm, Faye)
	CHAR

Ch1_1_LocationBasedEvents:
	#ifdef __DEBUG__
		Seize(8,7)
	#endif
	Village(0xA, VillageEvent, 3,11)
	House(0x0,House1,4,3)
	House(0x0,House2,2,8)
	LOCA

VillageEvent:
	VillageEventItem(Ch1_House3, HouseBG, MetissTome)
	END

House1:
	HouseEvent(Ch1_House1,HouseBG)
	END

House2:
	HouseEvent(Ch1_House2,HouseBG)
	END

Ch1_1_MiscBasedEvents:
	CauseGameOverIfLordDies
	DefeatAll(Ch1_1_EndingScene)
	MournQuoteAFEVS
	AFEV

Ch1_1_Dunno:
	END_MAIN

Ch1_1_Tutorial:
	END_MAIN

Ch1_1_Ballista1:
	BLST

Ch1_1_Ballista2:
	BLST
	ALIGN 4

Ch1_1_Units1:
	UNIT Alm AlmFighter 0x00 Level(1, Ally, 1) [8,7] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI
	UNIT Lukas Soldier 0x00 Level(3, Ally, 1) [9,7] 0x00 0x00 0x0 0x00 [IronLance, BaseLance, Vulnerary] NoAI
	UNIT Tobin Villager 0x00 Level(2, Ally, 1) [8,8] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI
	UNIT Gray Villager 0x00 Level(5, Ally, 1) [8,6] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI
	UNIT

EnemyUnits:
	UNIT BanditAlm1 Brigand BanditAlm1 Level(3,Enemy,True) [19,7] DropItem 0x00 0x1 REDA18R4 [BaseAxe,_200G] AttackInRangeAI // A matter of personal preference, but I would not put spaces after commas.
	UNIT Bandit_Axe Brigand BanditAlm1 Level(1,Enemy,True) [19,7] 0x00 0x00 0x1 REDA15R6 [BaseAxe] NoAI // Keeps things more compact, so "Level(1,Enemy,True)" instead of "Level(1, Enemy, 1)".
	UNIT Bandit_Axe Brigand BanditAlm1 Level(1,Enemy,True) [19,7] 0x00 0x00 0x1 REDA16R4 [BaseAxe] NoAI // Also note my use of "True" instead of "1".
	UNIT Bandit_Axe Brigand BanditAlm1 Level(1,Enemy,True) [19,7] 0x00 0x00 0x1 REDA14R3 [BaseAxe] NoAI // It doesn't affect much, but I like to make the boss' leader him/herself. Really more of a consistency thing.
	UNIT Bandit_Axe Brigand BanditAlm1 Level(1,Enemy,True) [19,7] 0x00 0x00 0x1 REDA15R10 [BaseAxe] AttackInRangeAI // I suggest using my REDA Helpers. They make single-command REDAs easier.
	UNIT Bandit_Axe Brigand BanditAlm1 Level(1,Enemy,True) [19,7] 0x00 0x00 0x1 REDA17R8 [BaseAxe] NoAI
	UNIT

NeutralUnits:
	UNIT Kliff Villager 0x00 Level(1, NPC, 1) [6,6] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] [0x03, 0x03, 0x0, 0x0]
	UNIT Faye Villager_F 0x00 Level(1, NPC, 1) [6,8] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] [0x03, 0x03, 0x0, 0x0]
	UNIT

Ch1_1_CutsceneUnits:
	UNIT Alm AlmFighter 0x00 Level(1, Ally, 1) [1,2] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI
	UNIT

Ch1_1_Mycen_Green:
	UNIT Mycen GoldKnight 0x00 Level(1, NPC, 1) [1,2] 0x00 0x00 0x00 0x00 [BaseLance] NoAI
	UNIT

Ch1_1_CutsceneUnits_Green:
	UNIT Lukas Soldier 0x00 Level(3, NPC, 1) [18,7] 0x00 0x00 0x0 0x00 [IronLance, BaseLance, Vulnerary] NoAI
	UNIT Tobin Villager 0x00 Level(2, NPC, 1) [5,6] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI
	UNIT Gray Villager 0x00 Level(5, NPC, 1) [5,8] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI
	UNIT Kliff Villager 0x00 Level(1, NPC, 1) [6,6] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI
	UNIT Faye Villager_F 0x00 Level(1, NPC, 1) [6,8] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI
	UNIT

Ch1_1_BeginningScene:
	MUSIC SongID_Advance
	LOAD 0x1 Ch1_1_CutsceneUnits
	ENUN
	LOAD 0x1 Ch1_1_CutsceneUnits_Green
	ENUN

	SetBackground(HouseBG)
	BackgroundText(OpeningScene_1)
	EndConvo
	STAL 0x08
	MOVE 0x5 Lukas [7, 7]
	ENUN

	CAMERA_CENTERED [5, 7]
	MOVE 0x8 Alm [5, 7]
	STAL 0x40
	FlashCursor(Lukas, 60)
	SetBackground(Village)
	TEXTMODE 1
	TEXTSHOW OpeningScene_2
	TEXTEND
	MUSIC SongID_ShadowOfVictory
	TEXTCONT
	TEXTEND
	TEXTCLEAR
		FadeOutMusic
	ClearBackground
	CAMERA_CENTERED [7, 5]
	STAL 0x08
	MOVE 0x10 Lukas [1, 2]
	MOVE 0x10 Alm [1, 2]
	ENUN
	HIDEUNIT Lukas
	HIDEUNIT Alm
	MUSCSLOW SongID_Env_Birds

	FlashCursor(Kliff, 60)
	Text(Village,OpeningScene_3)
		FadeOutMusic
	STAL 0x14

	MUSIC SongID_Advance
	FlashCursor(1, 2, 60)
	SetBackground(HouseBG)
		TEXTMODE 1
	TEXTSHOW OpeningScene_4
	TEXTEND
	FadeOutMusic
	TEXTCONT
	TEXTEND
	MUSIC SongID_Uprising
	TEXTCONT
	TEXTEND
	EndConvo
		FadeOutMusic

	SHOWUNIT Lukas
	STAL 0x30
		TILECHANGE 0x00
		EVBIT_T 0x9
	MOVE 0x08 Lukas [9, 7]
	ENUN
		EVBIT_F 0x9
		CAMERA_CENTERED Lukas

	SHOWUNIT Alm
	STAL 0x14
	MOVE 0x10 Alm [8, 7]
	ENUN

	SetBackground(Outside_Village_Day)
		TEXTMODE 1
	TEXTSHOW OpeningScene_5
	TEXTEND
	MUSIC SongID_Uprising
	TEXTCONT
	TEXTEND
	MUSIC SongID_Reunion
	TEXTCONT
	TEXTEND
	EndConvo

	STAL 0x14
	MOVE 0x10 Gray [4, 3]
	MOVE 0x10 Tobin [2, 8]
		MOVE 0x10 Alm [1, 2]
	ENUN
	HIDEUNIT Gray
	HIDEUNIT Tobin
	STAL 60
		MUSIC_QUIET_T
		CameraCursor(Alm)
		MapText(OpeningScene_WheresGrandpa)
		MUSIC_QUIET_F
		STAL 30
		MOVE 0x10 Alm [8, 7]
		ENUN
	SHOWUNIT Gray
	SHOWUNIT Tobin
	STAL 0x14
	MOVE 0x10 Gray [8, 6]
	MOVE 0x10 Tobin [8, 8]
	ENUN

	STAL 0x20

	FACTION_BLUE Gray
	FACTION_BLUE Tobin
	CharacterJoinedNotification(Notif_GrayTobinJoin)

	STAL 0x20
	FlashCursor(Alm, 60)
	FADEIN 5
		STAL 5
	REMOVEALL_BLUE
	REMOVEALL_RED
	REMOVEALL_GREEN
	LOAD 0x1 Ch1_1_Units1 //0x0 instead of 0x1 seems to be for allies on maps with prep screens
	LOAD 0x1 NeutralUnits
	ENUN
	SetBackground(Outside_Village_Day)
		TEXTMODE 1
	TEXTSHOW OpeningScene_GetGrayAndTobin
	TEXTEND
	TEXTCLEAR
	TEXTMODE 1
	BACKGROUND_CHANGE AlmPrologueCG 1 4
	STAL 60
	CgText(OpeningScene_CelicaCG)
	EndConvo
		STAL 5

	MUSIC SongID_Tension
	EVBIT_T 0x9
	LOAD 0x1 EnemyUnits
	ENUN
		EVBIT_F 0x9
		STAL 5

	CameraCursor(BanditAlm1)
	MapText(Ch1_1_PreBattle)

		CAMERA_CENTERED Alm
	MOVE 0x10 Alm [10,6]
	MOVE 0x10 Lukas [10,8]
	MOVE 0x10 Tobin [9,7]
	MOVE 0x10 Gray [8,7]
	ENUN

	NoFade
	// GotoPrepScreen
	END

Ch1_1_EndingScene:
	CONFIG_SKIP 0x1
	FLAG_T gEnableWorldMap			//enable world map
	CONFIG_SKIP 0x0
		CALL EndMusicAndMMS
	CALL UnifiedBEXPCaller
		FADEIN 5
		STAL 5
	REMOVEALL_GREEN
	MUSIC SongID_Victory //victory
	Text(Plain,Ch1_1_PostBattle)
	GOTO_CHAPTER 0x01 //go to ch1
	END

TalkToKliff:
	MUSIC_REPLACE SongID_Recruitment
	STAL 0x20
	MapText(Ch1_1_GetKliff)
	SVAL 0x1 0x0
	SET_AI Kliff
	STAL 5
	FACTION_BLUE Kliff
		CALL $591F40
	CharacterJoinedNotification(Notif_KliffJoin)
	STAL 0x14
	MUSIC_RESTORE 0x05
	NoFade
	END

TalkToFaye:
	MUSIC_REPLACE SongID_Recruitment
	STAL 0x20
	MapText(Ch1_1_GetFaye)
	SVAL 0x1 0x0
	SET_AI Faye
	STAL 5
	FACTION_BLUE Faye
		CALL $591F40
	CharacterJoinedNotification(Notif_FayeJoin)
	STAL 0x14
	MUSIC_RESTORE 0x05
	NoFade
	END

#ifndef InlineEvents
	MESSAGE Events end at offset currentOffset
#endif
