//Made by CrazyColorz5 of Fire Emblem Universe
//Based off of Camtech75's Skeleton
#include EAstdlib.event

EventPointerTable(plHideoutEvent, Hideout_ThisChapter)

Hideout_ThisChapter:
	POIN Hideout_TurnBasedEvents
	POIN Hideout_CharacterBasedEvents
	POIN Hideout_LocationBasedEvents
	POIN Hideout_MiscBasedEvents
	POIN Hideout_Dunno Hideout_Dunno Hideout_Dunno
	POIN Hideout_Tutorial
	POIN Hideout_Ballista1 Hideout_Ballista3
	POIN PlayerUnits PlayerUnits
	POIN $0 $0 $0 $0 $0 $0
	POIN Hideout_BeginningScene Hideout_EndingScene

Hideout_TurnBasedEvents:
	TURN

Hideout_CharacterBasedEvents:
	CHAR

Hideout_LocationBasedEvents:
	#ifdef __DEBUG__
	  Seize(6,18)
	#endif
	CHESRANDOM 0x9 Act1RandomChest|IsPointer [4,9]
	Door(4, 7)
	Door(4, 12)

	#ifdef LION_HEADS_ENABLED
		//Lion heads
		LionHead(LionheadLocalTrigger1,LionHeadHideoutDef, 1, 17)
		LionHead(LionheadLocalTrigger1,LionHeadHideoutAtk, 21, 17)
	#endif
	LOCA

Hideout_MiscBasedEvents:
	CauseGameOverIfLordDies
	DefeatAll(Hideout_EndingScene)
	AFEV 0xA ChestEvent 0x9
	MournQuoteAFEVS
	AFEV

Hideout_Dunno:
	END_MAIN

Hideout_Tutorial:
	END_MAIN

Hideout_Ballista1:
	BLST

Hideout_Ballista3:
	BLST
	ALIGN 4

PlayerUnits:
	UNIT Alm AlmFighter 0x00 Level(1, Ally, 1) [6,18] 0x00 0x00 0x0 0x00 [Vulnerary] NoAI 
	UNIT Lukas Soldier 0x00 Level(1, Ally, 1) [15,19] 0x00 0x00 0x0 0x00 [Vulnerary] NoAI 
	UNIT Lukas Soldier 0x00 Level(1, Ally, 1) [6,20] 0x00 0x00 0x0 0x00 [Vulnerary] NoAI
	UNIT Lukas Soldier 0x00 Level(1, Ally, 1) [15,21] 0x00 0x00 0x0 0x00 [Vulnerary] NoAI
	UNIT Lukas Soldier 0x00 Level(1, Ally, 1) [16,18] 0x00 0x00 0x0 0x00 [Vulnerary] NoAI
	UNIT Lukas Soldier 0x00 Level(1, Ally, 1) [7,19] 0x00 0x00 0x0 0x00 [Vulnerary] NoAI
	UNIT Lukas Soldier 0x00 Level(1, Ally, 1) [16,20] 0x00 0x00 0x0 0x00 [Vulnerary] NoAI 
	UNIT Lukas Soldier 0x00 Level(1, Ally, 1) [7,21] 0x00 0x00 0x0 0x00 [Vulnerary] NoAI 
	UNIT Lukas Soldier 0x00 Level(1, Ally, 1) [8,17] 0x00 0x00 0x0 0x00 [Vulnerary] NoAI 
	UNIT Lukas Soldier 0x00 Level(1, Ally, 1) [14,17] 0x00 0x00 0x0 0x00 [Vulnerary] NoAI
	UNIT
	
CutsceneAlliesFernand:
	UNIT Alm AlmFighter 0x00 Level(1, Ally, 1) [3, 6] 0x00 0x00 0x0 0x00 [Vulnerary] NoAI 
	UNIT Lukas Soldier 0x00 Level(1, Ally, 1) [3, 6] 0x00 0x00 0x0 0x00 [Vulnerary] NoAI 
	UNIT Clair PegasusKnight 0x00 Level(1, Ally, 1) [3, 6] 0x00 0x00 0x0 0x00 [Vulnerary] NoAI 
	UNIT Gray Villager 0x00 Level(1, Ally, 1) [3, 6] 0x00 0x00 0x0 0x00 [Vulnerary] NoAI 
	UNIT Tobin Villager 0x00 Level(1, Ally, 1) [3, 6] 0x00 0x00 0x0 0x00 [Vulnerary] NoAI 
	UNIT 0x2D Cavalier 0x00 Level(1, Ally, 1) [3, 6] 0x00 0x00 0x0 0x00 [BaseLance] NoAI
	UNIT
	
CutsceneAlliesNoFernand:
	UNIT Alm AlmFighter 0x00 Level(1, Ally, 1) [3, 6] 0x00 0x00 0x0 0x00 [Vulnerary] NoAI 
	UNIT Lukas Soldier 0x00 Level(1, Ally, 1) [3, 6] 0x00 0x00 0x0 0x00 [Vulnerary] NoAI 
	UNIT Clair PegasusKnight 0x00 Level(1, Ally, 1) [3, 6] 0x00 0x00 0x0 0x00 [Vulnerary] NoAI 
	UNIT Gray Villager 0x00 Level(1, Ally, 1) [3, 6] 0x00 0x00 0x0 0x00 [Vulnerary] NoAI 
	UNIT Tobin Villager 0x00 Level(1, Ally, 1) [3, 6] 0x00 0x00 0x0 0x00 [Vulnerary] NoAI 
	UNIT
	
CutsceneFernand:
	UNIT 0x2D Cavalier 0x00 Level(1, Ally, 1) [3, 6] 0x00 0x00 0x0 0x00 [BaseLance] NoAI
	UNIT
	
EnemyUnits:
	UNIT 0x5F Entombed 0x00 Level(1, Enemy, False) [11,3] DropItem 0x00 0x0 0x00 [RottenClaw, _200G] AttackInRangeAI 
	UNIT 0xAA Revenant 0x00 Level(9, Enemy, 1) [10,2] 0x04 0x00 0x0 0x00 [RottenClaw] NoAI
	UNIT 0xAA Revenant 0x00 Level(11, Enemy, 1) [12,2] 0x04 0x00 0x0 0x00 [RottenClaw] NoAI
	UNIT 0xAD Bonewalker_Bow 0x00 Level(10, Enemy, 1) [10,4] 0x04 0x00 0x0 0x00 [BaseBow] NoAI
	UNIT 0xAA Revenant 0x00 Level(7, Enemy, 1) [12,4] 0x04 0x00 0x0 0x00 [RottenClaw] NoAI
	UNIT 0xAC Bonewalker 0x00 Level(8, Enemy, 1) [6,9] 0x04 0x00 0x0 0x00 [BaseLance] NoAI
	UNIT 0xAA Revenant 0x00 Level(9, Enemy, 1) [6,15] 0x04 0x00 0x0 0x00 [RottenClaw] NoAI
	UNIT 0xAA Revenant 0x00 Level(7, Enemy, 1) [18,14] 0x04 0x00 0x0 0x00 [RottenClaw] NoAI
	UNIT
	
EnemyUnits2:
	UNIT 0xAA Revenant 0x00 Level(10, Enemy, 1) [19,13] 0x04 0x00 0x0 0x00 [RottenClaw] NoAI
	UNIT 0xAC Bonewalker 0x00 Level(7, Enemy, 1) [14,9] 0x04 0x00 0x0 0x00 [BaseSword] NoAI
	UNIT 0xAA Revenant 0x00 Level(8, Enemy, 1) [5,5] 0x04 0x00 0x0 0x00 [RottenClaw] NoAI
	UNIT 0xAA Revenant 0x00 Level(9, Enemy, 1) [19,9] 0x04 0x00 0x0 0x00 [RottenClaw] NoAI
	UNIT 0xAA Revenant 0x00 Level(7, Enemy, 1) [17,10] 0x04 0x00 0x0 0x00 [RottenClaw] NoAI
	UNIT 0xAA Revenant 0x00 Level(10, Enemy, 1) [10,13] 0x04 0x00 0x0 0x00 [RottenClaw] NoAI
	UNIT 0xAD Bonewalker_Bow 0x00 Level(8, Enemy, 1) [9,10] 0x04 0x00 0x0 0x00 [BaseBow] NoAI
	UNIT 0xAA Revenant 0x00 Level(11, Enemy, 1) [17,6] 0x04 0x00 0x0 0x00 [RottenClaw] NoAI
	UNIT 0xAA Revenant 0x00 Level(10, Enemy, 1) [3,17] 0x04 0x00 0x0 0x00 [RottenClaw] NoAI
	UNIT
	
EnemyUnitsReplay:
	UNIT 0x5F Entombed 0x00 Level(1, Enemy, True) [11,3] DropItemWithRandomPosition 0x00 0x0 0x00 [RottenClaw, _200G] AttackInRangeAI 
	UNIT 0xAA Random_RevBonewalker 0x00 Level(9, Enemy, 1) [10,2] MonsterTemplate 0x00 0x0 0x00 NoItems NoAI
	UNIT 0xAA Random_AnyMonster 0x00 Level(10, Enemy, 1) [12,2] MonsterTemplate 0x00 0x0 0x00 NoItems NoAI
	UNIT 0xAD Bonewalker_Bow 0x00 Level(9, Enemy, 1) [10,4] 0x00 0x00 0x0 0x00 [BaseBow] NoAI
	UNIT 0xAA Random_RevBonewalker 0x00 Level(9, Enemy, 1) [12,4] MonsterTemplate 0x00 0x0 0x00 NoItems NoAI
	UNIT 0xAC Bonewalker 0x00 Level(10, Enemy, 1) [6,9] RandomizePosition 0x00 0x0 0x00 [BaseLance] NoAI
	UNIT 0xAA Random_RevBonewalker 0x00 Level(8, Enemy, 1) [6,15] RandomMonsterTemplate 0x00 0x0 0x00 NoItems NoAI
	UNIT 0xAA Random_RevBonewalker 0x00 Level(9, Enemy, 1) [18,14] RandomMonsterTemplate 0x00 0x0 0x00 NoItems NoAI
	UNIT 0xAA Random_RevBonewalker 0x00 Level(10, Enemy, 1) [19,13] RandomMonsterTemplate 0x00 0x0 0x00 NoItems NoAI
	UNIT 0xAC Bonewalker 0x00 Level(7, Enemy, 1) [14,9] 0x00 0x00 0x0 0x00 [BaseSword] NoAI
	UNIT 0xAA Random_RevBonewalker 0x00 Level(9, Enemy, 1) [5,5] RandomMonsterTemplate 0x00 0x0 0x00 NoItems NoAI
	UNIT 0xAA Random_RevBonewalker 0x00 Level(8, Enemy, 1) [19,9] RandomMonsterTemplate 0x00 0x0 0x00 NoItems NoAI
	UNIT 0xAA Random_RevBonewalker 0x00 Level(9, Enemy, 1) [17,10] RandomMonsterTemplate 0x00 0x0 0x00 NoItems NoAI
	UNIT 0xAA Random_RevBonewalker 0x00 Level(9, Enemy, 1) [10,13] RandomMonsterTemplate 0x00 0x0 0x00 NoItems NoAI
	UNIT 0xAD Bonewalker_Bow 0x00 Level(8, Enemy, 1) [9,10] RandomizePosition 0x00 0x0 0x00 [BaseBow] NoAI
	UNIT 0xAA Random_RevBonewalker 0x00 Level(9, Enemy, 1) [17,6] RandomMonsterTemplate 0x00 0x0 0x00 NoItems NoAI
	UNIT 0xAA Random_RevBonewalker 0x00 Level(8, Enemy, 1) [3,17] MonsterTemplate 0x00 0x0 0x00 NoItems NoAI
	UNIT
	
DeliveranceUnits:
	UNIT Clive Cavalier 0x00 Level(6,Ally,1) [11,2] 0x00 0x00 0x0 0x00 [SteelLance, BaseLance] NoAI
	UNIT Forsyth Soldier 0x00 Level(4,Ally,1) [10,2] 0x00 0x00 0x0 0x00 [BaseLance,Forscythe] NoAI
	UNIT Python Archer 0x00 Level(3,Ally,1) [12,2] 0x00 0x00 0x0 0x00 [IronBow, BaseBow] NoAI
	UNIT

Hideout_BeginningScene:
	LOAD3 0x0 PlayerUnits //0x0 instead of 0x1 seems to be for allies on maps with prep screens
	ENUN
	
	SVAL 0x1 0x1
	CHECK_EVENTID gDeliveranceHideoutCompleted //has Clive joined?
	IFNE 0x5 0xC 0x1 //if true, don't show cutscene
		LOAD1 0x1 EnemyUnits
		ENUN
		LOAD1 0x1 EnemyUnits2
		ENUN
		MUSC 0x4A
		SetBackground(0x11)
		TEXTSTART
		TEXTSHOW DeliveranceHideout_Opening_1
		TEXTEND
		FADI 60
		REMA
		GOTO 0x4
	LABEL 0x5
		SVAL 0x2 0x0 //Number of skirmish enemies to load
		CALL EnemyRandomizerHelper
		LOAD1 0x1 EnemyUnitsReplay
		ENUN
	LABEL 0x4
	GotoPrepScreen
	NoFade
	ENDA
	
ChestEvent:
	SVAL 0x1 0x1
	CHECK_EVENTID gDeliveranceHideoutCompleted //has Clive joined?
	IFNE 0x2 0xC 0x1 //if true, don't give speedwing
		SVAL 0x3 Speedwings
		GIVEITEMTO 0xFFFF
		GOTO 0x2
	LABEL 0x2
	NoFade
	ENDA

Hideout_EndingScene:
	EVBIT_MODIFY 0x1
	SVAL 0x1 0x1
	CHECK_EVENTID gDeliveranceHideoutCompleted //has Clive joined?
	IFNE 0x1 0xC 0x1 //if true, skip cutscene
		EVBIT_MODIFY 0x0
		CALL UnifiedBEXPCaller
		FADI 10
		EVBIT_MODIFY 0x1
		CLEA 0x0
		CLEE 0x0
		MUSC 0x31
		LOAD1 0x1 DeliveranceUnits
		ENUN
		EVBIT_MODIFY 0x0
		CAM1 [12, 6]
		FADU 10
		
		EVBIT_MODIFY 0x1
		CHECK_ALIVE 0x2D
		BEQ 0x5 0xC 0x0
			LOAD2 0x1 CutsceneAlliesFernand
			ENUN
			CHECK_CLASS 0x2D
			SVAL 0x2 0x07 //Paladin
			BEQ 0x8 0xC 0x2
			SVAL 0x2 0x35 //GoldKnight
			BEQ 0x8 0xC 0x2
			GOTO 0x6
				LABEL 0x8
				ENUT gPlayerPromotedFernand
				GOTO 0x6
		LABEL 0x5 //if dead, reload him
			LOAD2 0x1 CutsceneAlliesNoFernand
			ENUN
			LOAD1 0x1 CutsceneFernand 
			ENUN
		LABEL 0x6
		EVBIT_MODIFY 0x0
		
		MOVE 0x10 Clair [11,3]
		MOVE 0x10 Alm [10,3]
		MOVE 0x10 Lukas [12, 3]
		MOVE 0x10 0x2D [11,4]
		ENUN
		
		FADI 10
		SetBackground(0x2E)
		Text(DeliveranceHideout_PostBattle_1)
		
		SVAL 0x1 0x1
		CHECK_ALIVE Gray 
		BEQ 0x7 0xC 0x0 //if not, goto NoFaye
		CHECK_ALIVE Tobin 
		BEQ 0x7 0xC 0x0 //if not, goto NoFaye
			ClearBackgroundSmooth
			MOVE 0x8 Tobin [6, 4]
			MOVE 0x8 Gray [6, 5]
			ENUN
			FlashCursor(6, 5, 60)
			
			TEXTSTART
			TEXTSHOW DeliveranceHideout_GrayTobin
			TEXTEND
			REMA
			STAL 30
			FlashCursor(Clive, 60)
			SetBackground(0x2E)
		LABEL 0x7
		
		//alm WHAT in hideout - village tension music, keep going until fernand cutscene
		//start sad music once fernand says "lost my taste for this long ago"
		
		TEXTSTART 
		TEXTSHOW DeliveranceHideout_PostBattle_2
		TEXTEND
		MUSC 0x26
		TEXTCONT
		TEXTEND
		MUSC 0x27
		TEXTCONT
		TEXTEND
		REMA
		ClearBackgroundSmooth
		MUSC 0x7FFF
		
		MOVE 0x10 0x2D [11,3]
		MOVE 0x5 Clair [11,4]
		ENUN
		STAL 10
		MOVE 0x10 0x2D [11,2]
		STAL 5
		MOVE 0x10 Clive [11,1]
		ENUN
		STAL 30
		FADI 0x5
		
		MUSC 0x28
		//DeliveranceHideout_CGText

		SETVAL 0x2 (CliveFernandCG)
		REMOVEPORTRAITS
		BACG 0xFFFF
		STAL 60
		_0x1A22
		TEXTSHOW DeliveranceHideout_CGText_1
		TEXTEND
		_0x1A22
		TEXTSHOW DeliveranceHideout_CGText_2
		TEXTEND
		_0x1A22
		TEXTSHOW DeliveranceHideout_CGText_3
		TEXTEND
		FADI 5
		REMA
		
		SetBackground(0x2E)
		Text(DeliveranceHideout_FernandClive)
		ClearBackgroundSmooth
		
		MUSC 0x32
		//Make Fernand green and move him off the map
		CUSN 0x2D
		STAL 20
		MOVE 0x10 0x2D [20,6]
		ENUN
		DISA 0x2D
		MOVE 0x10 Clive [11,2]
		ENUN
		
		//DeliveranceHideout_AfterCGText
		FADI 10
		SetBackground(0x2E)
		TEXTSTART 
		TEXTSHOW DeliveranceHideout_AfterCGText
		TEXTEND
		CharacterJoinedNotification(Notif_ForsythPythonJoin)
		CharacterJoinedNotification(Notif_CliveJoin)
		REMA
		
		EVBIT_MODIFY 0x1
		ENUT gDeliveranceHideoutCompleted //set a flag for clive joining
		SVAL 0x1 0x1
		CHECK_EVENTID gPlayerPromotedFernand //has the gaiden condition been met? joined?
		BNE 0x3 0xC 0x1 
			STAL 20
			SetBackground(0x2E)
			SETVAL 0xB 0xFFFFFFFF
			TUTORIALTEXTBOXSTART
			TEXTSHOW2 DeliveranceHideout_AcceptSidequest
			TEXTEND
			REMA
			STARTFADE
			FADECOLORS 0x2000 0x8 0x80 0x80 0x80
			SVAL 0x1 0x1
			//"Accept side quest?"
			//yes = 1 no = 2
			BNE 0x2 0xC 0x1
				MNC2 0x3D
			//if yes, MNC2 0x3D
			ENDA
	LABEL 0x1
		SVAL 0x3 0x3
		ASMC 0x85C65
		CALL UnifiedBEXPCaller
		EVBIT_MODIFY 0x0
	LABEL 0x3
		STARTFADE
		FADECOLORS 0x2000 0x8 0x80 0x80 0x80
	LABEL 0x2
		SVAL 0x2 0x3F
		MNCH 0xFFFF
	ENDA

#ifndef InlineEvents
  MESSAGE Events end at offset currentOffset
#endif
