//Made by CrazyColorz5 of Fire Emblem Universe
//Based off of Camtech75's Skeleton
#include EAstdlib.event

EventPointerTable(plCh1_3_Event, Ch1_3_ThisChapter)

Ch1_3_ThisChapter:
	POIN Ch1_3_TurnBasedEvents
	POIN Ch1_3_CharacterBasedEvents
	POIN Ch1_3_LocationBasedEvents
	POIN Ch1_3_MiscBasedEvents
	POIN Ch1_3_Dunno Ch1_3_Dunno Ch1_3_Dunno
	POIN Ch1_3_Tutorial
	POIN Ch1_3_Ballista1 Ch1_3_Ballista3
	POIN Ch1_3_Units1 Ch1_3_Units1
	POIN $0 $0 $0 $0 $0 $0
	POIN Ch1_3_BeginningScene Ch1_3_EndingScene

Ch1_3_TurnBasedEvents:
	TURN 0x0 WaveFirst[2,0] 0x0
	TURN 0x0 WaveBottom1[3,0] 0x0
	TURN 0x0 WaveSecond[4,0] 0x0
	TURN 0x0 WaveThird[5,0] 0x0
	TURN 0x0 WaveFourth[6,0] 0x0
	TurnEventPlayer(0x0, Ch1_3_EndingScene, 8)
	OpeningTurnEvent(ChestEvent)
	TURN

ChestEvent:
	MUSIC SongID_MarchToDeliverance
	MUSIC_QUIET_T
	CAMERA_CENTERED Alm
	STAL 30
	FlashCursor(12, 8, 60)
	Text(ChestText_1_3)
	STAL 30
	FlashCursor(8, 2, 60)
	TEXTSTART
	#ifdef LION_HEADS_ENABLED
		TEXTSHOW StatueFountainText_1_3
		TEXTEND
		STAL 30
		FlashCursor(4, 1, 60)
		FlashCursor(12,1, 60)
		TEXTCONT
		TEXTEND
	#else
		TEXTSHOW StatueText_1_3
		TEXTEND
	#endif
	TEXTCLEAR
	MUSIC_QUIET_F
	NoFade
	END

WaveFirst:
	LoadReinforcements(Reinforcements1)
	STAL 30
	LoadReinforcements(AcanthaJoins)
	ENUN
	CAMERA_CENTERED Acantha
	FlashCursor(Acantha, 60)
	ConvoEvent(AcanthaAppears_Text)
	END

WaveBottom1:
	ReinforcementEvent(ReinforcementsBottom)
	END

WaveSecond:
	ReinforcementEvent(Reinforcements2)
	END

WaveThird:
	ReinforcementEvent(Reinforcements3)
	END

WaveFourth:
	ReinforcementEvent(Reinforcements4)
	END

Ch1_3_CharacterBasedEvents:
	CharacterEvent(9, TalkToAcanthaWithSilque, Silque, Acantha)
	CharacterEvent(9, TalkToAcantha, Alm, Acantha)
	CHAR

Ch1_3_LocationBasedEvents:
	#ifdef __DEBUG__
		Seize(8,5)
	#endif
	Chest(IronSword, 12, 8)
	Door(12, 6)

	#ifdef LION_HEADS_ENABLED
		//Lion heads
		LionHead(gFountainDry_ThiefShrine,LionHeadThiefshrineSpeed, 4, 1)
		LionHead(gFountainDry_ThiefShrine,LionHeadThiefshrineHP, 12, 1)
	#endif
	LOCA

Ch1_3_MiscBasedEvents:
	CauseGameOverIfLordDies
	DefeatAll(Ch1_3_EndingScene)
	MournQuoteAFEVS
	AFEV

Ch1_3_Dunno:
	END_MAIN

Ch1_3_Tutorial:
	END_MAIN

Ch1_3_Ballista1:
	BLST

Ch1_3_Ballista3:
	BLST
	ALIGN 4

Ch1_3_Units1:
	UNIT Alm AlmFighter 0x00 Level(1, Ally, 1) [8,5] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI
	UNIT Silque Cleric 0x00 Level(1, Ally, 1) [8,4] 0x00 0x00 0x0 0x00 [Nosferatu, Vulnerary] NoAI
	UNIT Lukas Soldier 0x00 Level(1, Ally, 1) [8,1] 0x00 0x00 0x0 0x00 [BaseLance, Vulnerary] NoAI
	UNIT Tobin Villager 0x00 Level(1, Ally, 1) [6,4] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI
	UNIT Gray Villager 0x00 Level(1, Ally, 1) [10,4] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI
	UNIT Kliff Villager 0x00 Level(1, Ally, 1) [6,2] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI
	UNIT Faye Villager_F 0x00 Level(1, Ally, 1) [10,2] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI
	UNIT

SilqueFirst:
	UNIT Silque Cleric 0x00 Level(1, NPC, 1) [9,0] 0x00 0x00 0x1 SilqueReda [Nosferatu, Vulnerary] [AlternateStavesAndAttackWithoutMoving, DoNotMove, CautiousRecovery, DefaultAI4]
	UNIT

Ch1_3_CutsceneUnits:
	UNIT Alm AlmFighter 0x00 Level(1, Ally, 1) [0,4] 0x00 0x00 0x1 AlmReda [BaseSword, Vulnerary] NoAI
	UNIT

AcanthaJoins:
	UNIT Acantha Thief_F 0x00 Level(3, Enemy, 1) [8,0] 0x00 0x00 0x1 AcanthaReda [BaseSword, Lockpick] LootingThiefAI
	UNIT

EnemyUnits:
	UNIT BrigandBoss Brigand 0x00 Level(7, Enemy, 1) [6,11] DropItem 0x00 0x0 0x00 [BaseAxe, _200G] AttackInRangeAI 0x4 0x0
	UNIT Bandit_Axe Brigand BrigandBoss Level(5, Enemy, 1) [3,11] 0x00 0x00 0x0 0x00  [BaseAxe] NoAI
	UNIT Bandit_Bow Archer BrigandBoss Level(3, Enemy, 1) [16,9] 0x00 0x00 0x0 0x00  [BaseBow] NoAI
	UNIT Bandit_Axe Brigand BrigandBoss Level(5, Enemy, 1) [6,9] 0x00 0x00 0x0 0x00  [BaseAxe] NoAI
	UNIT Bandit_Axe Brigand BrigandBoss Level(4, Enemy, 1) [14,4] 0x00 0x00 0x0 0x00  [BaseAxe] NoAI
	UNIT Bandit_Axe Brigand BrigandBoss Level(6, Enemy, 1) [16,6] 0x00 0x00 0x0 0x00   [BaseAxe] NoAI
	UNIT Bandit_Axe Brigand BrigandBoss Level(5, Enemy, 1) [1,3] 0x00 0x00 0x0 0x00   [BaseAxe] NoAI
	UNIT Bandit_Bow Archer BrigandBoss Level(3, Enemy, 1) [5,10] 0x00 0x00 0x0 0x00  [BaseBow] NoAI
	UNIT Bandit_Axe Brigand BrigandBoss Level(5, Enemy, 1) [8,10] 0x00 0x00 0x0 0x00   [BaseAxe] NoAI
	UNIT

Reinforcements1:
	UNIT TreasureHunter Thief BrigandBoss Level(3, Enemy, 1) [16,5] DropItem 0x00 0x1 REDA16R5  [IronSword, Lockpick] LootingThiefAI
	UNIT

ReinforcementsBottom:
	UNIT Bandit_Axe Brigand BrigandBoss Level(5, Enemy, 1) [4,15] 0x00 0x00 0x1 REDA4R15  [BaseAxe] NoAI
	UNIT Bandit_Bow Archer BrigandBoss Level(4, Enemy, 1) [3,15] 0x00 0x00 0x1 REDA3R15  [BaseBow] NoAI
	UNIT

Reinforcements2:
	UNIT Bandit_Axe Brigand BrigandBoss Level(5, Enemy, 1) [0,3] 0x00 0x00 0x1 REDA0R3  [BaseAxe] NoAI
	UNIT Bandit_Axe Brigand BrigandBoss Level(4, Enemy, 1) [0,4] 0x00 0x00 0x1 REDA0R4  [BaseAxe] NoAI
	UNIT

Reinforcements3:
	UNIT Bandit_Axe Brigand BrigandBoss Level(5, Enemy, 1) [16,4] 0x00 0x00 0x1 REDA16R3  [BaseAxe] NoAI
	UNIT Bandit_Bow Archer BrigandBoss Level(4, Enemy, 1) [16,4] 0x00 0x00 0x1 REDA16R4  [BaseBow] NoAI
	UNIT

Reinforcements4:
	UNIT Bandit_Axe Brigand BrigandBoss Level(6, Enemy, 1) [9,0] 0x00 0x00 0x1 REDA9R0  [BaseAxe] NoAI
	UNIT

AlmReda:
REDA [8,5] NoFlags Walk_Fast 0x0 0x0
SilqueReda:
REDA [8,4] NoFlags Walk_Slow 0x0 0x0
AcanthaReda:
REDA [8,1] NoFlags Walk_Fast 0x0 0x0

ThiefReda:
REDA [13,17] NoFlags Walk_Fast 0x0 0x0
Brigand1x1Reda:
REDA [6,21] NoFlags Walk_Fast 0x0 0x0
Brigand1x2Reda:
REDA [7,20] NoFlags Walk_Fast 0x0 0x0
Brigand2x1Reda:
REDA [16,3] NoFlags Walk_Fast 0x0 0x0
Brigand2x2Reda:
REDA [15,4] NoFlags Walk_Fast 0x0 0x0

Ch1_3_BeginningScene:
	MUSIC SongID_FromTheDarkness
	LOAD 0x1 EnemyUnits
	ENUN
	FADEOUT 0x5

	//CAMERA_CENTERED [7,5]

	LOAD 0x1 SilqueFirst
	ENUN
	STAL 30
	FlashCursor(Silque, 60)
	SetBackground(Shrine_2)
	Text(Ch1_3_OpeningScene_2)
	EndConvo
	STAL 20

	LOAD_CUTSCENE Ch1_3_CutsceneUnits //0x0 instead of 0x1 seems to be for allies on maps with prep screens
	ENUN
	STAL 30
	CAMERA_CENTERED BrigandBoss
	FlashCursor(BrigandBoss, 60)
	STAL 30
	Text(Ch1_3_OpeningScene_1)
	STAL 20
	CAMERA_CENTERED Silque
	FlashCursor(Silque, 60)
	Text(Ch1_3_OpeningScene_3)
	STAL 30
	FACTION_BLUE Silque
	FADEIN 5
	STAL 5
	REMOVEALL_BLUE
	LOAD_DEPLOYED 0x0 Ch1_3_Units1
	GotoPrepScreen
	END


TalkToSilque:
	MUSIC_REPLACE SongID_Recruitment
	STAL 33
	Text(RecruitSilque_Text)
	FACTION_BLUE Silque
	STAL 20
	NoFade
	MUSIC_RESTORE 0x05
	END

TalkToSilqueWithAcantha:
	MUSIC_REPLACE SongID_Recruitment
	STAL 33
	Text(RecruitSilque_Acantha_Text)
	FACTION_BLUE Silque
	STAL 20
	NoFade
	MUSIC_RESTORE 0x05
	END

TalkToAcantha:
	MUSIC_REPLACE SongID_Recruitment
	STAL 33
	Text(RecruitAcantha_Text)
	FACTION_BLUE Acantha
	STAL 20
	CALL $591F40
	CharacterJoinedNotification(Notif_AcanthaJoin)
	NoFade
	MUSIC_RESTORE 0x05
	END

TalkToAcanthaWithSilque:
	MUSIC_REPLACE SongID_Recruitment
	STAL 33
	Text(RecruitAcantha_Silque_Text)
	FACTION_BLUE Acantha
	STAL 20
	CALL $591F40
	CharacterJoinedNotification(Notif_AcanthaJoin)
	NoFade
	MUSIC_RESTORE 0x05
	END

Ch1_3_EndingScene:
	CONFIG_SKIP 0x1
	FLAG_T gThiefShrineCompleted
	CONFIG_SKIP 0x0
	CALL EndMusicAndMMS
	CALL UnifiedBEXPCaller
	FADEIN 10
	STAL 5
	MUSIC SongID_Victory
	SetBackground(Shrine_2)
	TEXTSTART
	TEXTSHOW Ch1_3_PostBattle
	TEXTEND
	MUSIC SongID_Silence
	FADEIN_WHITE 5
	STAL 5
	FADEOUT_WHITE 5
	MUSIC SongID_Legacy
	TEXTCONT
	TEXTEND
	TEXTCLEAR
	TEXTMODE 1

	BACKGROUND_CHANGE TurnwheelCG 1 4
	TEXTMODE 1
	STAL 180
	BACKGROUND_CHANGE Shrine_2 1 4
	TEXTMODE 1

	TEXTSTART
	TEXTSHOW Ch1_3_PostBattle_AfterCG
	TEXTEND
	TEXTCLEAR
	CALL $591F40
	CharacterJoinedNotification(Notif_SilqueJoin)
	FADEIN 2
	GOTO_CHAPTER 0x03 //go to ch3
	WM_SETLORDLOCATION 0x1
	END

#ifndef InlineEvents
	MESSAGE Events end at offset currentOffset
#endif
