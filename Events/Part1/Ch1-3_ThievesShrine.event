//Made by CrazyColorz5 of Fire Emblem Universe
//Based off of Camtech75's Skeleton
#include EAstdlib.event

EventPointerTable(plCh1_3_Event, Ch1_3_ThisChapter)

Ch1_3_ThisChapter:
	POIN Ch1_3_TurnBasedEvents
	POIN Ch1_3_CharacterBasedEvents
	POIN Ch1_3_LocationBasedEvents
	POIN Ch1_3_MiscBasedEvents
	POIN Ch1_3_Dunno Ch1_3_Dunno Ch1_3_Dunno
	POIN Ch1_3_Tutorial
	POIN Ch1_3_Ballista1 Ch1_3_Ballista3
	POIN Ch1_3_Units1 Ch1_3_Units1
	POIN $0 $0 $0 $0 $0 $0
	POIN Ch1_3_BeginningScene Ch1_3_EndingScene

Ch1_3_TurnBasedEvents:
	TURN 0x0 WaveFirst[2,0] 0x0
	TURN 0x0 WaveBottom1[3,0] 0x0
	TURN 0x0 WaveSecond[4,0] 0x0
	TURN 0x0 WaveThird[5,0] 0x0
	TURN 0x0 WaveFourth[6,0] 0x0
	TurnEventPlayer(0x0, Ch1_3_EndingScene, 8)
	OpeningTurnEvent(ChestEvent)
	TURN

ChestEvent:
	MUSC SongID_MarchToDeliverance
	MUSI
	CAM1 Alm
	STAL 30
	FlashCursor(12, 8, 60)
	Text(ChestText_1_3)
	STAL 30
	FlashCursor(8, 2, 60)
	TEXTSTART
	#ifdef LION_HEADS_ENABLED
		TEXTSHOW StatueFountainText_1_3
		TEXTEND
		STAL 30
		FlashCursor(4, 1, 60)
		FlashCursor(12,1, 60)
		TEXTCONT
		TEXTEND
	#else
		TEXTSHOW StatueText_1_3
		TEXTEND
	#endif
	REMA
	MUNO
	NoFade
	ENDA

WaveFirst:
	LoadReinforcements(Reinforcements1)
	STAL 30
	LoadReinforcements(AcanthaJoins)
	ENUN
	CAM1 Acantha
	FlashCursor(Acantha, 60)
	ConvoEvent(AcanthaAppears_Text)
	ENDA

WaveBottom1:
	ReinforcementEvent(ReinforcementsBottom)
	ENDA

WaveSecond:
	ReinforcementEvent(Reinforcements2)
	ENDA

WaveThird:
	ReinforcementEvent(Reinforcements3)
	ENDA

WaveFourth:
	ReinforcementEvent(Reinforcements4)
	ENDA

Ch1_3_CharacterBasedEvents:
	CharacterEvent(9, TalkToAcanthaWithSilque, Silque, Acantha)
	CharacterEvent(9, TalkToAcantha, Alm, Acantha)
	CHAR

Ch1_3_LocationBasedEvents:
	#ifdef __DEBUG__
	  Seize(8,5)
	#endif
	Chest(IronSword, 12, 8)
	Door(12, 6)

	#ifdef LION_HEADS_ENABLED
		//Lion heads
		LionHead(gFountainDry_ThiefShrine,LionHeadThiefshrineSpeed, 4, 1)
		LionHead(gFountainDry_ThiefShrine,LionHeadThiefshrineHP, 12, 1)
	#endif
	LOCA

Ch1_3_MiscBasedEvents:
	CauseGameOverIfLordDies
	DefeatAll(Ch1_3_EndingScene)
	MournQuoteAFEVS
	AFEV

Ch1_3_Dunno:
	END_MAIN

Ch1_3_Tutorial:
	END_MAIN

Ch1_3_Ballista1:
	BLST

Ch1_3_Ballista3:
	BLST
	ALIGN 4

Ch1_3_Units1:
	UNIT Alm AlmFighter 0x00 Level(1, Ally, 1) [8,5] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI
	UNIT Silque Cleric 0x00 Level(1, Ally, 1) [8,4] 0x00 0x00 0x0 0x00 [Nosferatu, Vulnerary] NoAI
	UNIT Lukas Soldier 0x00 Level(1, Ally, 1) [8,1] 0x00 0x00 0x0 0x00 [BaseLance, Vulnerary] NoAI 
	UNIT Tobin Villager 0x00 Level(1, Ally, 1) [6,4] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI 
	UNIT Gray Villager 0x00 Level(1, Ally, 1) [10,4] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI 
	UNIT Kliff Villager 0x00 Level(1, Ally, 1) [6,2] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI
	UNIT Faye Villager_F 0x00 Level(1, Ally, 1) [10,2] 0x00 0x00 0x0 0x00 [BaseSword, Vulnerary] NoAI
	UNIT

SilqueFirst:
	UNIT Silque Cleric 0x00 Level(1, NPC, 1) [9,0] 0x00 0x00 0x1 SilqueReda [Nosferatu, Vulnerary] [AlternateStavesAndAttackWithoutMoving, DoNotMove, CautiousRecovery, DefaultAI4]
	UNIT
	
Ch1_3_CutsceneUnits:
	UNIT Alm AlmFighter 0x00 Level(1, Ally, 1) [0,4] 0x00 0x00 0x1 AlmReda [BaseSword, Vulnerary] NoAI 
	UNIT
	
AcanthaJoins:
	UNIT Acantha 0xE 0x00 Level(3, Enemy, 1) [8,0] 0x00 0x00 0x1 AcanthaReda [BaseSword, Lockpick] LootingThiefAI
	UNIT

EnemyUnits:
	UNIT 0x47 Brigand 0x00 Level(7, Enemy, 1) [6,11] DropItem 0x00 0x0 0x00 [BaseAxe, _200G] AttackInRangeAI 0x4 0x0
	UNIT 0x65 Brigand 0x47 Level(5, Enemy, 1) [3,11] 0x00 0x00 0x0 0x00  [BaseAxe] NoAI 
	UNIT 0x65 Archer 0x47 Level(3, Enemy, 1) [16,9] 0x00 0x00 0x0 0x00  [BaseBow] NoAI 
	UNIT 0x65 Brigand 0x47 Level(5, Enemy, 1) [6,9] 0x00 0x00 0x0 0x00  [BaseAxe] NoAI 
	UNIT 0x65 Brigand 0x47 Level(4, Enemy, 1) [14,4] 0x00 0x00 0x0 0x00  [BaseAxe] NoAI 
	UNIT 0x65 Brigand 0x47 Level(6, Enemy, 1) [16,6] 0x00 0x00 0x0 0x00   [BaseAxe] NoAI 
	UNIT 0x65 Brigand 0x47 Level(5, Enemy, 1) [1,3] 0x00 0x00 0x0 0x00   [BaseAxe] NoAI
	UNIT 0x65 Archer 0x47 Level(3, Enemy, 1) [5,10] 0x00 0x00 0x0 0x00  [BaseBow] NoAI	
	UNIT 0x65 Brigand 0x47 Level(5, Enemy, 1) [8,10] 0x00 0x00 0x0 0x00   [BaseAxe] NoAI
	UNIT

Reinforcements1:
	UNIT 0x65 Thief 0x47 Level(3, Enemy, 1) [16,5] DropItem 0x00 0x1 REDA16R5  [BaseSword, Lockpick] LootingThiefAI
	UNIT
	
ReinforcementsBottom: 
	UNIT 0x65 Brigand 0x47 Level(5, Enemy, 1) [4,15] 0x00 0x00 0x1 REDA4R15  [BaseAxe] NoAI
	UNIT 0x65 Archer 0x47 Level(4, Enemy, 1) [3,15] 0x00 0x00 0x1 REDA3R15  [BaseBow] NoAI
	UNIT

Reinforcements2:
	UNIT 0x65 Brigand 0x47 Level(5, Enemy, 1) [0,3] 0x00 0x00 0x1 REDA0R3  [BaseAxe] NoAI 
	UNIT 0x65 Brigand 0x47 Level(4, Enemy, 1) [0,4] 0x00 0x00 0x1 REDA0R4  [BaseAxe] NoAI
	UNIT

Reinforcements3:
	UNIT 0x65 Brigand 0x47 Level(5, Enemy, 1) [16,4] 0x00 0x00 0x1 REDA16R3  [BaseAxe] NoAI 
	UNIT 0x65 Archer 0x47 Level(4, Enemy, 1) [16,4] 0x00 0x00 0x1 REDA16R4  [BaseBow] NoAI
	UNIT
	
Reinforcements4:
	UNIT 0x65 Brigand 0x47 Level(6, Enemy, 1) [9,0] 0x00 0x00 0x1 REDA9R0  [BaseAxe] NoAI
	UNIT
	
AlmReda:
REDA [8,5] 0x20 0x0 0x0 0x0 0x0
SilqueReda:
REDA [8,4] 0x10 0x0 0x0 0x0 0x0
AcanthaReda:
REDA [8,1] 0x20 0x0 0x0 0x0 0x0

ThiefReda:
REDA [13,17] 0x20 0x0 0x0 0xFF 0xFF
Brigand1x1Reda:
REDA [6,21] 0x20 0x0 0x0 0xFF 0xFF
Brigand1x2Reda:
REDA [7,20] 0x20 0x0 0x0 0xFF 0xFF
Brigand2x1Reda:
REDA [16,3] 0x20 0x0 0x0 0xFF 0xFF
Brigand2x2Reda:
REDA [15,4] 0x20 0x0 0x0 0xFF 0xFF

Ch1_3_BeginningScene:
	MUSC SongID_FromTheDarkness
	LOAD1 0x1 EnemyUnits
	ENUN
	FADU 0x5
	
	//CAM1 [7,5]

	LOAD1 0x1 SilqueFirst
	ENUN
	STAL 30
	FlashCursor(Silque, 60)
	SetBackground(0x2E)
	Text(Ch1_3_OpeningScene_2)
	EndConvo
	STAL 20
	
	LOAD2 0x0 Ch1_3_CutsceneUnits //0x0 instead of 0x1 seems to be for allies on maps with prep screens
	ENUN
	STAL 30
	CAM1 0x47
	FlashCursor(0x47, 60)
	STAL 30
	Text(Ch1_3_OpeningScene_1)
	STAL 20
	CAM1 Silque
	FlashCursor(Silque, 60)
	Text(Ch1_3_OpeningScene_3)
	STAL 30
	CUSA Silque
	FADI 0x8
	CLEA 0x0
	LOAD3 0x0 Ch1_3_Units1
	GotoPrepScreen
	ENDA


TalkToSilque:
	MUSS 0x30
	STAL 33
	Text(RecruitSilque_Text)
	CUSA Silque
	STAL 20
	NoFade
	MURE 0x05
	ENDA

TalkToSilqueWithAcantha:
	MUSS 0x30
	STAL 33
	Text(RecruitSilque_Acantha_Text)
	CUSA Silque
	STAL 20
	NoFade
	MURE 0x05
	ENDA

TalkToAcantha:
	MUSS 0x30
	STAL 33
	Text(RecruitAcantha_Text)
	CUSA Acantha
	STAL 20
	CharacterJoinedNotification(Notif_AcanthaJoin)
	NoFade
	MURE 0x05
	ENDA

TalkToAcanthaWithSilque:
	MUSS 0x30
	STAL 33
	Text(RecruitAcantha_Silque_Text)
	CUSA Acantha
	STAL 20
	CharacterJoinedNotification(Notif_AcanthaJoin)
	NoFade
	MURE 0x05
	ENDA

Ch1_3_EndingScene:
	EVBIT_MODIFY 0x1
	ENUT gThiefShrineCompleted
	EVBIT_MODIFY 0x0
	CALL UnifiedBEXPCaller
	FADI 10
	MUSC SongID_Victory
	SetBackground(0x2E)
	TEXTSTART 
	TEXTSHOW Ch1_3_PostBattle
	TEXTEND
	MUSC SongID_Silence
	FAWI 5
	STAL 5
	FAWU 5
	MUSC SongID_Legacy
	TEXTCONT
	TEXTEND
	REMA
	REMOVEPORTRAITS
	
	_0x2141 TurnwheelCG 0x1 0x4
	REMOVEPORTRAITS
	STAL 180
	_0x2141 0x2E 0x1 0x4
	REMOVEPORTRAITS

	TEXTSTART 
	TEXTSHOW Ch1_3_PostBattle_AfterCG
	TEXTEND
	REMA
	CharacterJoinedNotification(Notif_SilqueJoin)
	STARTFADE
	FADECOLORS 0x2000 0x8 0x80 0x80 0x80
	MNCH 0x03 //go to ch3
	_0xA640 0x0 0x0 0x1
	ENDA

#ifndef InlineEvents
  MESSAGE Events end at offset currentOffset
#endif