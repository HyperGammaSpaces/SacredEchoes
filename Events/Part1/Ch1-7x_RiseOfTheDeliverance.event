//Made by CrazyColorz5 of Fire Emblem Universe
//Based off of Camtech75's Skeleton
#include EAstdlib.event

EventPointerTable(plCh1_7x_Event, Ch1_7x_ThisChapter)

Ch1_7x_ThisChapter:
POIN Ch1_7x_TurnBasedEvents
POIN Ch1_7x_CharacterBasedEvents
POIN Ch1_7x_LocationBasedEvents
POIN Ch1_7x_MiscBasedEvents
POIN Ch1_7x_Dunno Ch1_7x_Dunno Ch1_7x_Dunno
POIN Ch1_7x_Tutorial
POIN TrapList1 TrapList2
POIN Ch1_7x_Units1 Ch1_7x_Units1
POIN $0 $0 $0 $0 $0 $0
POIN Ch1_7x_BeginningScene Ch1_7x_EndingScene

Ch1_7x_TurnBasedEvents:
	TurnEventPlayer(0x0,StairsSpawn,4)
	TurnEventPlayer(0x0,TopSpawn,6)
	TurnEventPlayer(0x0,TopSpawn,8)
	TurnEventPlayer(0x0,StairsSpawn,9)
	TurnEventPlayer(0x0,TopSpawn,10)
	TurnEventPlayer(0x0,TopSpawn,12)
	TurnEventPlayer(0x0,TopSpawn,14)
	TurnEventPlayer(0x0,TopSpawn,16)
	TURN
	
StairsSpawn:
	LoadReinforcements(Reinforcements_Right)
	ReinforcementEvent(Reinforcements_Left)
	
TopSpawn:
	ReinforcementEvent(Reinforcements_Top)
	
ArriveAtExitCheck:
	CHECK_FACTION 0xFFFF
	BNE 1 rC r0 // branch label 1 if not ally
		FLAG_T 0x3
		GOTO 2
	LABEL 1
		FLAG_F 0x8
		LABEL 2
	NoFade
	END

Ch1_7x_CharacterBasedEvents:
	CHAR

Ch1_7x_LocationBasedEvents:
	#ifdef __DEBUG__
		Seize(10,11)
	#endif
	Arrive(11,20)
	Arrive(12,20)
	Arrive(13,20)
	LOCA

Ch1_7x_MiscBasedEvents:
	CauseGameOverIfLordDies
	DefeatAll(Ch1_7x_EndingScene)
	MournQuoteAFEVS
	//AREA 0x8 ArriveAtExitCheck [11,20] [13,20]
	AFEV

Ch1_7x_Dunno:
	END_MAIN

Ch1_7x_Tutorial:
	END_MAIN

TrapList1:
	BlueSigil(11,20)
	BlueSigil(12,20)
	BlueSigil(13,20)
	BLST
	ALIGN 4

TrapList2:
	BLST
	ALIGN 4

Ch1_7x_Units1:
	UNIT Clive Cavalier 0x00 Level(6,Ally,1) [10,10] 0x00 0x00 0x0 0x00 [SteelLance, BaseLance] NoAI
	UNIT Forsyth Soldier 0x00 Level(4,Ally,1) [10,9] 0x00 0x00 0x0 0x00 [BaseLance,Forscythe] NoAI
	UNIT Python Archer 0x00 Level(3,Ally,1) [8,9] 0x00 0x00 0x0 0x00 [IronBow, BaseBow] NoAI
	UNIT

LoadFernandMathilda:
	UNIT FernandPlayable Cavalier 0x00 Level(4, Ally, 1) [9,9] 0x00 0x00 0x0 0x00 [BaseLance,Javelin,Vulnerary] NoAI
	UNIT Mathilda Paladin_F 0x00 Level(1, Ally, False) [10,11] 0x00 0x00 0x0 0x00 [BaseLance,SteelLance,HeavySpear] NoAI
	UNIT

EnemyUnits:
//Left
	UNIT Zofia_Mage Mage Zopyrus Level(3, Enemy, True) [4,14] 0x00 0x00 0x1 REDA7R4 [Fire] NoAI
	UNIT Zofia_Archer Archer Zopyrus Level(3, Enemy, True) [4,14] 0x00 0x00 0x1 REDA9R14 [IronBow] NoAI
	UNIT Zofia_Soldier Soldier Zopyrus Level(3, Enemy, True) [4,14] DropItem 0x00 0x1 REDA2R8 [HeavySpear] NoAI
	UNIT Zofia_Soldier Soldier Zopyrus Level(3, Enemy, True) [4,14] 0x00 0x00 0x1 REDA3R5 [BaseLance] NoAI
	UNIT Zofia_Soldier Soldier Zopyrus Level(3, Enemy, True) [4,14] DropItem 0x00 0x1 REDA3R11 [IronLance,Vulnerary] ChargeAI
	UNIT Zofia_Merc Mercenary Zopyrus Level(3, Enemy, True) [4,14] DropItem 0x00 0x1 REDA6R14 [IronSword,Ambrosia] NoAI
//Right
	UNIT Zofia_Merc Mercenary Zopyrus Level(3, Enemy, True) [20,14] 0x00 0x00 0x1 REDA15R4 [BaseSword] NoAI
	UNIT Zofia_Soldier Soldier Zopyrus Level(3, Enemy, True) [20,14] DropItem 0x00 0x1 REDA19R4 [IronLance,Soma] NoAI
	UNIT Zofia_Archer Archer Zopyrus Level(3, Enemy, True) [20,14] DropItem 0x00 0x1 REDA15R14 [IronBow,SteelBow] NoAI
	UNIT Zofia_Mage Mage Zopyrus Level(3, Enemy, True) [20,14] 0x00 0x00 0x1 REDA21R10 [Fire] NoAI
	UNIT Zofia_Soldier Soldier Zopyrus Level(3, Enemy, True) [20,14] 0x00 0x00 0x1 REDA18R14 [BaseLance] NoAI
	UNIT Zofia_Soldier Soldier Zopyrus Level(3, Enemy, True) [20,14] DropItem 0x00 0x1 REDA22R8 [IronLance,Vulnerary] ChargeAI
//Armors
	UNIT Zofia_Soldier Knight Zopyrus Level(6, Enemy, True) [12,0] DropItem 0x00 0x1 REDA12R5 [KillerAxe,Rhomphaia] NeverMoveAI
	UNIT Zofia_Soldier Knight Zopyrus Level(6, Enemy, True) [20,14] DropItem 0x00 0x1 REDA12R16 [KillerAxe, SilverShield] NeverMoveAI
	UNIT

Enemy_Envoy:
	UNIT Zopyrus Cavalier	0x00 Level(8, NPC, False) [0,20]	DropItem 0x00 0x1 REDA14R10 [SteelLance, IronShield] DelayedRushAI
	UNIT Zofia_Cav Cavalier	Zopyrus Level(3, NPC, False) [0,20]	0x00 0x00 0x1 REDA15R9 [IronLance] DelayedRushAI
	UNIT Zofia_Cav Cavalier	Zopyrus Level(3, NPC, False) [0,20]	0x00 0x00 0x1 REDA15R11 [IronLance] DelayedRushAI
	UNIT

Reinforcements_Left:
	UNIT Zofia_Archer Archer Zopyrus Level(3, Enemy, True) [4,14] 0x00 0x00 0x1 REDA5R13 [BaseBow] NoAI
	UNIT Zofia_Merc Mercenary Zopyrus Level(3, Enemy, True) [4,14] 0x00 0x00 0x1 REDA3R13 [BaseSword] NoAI
	UNIT Zofia_Soldier Soldier Zopyrus Level(3, Enemy, True) [4,14] 0x00 0x00 0x1 REDA6R15 [Javelin] NoAI
	UNIT

Reinforcements_Right:
	UNIT Zofia_Archer Archer Zopyrus Level(3, Enemy, True) [20,14] 0x00 0x00 0x1 REDA19R13 [BaseBow] NoAI
	UNIT Zofia_Mage Mage Zopyrus Level(3, Enemy, True) [20,14] 0x00 0x00 0x1 REDA21R13 [Fire] NoAI
	UNIT Zofia_Soldier Soldier Zopyrus Level(3, Enemy, True) [20,14] 0x00 0x00 0x1 REDA18R15 [Javelin] NoAI
	UNIT

Reinforcements_Top:
	UNIT Zofia_Cav Cavalier Zopyrus Level(9, Enemy, True) [12,0] 0x00 0x00 0x1 REDA11R8 [BaseLance] NoAI
	UNIT Zofia_Cav Cavalier Zopyrus Level(9, Enemy, True) [12,0] DropItem 0x00 0x1 REDA12R8 [IronLance,Vulnerary] NoAI
	UNIT Zofia_Cav Cavalier Zopyrus Level(9, Enemy, True) [12,0] 0x00 0x00 0x1 REDA13R8 [BaseLance] NoAI
	UNIT

Ch1_7x_BeginningScene:
	MUSIC SongID_Env_Birds
	REMOVEALL_BLUE

	HIDEUNIT Alm
	HIDEUNIT Lukas
	HIDEUNIT Gray
	HIDEUNIT Tobin
	HIDEUNIT Kliff
	HIDEUNIT Faye
	HIDEUNIT Silque
	HIDEUNIT Clair
	HIDEUNIT Acantha

	LOAD_CUTSCENE Ch1_7x_Units1 //0x0 instead of 0x1 seems to be for allies on maps with prep screens
	ENUN
	LOAD 0x1 LoadFernandMathilda
	ENUN
	CAMERA_CENTERED Clive

	FADEOUT 2
	BROWNBOXTEXT Ch1_7x_BrownboxText [8,8]
	FlashCursor(Clive, 60)

	SetBackground(Fort)
	Text(Ch1_7x_OpeningText)
	ClearBackground

	STAL 20
	CAMERA_CENTERED [12, 18]
	EVBIT_T 0x9
	//Boss walks in
	LOAD 0x1 Enemy_Envoy
	ENUN
	EVBIT_F 0x9
	CAMERA_CENTERED Zopyrus
	STAL 20

	SetBackground(Fort)
	Text(Ch1_7x_BossIntro)
	ClearBackground

	//Enemy ambush
	MUSIC SongID_Tension
	CAMERA_CENTERED [12, 11]
	FACTION_RED Zopyrus
	SVAL 0xB Coords(15,9)
	FACTION_RED 0xFFFE
	SVAL 0xB Coords(15,11)
	FACTION_RED 0xFFFE
	LOAD 0x1 EnemyUnits
	ENUN

	Text(Ch1_7x_EnemiesAppear)
	NoFade
	END

Ch1_7x_EndingScene:
	CALL EndMusicAndMMS
	CALL UnifiedBEXPCaller
	FADEIN 5
	STAL 5
	RemoveItemFromUnit(FernandPlayable,BaseLance)
	RemoveItemFromUnit(Mathilda,BaseLance)
	DumpToConvoy(FernandPlayable)
	DumpToConvoy(Mathilda)

	SHOWUNIT Alm
	SHOWUNIT Lukas
	SHOWUNIT Gray
	SHOWUNIT Tobin
	SHOWUNIT Silque
	SHOWUNIT Clair
	SHOWUNIT Acantha

//special
	CHECK_FLAG gKliffRecruited
	BEQ 0xA 0xC 0x0
		SHOWUNIT Kliff
		GOTO 0xB
	LABEL 0xA
		HIDEUNIT Kliff
	LABEL 0xB
	CHECK_FLAG gFayeRecruited
	BEQ 0xC 0xC 0x0
		SHOWUNIT Faye
		GOTO 0xD
	LABEL 0xC
		HIDEUNIT Faye

	LABEL 0xD
	REMOVEUNIT FernandPlayable
	REMOVEUNIT Mathilda
	RevertUnitSupport(Clive,0)

//Uncomment when re-enabling

	MUSIC SongID_Uprising
	SetBackground(Fort_Outside)
	Text(Ch1_7x_EndingText)
	MUSCMID SongID_Silence
	FADEIN 2
	GOTO_CHAPTER 0x07 //go to ch7
END
