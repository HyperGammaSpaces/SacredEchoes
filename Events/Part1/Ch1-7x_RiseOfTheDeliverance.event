//Made by CrazyColorz5 of Fire Emblem Universe
//Based off of Camtech75's Skeleton
#include EAstdlib.event

EventPointerTable(plCh1_7x_Event, Ch1_7x_ThisChapter)

Ch1_7x_ThisChapter:
POIN Ch1_7x_TurnBasedEvents
POIN Ch1_7x_CharacterBasedEvents
POIN Ch1_7x_LocationBasedEvents
POIN Ch1_7x_MiscBasedEvents
POIN Ch1_7x_Dunno Ch1_7x_Dunno Ch1_7x_Dunno
POIN Ch1_7x_Tutorial
POIN TrapList1 TrapList2
POIN Ch1_7x_Units1 Ch1_7x_Units1
POIN $0 $0 $0 $0 $0 $0
POIN Ch1_7x_BeginningScene Ch1_7x_EndingScene

Ch1_7x_TurnBasedEvents:
	TurnEventPlayer(0x0,StairsSpawn,5)
	TURN
	
StairsSpawn:
	LoadReinforcements(Reinforcements_Right)
	ReinforcementEvent(Reinforcements_Left)
	
ArriveAtExitCheck:
	CHECK_ALLEGIANCE 0xFFFF
	BNE 1 0xC 0x0 // branch label 1 if not ally
		ENUT 0x3
		GOTO 2
	LABEL 1
		ENUF 0x8
		LABEL 2
	NoFade
	ENDA

Ch1_7x_CharacterBasedEvents:
	CHAR

Ch1_7x_LocationBasedEvents:
	#ifdef __DEBUG__
	  Seize(5,14)
	#endif
	LOCA

Ch1_7x_MiscBasedEvents:
	CauseGameOverIfLordDies
	DefeatAll(Ch1_7x_EndingScene)
	MournQuoteAFEVS
	AREA 0x8 ArriveAtExitCheck [11,22] [13,22]
	AFEV

Ch1_7x_Dunno:
	END_MAIN

Ch1_7x_Tutorial:
	END_MAIN

TrapList1:
	BlueSigil(11,22)
	BlueSigil(12,22)
	BlueSigil(13,22)
	BLST
	ALIGN 4

TrapList2:
	BLST
	ALIGN 4

Ch1_7x_Units1:
	UNIT Clive Cavalier 0x00 Level(6,Ally,1) [10,11] 0x00 0x00 0x0 0x00 [SteelLance, BaseLance] NoAI
	UNIT Forsyth Soldier 0x00 Level(4,Ally,1) [8,9] 0x00 0x00 0x0 0x00 [BaseLance,Forscythe] NoAI
	UNIT Python Archer 0x00 Level(3,Ally,1) [8,13] 0x00 0x00 0x0 0x00 [IronBow, BaseBow] NoAI
	UNIT

LoadFernandMathilda:
	UNIT FernandPlayable Cavalier 0x00 Level(4, Ally, 1) [9,10] 0x00 0x00 0x0 0x00 [BaseLance,Javelin,Vulnerary] NoAI
	UNIT Mathilda Paladin_F 0x00 Level(1, Ally, False) [9,12] 0x00 0x00 0x0 0x00 [BaseLance,SteelLance,HeavySpear] NoAI
	UNIT

EnemyUnits:
//Left
	UNIT 0x83 Mage Zopyrus Level(3, Enemy, True) [4,15] 0x00 0x00 0x1 REDA7R5 [Fire] NoAI
	UNIT 0x81 Archer Zopyrus Level(3, Enemy, True) [4,15] 0x00 0x00 0x1 REDA9R16 [BaseBow] NoAI
	UNIT 0x82 Soldier Zopyrus Level(3, Enemy, True) [4,15] 0x00 0x00 0x1 REDA2R9 [BaseLance] NoAI
	UNIT 0x82 Soldier Zopyrus Level(3, Enemy, True) [4,15] 0x00 0x00 0x1 REDA3R6 [BaseLance] NoAI
	UNIT 0x82 Soldier Zopyrus Level(3, Enemy, True) [4,15] 0x00 0x00 0x1 REDA3R12 [BaseLance] NoAI
	UNIT 0x84 Mercenary Zopyrus Level(3, Enemy, True) [4,15] 0x00 0x00 0x1 REDA5R15 [BaseSword] NoAI
//Right
	UNIT 0x84 Mercenary Zopyrus Level(3, Enemy, True) [20,15] 0x00 0x00 0x1 REDA15R5 [BaseSword] NoAI
	UNIT 0x82 Soldier Zopyrus Level(3, Enemy, True) [20,15] 0x00 0x00 0x1 REDA19R5 [BaseLance] NoAI
	UNIT 0x81 Archer Zopyrus Level(3, Enemy, True) [20,15] 0x00 0x00 0x1 REDA15R16 [BaseBow] NoAI
	UNIT 0x83 Mage Zopyrus Level(3, Enemy, True) [20,15] 0x00 0x00 0x1 REDA21R11 [Fire] NoAI
	UNIT 0x82 Soldier Zopyrus Level(3, Enemy, True) [20,15] 0x00 0x00 0x1 REDA19R15 [BaseLance] NoAI
	UNIT 0x82 Soldier Zopyrus Level(3, Enemy, True) [20,15] 0x00 0x00 0x1 REDA22R9 [BaseLance] NoAI
//Armors
	UNIT 0x82 Knight Zopyrus Level(3, Enemy, True) [12,0] 0x00 0x00 0x1 REDA12R6 [KillerAxe] NeverMoveAI
	UNIT 0x82 Knight Zopyrus Level(3, Enemy, True) [20,15] 0x00 0x00 0x1 REDA12R17 [KillerAxe] NeverMoveAI
	UNIT

Enemy_Envoy:
	UNIT Zopyrus Paladin	0x00 Level(8, NPC, False) [0,22]	DropItem 0x00 0x1 REDA14R11 [SteelLance, IronShield] NeverMoveAI
	UNIT 0x85 Cavalier	Zopyrus Level(3, NPC, False) [0,22]	0x00 0x00 0x1 REDA15R10 [IronLance] NeverMoveAI
	UNIT 0x85 Cavalier	Zopyrus Level(3, NPC, False) [0,22]	0x00 0x00 0x1 REDA15R12 [IronLance] NeverMoveAI
	UNIT

Reinforcements_Left:
	UNIT 0x81 Archer Zopyrus Level(3, Enemy, True) [4,15] 0x00 0x00 0x1 REDA5R14 [BaseBow] NoAI
	UNIT 0x84 Mercenary Zopyrus Level(3, Enemy, True) [4,15] 0x00 0x00 0x1 REDA3R14 [BaseSword] NoAI
	UNIT 0x82 Soldier Zopyrus Level(3, Enemy, True) [4,15] 0x00 0x00 0x1 REDA6R16 [BaseLance] NoAI
	UNIT
	
Reinforcements_Right:
	UNIT 0x81 Archer Zopyrus Level(3, Enemy, True) [20,15] 0x00 0x00 0x1 REDA19R14 [BaseBow] NoAI
	UNIT 0x83 Mage Zopyrus Level(3, Enemy, True) [20,15] 0x00 0x00 0x1 REDA21R14 [Fire] NoAI
	UNIT 0x82 Soldier Zopyrus Level(3, Enemy, True) [20,15] 0x00 0x00 0x1 REDA18R16 [BaseLance] NoAI
	UNIT

Ch1_7x_BeginningScene:
	MUSC 0x25
	CLEA 0x0

	REMU Alm
	REMU Lukas
	REMU Gray
	REMU Tobin
	REMU Kliff
	REMU Faye
	REMU Silque
	REMU Clair
	REMU Acantha
	
	LOAD2 0x0 Ch1_7x_Units1 //0x0 instead of 0x1 seems to be for allies on maps with prep screens
	ENUN
	LOAD1 0x0 LoadFernandMathilda
	ENUN
	CAM1 Clive
	
	FADU 5
	BROWNBOXTEXT Ch1_7x_BrownboxText [8,8]
	STAL 60
	
	SetBackground(0x26)
	Text(Ch1_7x_OpeningText)
	ClearBackground
	
	STAL 20
	CAM1 [12, 0]
	//Boss walks in
	LOAD1 0x1 Enemy_Envoy
	ENUN
	
	SetBackground(0x26)
	Text(Ch1_7x_BossIntro)
	ClearBackground

	//Enemy ambush
	CAM1 [12, 11]
	CUSE Zopyrus
	SVAL 0xB Coords(15,10)
	CUSE 0xFFFE
	SVAL 0xB Coords(15,12)
	CUSE 0xFFFE
	LOAD1 0x1 EnemyUnits
	ENUN

	Text(Ch1_7x_EnemiesAppear)
	NoFade
	ENDA

Ch1_7x_EndingScene:
	CALL UnifiedBEXPCaller
	FADI 5

	REVEAL Alm
	REVEAL Lukas
	REVEAL Gray
	REVEAL Tobin
	REVEAL Silque
	REVEAL Clair
	REVEAL Acantha

//special
	CHECK_EVENTID gKliffRecruited
	BEQ 0xA 0xC 0x0
		REVEAL Kliff
		GOTO 0xB
	LABEL 0xA
		REMU Kliff
	LABEL 0xB
	CHECK_EVENTID gFayeRecruited
	BEQ 0xC 0xC 0x0
		REVEAL Faye
		GOTO 0xD
	LABEL 0xC
		REMU Faye

	LABEL 0xD
	DISA FernandPlayable
	DISA Mathilda

//Uncomment when re-enabling

	MUSC 0x26
	SetBackground(0x25)
	Text(Ch1_7x_EndingText)
	FADI 5
	MNCH 0x07 //go to ch7
ENDA
